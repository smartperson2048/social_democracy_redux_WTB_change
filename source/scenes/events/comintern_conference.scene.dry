title: 7th Comintern Congress
subtitle: The Communist International convenes in Moscow...
view-if: year == 1935 and month >= 7
max-visits: 1
tags: event
new-page: true

= 7th Comintern Congress

The "7th World Congress of the Communist International" has begun in Moscow, seeing representatives from Communist parties around the world convene to discuss policy and strategy going forward.

Previously, the Comintern adopted the "Third Period" thesis, positing an end to collaboration with "social fascists" to defend the position of the workers and instead the beginning of a revolutionary offensive against capitalism. Perhaps this will change with this congress.

- @third_period: The Third Period thesis is maintained.
- @popular_front: The Third Period thesis is abandoned in favor of the "Popular Front".
- @influence_popular_front: Lobby the congress with our spies to abandon the Third Period thesis.

@third_period
view-if: (kpd_party_leader != "Conciliators" and (communist_coalition*soviet_relation < 50) and not kpd_ally) or ((kpd_relation <= 40 and rfb_banned == 1) or kpd_relation <= 20)
on-arrival: workers_spd -= 4; workers_kpd += 3; unemployed_spd -= 6; unemployed_kpd += 6; kpd_relation -= 10; third_period = 1

With the threat from fascism deemed insufficient, the Third Period thesis is maintained by the Communist International. This will hurt any attempts of ours to ally the KPD, and weaken leftism worldwide.

@popular_front
choose-if: (kpd_party_leader == "Conciliators" or (communist_coalition*soviet_relation >= 50) or kpd_ally) and ((kpd_relation > 40 or rfb_banned != 1) and kpd_relation > 20)
unavailable_subtitle: There is insufficient support for a Popular Front.
on-arrival: third_period = 0; kpd_relation += 10; communist_coalition += 3

Inspired by the relative success of cooperation between the KPD and the SPD in Germany in halting the spread of fascism, and for the Soviets wanting a more flexible diplomatic doctrine in order to break out of isolation, the 7th Comintern Congress ends with the Third Period thesis being dropped in favor of a new theory, that of the "Popular Front", an alliance of left-wing and socialist groups to halt the spread of fascism and defend the gains of the working class.

@influence_popular_front
subtitle: -1 resource
view-if: ((kpd_party_leader != "Conciliators" and (communist_coalition*soviet_relation < 50) and not kpd_ally) or (kpd_relation <= 0 and rfb_banned == 1)) and (communist_seen >= 2 and (kpd_ally or kpd_party_leader == "Conciliators" or (communist_coalition*soviet_relation > 30) or kpd_relation >= 60))
choose-if: resources >= 1
unavailable_subtitle: -1 resource. We lack the resources needed.
on-arrival: resources -= 1; third_period = 0; kpd_relation += 10; communist_coalition += 3

With a little nudge through our spy network in the comintern and inspired by the relative success of cooperation between the KPD and the SPD in Germany in halting the spread of fascism, the 7th Comintern Congress ends with the Third Period thesis being dropped in favor of a new theory, that of the "Popular Front", an alliance of left-wing and socialist groups to halt the spread of fascism and defend the gains of the working class.