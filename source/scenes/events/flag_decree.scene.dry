title: The Flag Dispute
subtitle: A dispute over the colors of the German flag abroad arises
view-if: year == 1926 and month >= 5 and chancellor == "Luther"
new-page: true
max-visits: 1
tags: event

= The Flag Dispute

It has come to Chancellor Luther's attention that German expatriate groups abroad only accept the black-white-red colors of the Imperial flag, in conflict with the official black-red-gold colors of the foreign service. 

To ease over tensions with these expatriates, Luther is pushing for a revision of the flag law to make the black-white-red trade flag a secondary flag.

- @unacceptable: This is unacceptable.

@unacceptable

And so echo the Center and Democratic parties. Luther has caved, modifying the proposal so that it should only apply in consular institutions abroad. This, still, is too great of a concession to German nationalism, and the DDP is in agreement on this manner.

- @in_government: Defeat Luther in a vote of the cabinet.
- @vonc_luther: Call a motion of no confidence against Luther.

@in_government
view-if: spd_in_government and not spd_toleration
on-arrival: coalition_dissent += 1

A cabinet vote is called, and Luther is defeated. The matter is dropped, for now.

@vonc_luther
view-if: spd_toleration and not spd_in_government
go-to: hindenburg_vonc if president == "Hindenburg"; marx_vonc if president == "Marx"

@hindenburg_vonc
on-arrival: chancellor = "Gessler"; chancellor_party = "DDP"

The DNVP sides with Luther in the vote of no confidence, and it fails. However, the DDP's proposal for a motion of censure against Luther is successful; he responds by announcing his resignation, effectively meaning the same effect as a vote of no confidence.

Otto Gessler of the DDP has been appointed as chancellor while a new government is negotiated, with Hindenburg hoping that Gessler may successfully form a government.

- @vonc_gessler: We cannot support a government in the pockets of the Reichswehr.

@vonc_gessler

Of course, there is no way that we could support Gessler's assumption of chancellorship. Another alternative is Konrad Adenauer of the Center Party; he is unwilling to preside over a minority government, and seeks to include us.

- @grand_coalition_adenauer: A Grand Coalition is formed!
- @failed: Adenauer fails to form a government.

@grand_coalition_adenauer
subtitle: SPD + Z + BVP + DDP + DVP (61%)
choose-if: z_relation >= 50 and dvp_relation >= 50
unavailable-subtitle: SPD + Z + BVP + DDP + DVP (61%) - [? if z_relation < 50 : The Center Party is unwilling. ?][? if dvp_relation < 50 : The DVP is unwilling. ?]
on-arrival: left_dissent += 5; coalition_dissent += 1; in_grand_coalition = 1; spd_in_government = 1; leverage = spd_r; leverage -= 5; chancellor = "Adenauer"; chancellor_party = "Z"; workers_spd -= 2; unemployed_spd -= 3; old_middle_dvp -= 2; new_middle_dvp -= 2
go-to: election_1928.ministries

Surprisingly, negotiations are successful! A "Grand Coalition" has been formed around Konrad Adenauer, and tensions eased over for now.

@failed
subtitle: The disagreements between the parties are too great.
on-arrival: z_relation -= 5 if z_relation >= 50 and dvp_relation >= 50; dvp_relation -= 5 if z_relation >= 45 and dvp_relation >= 50

As expected, Adenauer is unable to form a government, either, due to his insistence on including us and disagreements with Stresemann, whom Adenauer views as too Prussian.

- @what_now: So who next?

@what_now
subtitle: Wilhelm Marx assumes the task of government formation.
on-arrival: chancellor = "Marx"; chancellor_party = "Z"

In the end, another minority government is formed, with Wilhelm Marx at its head. While the DVP[? if z_relation < 40 : and Center Party?] view this minority government as a stepping stone towards a majority one with the DNVP, the DDP[? if z_relation > 50 : and Center Party?] view it as a stepping stone towards a majority one with us.

Which it shall end up being remains to be seen. For the time being, this is the government that shall rule Germany.

- @root: Tolerable. For now.
#- @intolerable: Intolerable. We shall call a motion of no confidence.

# voncing marx: +10 reformist dissent, +10 left strength, +5 kpd, -10 z, -10 dvp, -5 ddp, DNVP shoots it down and is eventually added to gov

@marx_vonc
on-arrival: chancellor = "Gessler"; chancellor_party = "DDP"

The DNVP sides with Luther in the vote of no confidence, and it fails. However, the DDP's proposal for a motion of censure against Luther is successful; he responds by announcing his resignation, effectively meaning the same effect as a vote of no confidence.

With Luther refusing to serve as interim, Otto Gessler succeeds him as Vice Chancellor. However, he and President Marx are under no illusion that he would win our support for a minority government.

- @so_who: So who next?

@so_who
subtitle: Marx asks Adenauer to form a government

President Marx asks Konrad Adenauer to take on the task of government formation. Unwilling to preside over a temporary minority government as a stepping stone towards a majority one, he has immediately made overtures towards us, hoping to form a Grand Coalition.

- @grand_coalition_adenauer2: A Grand Coalition is formed!
- @failed2: Adenauer fails to form a government.

@grand_coalition_adenauer2
subtitle: SPD + Z + BVP + DDP + DVP (61%)
choose-if: z_relation >= 45 and dvp_relation >= 50 or (dvp_relation >= 45 and marx_relation >= 55)
unavailable-subtitle: SPD + Z + BVP + DDP + DVP (61%) - [? if z_relation < 45 : The Center Party is unwilling. ?][? if dvp_relation < 50 : The DVP is unwilling. ?]
on-arrival: left_dissent += 5; in_grand_coalition = 1; spd_in_government = 1; leverage = spd_r; leverage -= 5; chancellor = "Adenauer"; chancellor_party = "Z"; workers_spd -= 1; unemployed_spd -= 2; old_middle_dvp -= 2; new_middle_dvp -= 2
go-to: election_1928.ministries

Surprisingly, negotiations are successful! A "Grand Coalition" has been formed around Konrad Adenauer, and tensions eased over for now. This may not have been possible without the efforts of Marx to ease over tensions.

@failed2
subtitle: The disagreements between the parties are too great.
on-arrival: z_relation -= 5 if z_relation >= 45 and dvp_relation >= 50; dvp_relation -= 5 if z_relation >= 40 and dvp_relation >= 50; marx_relation -= 5 if z_relation >= 40 and dvp_relation >= 45

As expected, Adenauer is unable to form a government, either, due to his insistence on including us and disagreements with Stresemann, whom Adenauer views as too Prussian.

- @what_now2: Brüning is appointed chancellor.

@what_now2
subtitle: Z + BVP + DDP + DVP (35%)
on-arrival: chancellor = "Brüning"; chancellor_party = "Z"

In the end, another minority government is formed, with Heinrich Brüning at its head. While the DVP[? if z_relation < 40 : and Center Party?] view this minority government as a stepping stone towards a majority one with the DNVP, the DDP[? if z_relation > 50 : and Center Party?] view it as a stepping stone towards a majority one with us.

Which it shall end up being remains to be seen. For the time being, this is the government that shall rule Germany.

- @root: Tolerable. For now.
#- @intolerable: Intolerable. We shall call a motion of no confidence.
