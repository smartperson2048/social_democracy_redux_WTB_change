title: Civil War in Spain!
subtitle: A civil war has erupted in the Spanish Republic...
view-if: year == 1936 and month >= 7
max-visits: 1
tags: event
new-page: true
go-to: govt_menu if foreign_minister_party != "SPD" and spd_in_government and foreign_minister_party != "KPD"; foreign_menu if foreign_minister_party = "SPD"; non_govt_menu if not spd_in_government; kpd_menu if foreign_minister_party = "KPD"
on-arrival: spanish_civil_war = 1; republic_strength = 6; national_strength = 6; republic_strength -= 1 if third_period; republic_strength += 1 if z_relation >= 60; republic_strength += 1 if west_relation >= 5; national_strength += 1 if victory_republic; national_strength += 1 if not abysinnia; national_strength += 1 if reichswehr_loyalty <= 0.1; national_strength += 1 if nsdap_votes + dnvp_votes >= 35

= Spanish Civil War

A civil war has broken out in Spain after a failed coup attempt against the newly elected [? if third_period : left-leaning government. ?][? if not third_period : Popular Front government. ?] Workers' militias clash with rebellious armies across the nation as both sides attempt an increasingly unlikely quick victory over the other.

@govt_menu

There is a broad consensus in our party in favor of sending aid to the Republicans, though this is complicated by the [+ foreign_minister_party +] Foreign Ministry, which plans to declare and commit to neutrality, as well as the fact that sending aid would be in violation of the Treaty of Versailles. We can try and persuade or pressure them to at the very least not commit to the French-proposed non-intervention pact, or allow them to go ahead with their plans.

- @pressure_foreign: Pressure [+ foreign_minister_party +] into taking a different course of action.
- @allow_foreign: Allow the pact to be made.

@pressure_foreign
go-to: dvp_foreign if foreign_minister_party == "DVP"; z_foreign if foreign_minister_party == "Z"

@dvp_foreign
on-arrival: coalition_dissent += 1; dvp_relation -= 5

The DVP is very reluctant to change their stance. The most they are willing to accept is to postpone the issue without signing onto the non-intervention pact.

- @persuade_dvp: Persuade the DVP to postpone the issue.
- @collapse_govt: Unacceptable. We will bring down this government, and call for new elections.
- @allow_foreign: On second thought, we will back down.

@persuade_dvp
choose-if: dvp_relation >= 45
unavailable_subtitle: The DVP is unwilling to compromise in the slightest.
on-arrival: left_dissent += 3; new_middle_spd -= 1; republic_strength += 1 if not third_period

We persuade a very reluctant DVP to accept the compromise of postponing, though this is unlikely to work as a long term solution. Let's hope the Republicans can survive in the meantime even without our help.

@collapse_govt
on-arrival: spd_in_government = 0; in_grand_coalition = 0; in_weimar_coalition = 0; chancellor = "BrÃ¼ning"; chancellor_party = "Z"; next_election_time = time + 3 if next_election_time > time+3; next_election_month = month + 3 if next_election_time >= time+3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; spd_toleration = 0; left_strength += 6; pro_republic -= 4; reformist_dissent += 4

Rather than accept anything less than full aid to the Republicans, we bring down the government. Perhaps by the next election we will be able to find a majority in favor.

@z_foreign
on-arrival: coalition_dissent += 1; z_relation -= 4; dvp_relation -= 2

The Center Party is reluctant to change their stance, but we may be able to negotiate them into very reluctantly accepting aid to the Republicans, or a compromise of postponing the issue.

- @demand_z: Pressure Zentrum into accepting aid.
- @persuade_z: Persuade Zentrum into postponing the issue.
- @collapse_govt: Unacceptable. We will bring down this government, and call for new elections.
- @allow_foreign: On second thought, we will back down.

@demand_z
choose-if: z_party_leader == "Joos" and chancellor_party == "SPD" and z_relation >= 60
unavailable_subtitle: The Center Party is unwilling to cave to our demands.
on-arrival: coalition_dissent += 1; weimar_dissent += 3 if weimar_plan_adopted; budget -= 1; reichswehr_loyalty -= 0.05; spain_involved = 1; dvp_relation -= 5; z_relation -= 3; coup_progress += 1 if coup_progress >= 6; coup_progress += 1; left_dissent -= 4; west_relation -= 1; west_relation -= 1 if third_period; nationalism -= democratization; democratization += 1; pro_republic += 5; workers_spd += 2*(1-dissent); new_middle_spd += 2*(1-dissent); republic_strength += 1; republic_strength += 1 if not third_period

We manage to persuade a very reluctant Center Party into allowing aid to the Republicans, though at great damage to our coalition. Let's hope this is enough to guarantee them a victory.

@persuade_z
choose-if: z_relation >= 35
unavailable_subtitle: The Center Party is unwilling to compromise in the slightest.
on-arrival: left_dissent += 3; weimar_dissent += 1 if weimar_plan_adopted; new_middle_spd -= 1; republic_strength += 1 if not third_period; kpd_coalition_dissent += 1

We persuade the Center Party into accepting the compromise of postponing the issue of aid. Let's hope the Republicans can survive in the meantime even without our help.

@allow_foreign
on-arrival: non_intervention = 1; capital_strike_progress -= 0.5; coup_progress -= 0.5; west_relation += 1; left_dissent += 5; kpd_coalition_dissent += 2; kpd_relation -= 3; workers_spd -= 2; new_middle_spd -= 2; republic_strength -= 1; republic_strength += 1 if not third_period; national_strength -= 1 if abysinnia

We allow the Foreign Ministry to go ahead with its plans to agree to the French-proposed non-intervention pact, angering the left flank of our party[? if in_popular_front : and the communists ?]. Let's just hope that Germany's refusal to support the Republicans does not win the Nationalists the war.

@foreign_menu

Many in our party are calling for us to send military aid to the Republicans. However, such an action would be a direct violation of the Treaty of Versailles and may strain our relations with France.

[? if in_popular_front or in_left_front : The KPD supports sending aid to the communists [? if in_popular_front : though the Center Party is opposed. ?] ?][? if in_weimar_coalition: The Center Party is opposed to sending aid. ?][? if in_grand_coalition : The Center Party and the DVP are opposed to sending aid. ?]

- @aid: Send aid to the Republicans.
- @neutrality: We will remain neutral, at least for now.
- @nonintervention: Sign the French-sponsored non-intervention pact.

@aid
subtitle: -1 budget
choose-if: budget >= 1
unavailable_subtitle: -1 budget. We do not have the budget to spare, but we can always come back to this later.
on-arrival: budget -= 1; reichswehr_loyalty -= 0.05; spain_involved = 1; weimar_dissent += 3 if weimar_plan_adopted; coalition_dissent += 1; coalition_dissent += 1 if z_party_leader == "Kaas"; coalition_dissent += 1 if in_grand_coalition; dvp_relation -= 5; z_relation -= 3; coup_progress += 1 if coup_progress >= 5; coup_progress += 1; left_dissent -= 5; west_relation -= 1; west_relation -= 1 if third_period; nationalism -= democratization; democratization += 1; pro_republic += 5; workers_spd += 3*(1-dissent); new_middle_spd += 2*(1-dissent); republic_strength += 1; republic_strength += 1 if not third_period

We go ahead with our plans to send aid to the Spanish Republicans, upsetting our bourgeois coalition partners and France but pleasing the left flank of our party. Let's hope its enough to win their war and stop the further spread of fascism.

@neutrality
subtitle: We will not involve ourselves, not yet at least.
on-arrival: left_dissent += 3; new_middle_spd -= 1; kpd_coalition_dissent += 1; z_relation += 2; republic_strength += 1 if not third_period

Whether it be by choice or being unable, we do not send aid to the Republicans. This decision is well received by our bourgeois partners, but criticized by [? if in_popular_front or in_left_front : our communist coalition partners as well as ?] the Left of our party.

@nonintervention
subtitle: We will commit fully to neutrality.
on-arrival: nonintervention = 1; capital_strike_progress -= 1; coup_progress -= 1; west_relation += 1; left_dissent += 7; z_relation += 5; dvp_relation += 4; coalition_dissent -= 1; kpd_coalition_dissent += 2; kpd_relation -= 4; workers_spd -= 2; new_middle_spd -= 2; republic_strength -= 1; republic_strength += 1 if not third_period; national_strength -= 1 if abysinnia

Rather than involve ourselves, we sign the French proposed non-intervention pact, pledging neutrality in the Spanish Civil War. This move is highly criticized by the Left of our party and the communists, but is met with approval abroad and from our bourgeois allies. Let's just hope that our refusal to support the Republicans does not win the Nationalists the war.

@non_govt_menu
on-arrival: nonintervention = 1; spd_intervention = 1; west_relation += 1; republic_strength -= 1; republic_strength += 1 if not third_period; national_strength -= 1 if abysinnia

There is a broad consensus in our party in favor of sending aid to the Republicans despite the restrictions on sending military aid in the treaty of Versailles. The government however has pulled Germany into the French non-intervention pact.

- @nothing_to_do: There is nothing we can do.
- @break_toleration: We will break toleration and bring down the government.

@nothing_to_do
on-arrival: left_dissent += 5 if spd_toleration; new_middle_spd -= 1 if spd_toleration;

As we are not in government, there is nothing we can really do to prevent this. Let's hope that the Republicans can hold their own.

@break_toleration
subtitle: This is likely to result in new elections.
view-if: spd_toleration == 1
on-arrival: next_election_time = time+3 if next_election_time >= time+3; next_election_month = month + 3 if next_election_time >= time+3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; spd_in_government = 0; in_grand_coalition = 0; spd_toleration = 0; weimar_dissent += 1 if weimar_plan_adopted

We have definitively broken with the [+ chancellor +] government, and have voted against them in a vote of no confidence.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@kpd_menu

Many in our party are calling for us to send military aid to the Republicans. The Communists, whom we govern with and whom control the Foreign Ministry, plan to send military aid to Spain, as do the Soviets.

- @foreign_support: Yes, we support sending aid to the Republicans.
- @foreign_oppose: No, this must be stopped.

@foreign_support
on-arrival: budget -= 1; reichswehr_loyalty -= 0.05; spain_involved = 1; left_dissent -= 4; dvp_relation -= 4; z_relation -= 2; coup_progress += 1 if coup_progress >= 4; coup_progress += 1; west_relation -= 1; soviet_relation += 1; nationalism -= democratization; democratization += 1; pro_republic += 3; socialism += 2; workers_spd += 2*(1-dissent); new_middle_spd += 1*(1-dissent); workers_kpd += 2; republic_strength += 2

We support the KPD's sending of military aid to the Republicans, and allow it to take place. Republican forces are bolstered, though the aid disproportionately goes to the socialists, and most of all the PCE.

@foreign_oppose
on-arrival: kpd_coalition_dissent += 1; kpd_relation -= 5; left_dissent += 5; new_middle_spd -= 1; z_relation += 2; republic_strength += 1

We block the planned sending of aid to the Republicans, disappointing the Communists, as well as the Left flank of our party. 

# todo: may days in barcelona event

# todo: sending aid is actually secret...how does this effect french & coalition reactions?