title: May Day, 1929
new-page: true
subtitle: A Communist demonstration
tags: event
view-if: year = 1929 and month = 5 and in_right_coalition == 1
max-visits: 1

= May Day, 1929

Tensions run high in Berlin. The Communists are planning a demonstration to commemorate the 10th anniversary of the Spartacist Uprising, in defiance of a ban on public gatherings in Berlin made by the right-wing government, which fears the possibility of another violent uprising.

Many on our party's left-wing have called on us to join in the demonstration, but this would only increase the risk of violence and could make reconciliation with the right-wing parties impossible. What should we do?

- @join: We will join in the demonstration.
- @not: We will stay out of this.

@join
choose-if: kpd_relation >= 30
unavailable-subtitle: The KPD does not trust us enough.
on-arrival: kpd_relation += 20; volksmai = 1; z_relation -= 10; z_relation -= 5 if z_party_leader != "Joos"; dvp_relation -= 25; workers_spd += 3*(1-dissent); new_middle_spd -= 5; new_middle_spd += 3 if new_middle_conscious; old_middle_spd -= 5; unemployed_spd += 3*(1-dissent); workers_kpd += 3; unemployed_kpd += 3; workers_z -= 3; workers_dnvp -= 3; communist_coalition += 2; reformist_strength -= 10; reformist_dissent += 10; neorevisionist_dissent += 10; left_strength += 10; rfb_banned = 1; rfb_strength -= 100; strife += 2; socialism += 4
achievement: die_rote_fahne
face-image: img/blutmai.jpg

We join in the demonstrations, though most of the party right boycott and refuse to participate. The police are sent to disperse our demonstration and violence erupts. Reports say that the Berlin police killed dozens of demonstrators and innocent bystanders. We are already calling this incident *Blutmai*, "Bloody May".

This may end any attempt at reconciliation with the right, but the Communists approve of our action.


@not
on-arrival: kpd_relation -= 5; left_dissent += 5; workers_kpd += 3; unemployed_kpd += 3; rfb_banned = 1; rfb_strength -= 100; strife += 1; socialism -= 2

We do not participate in the demonstration, and it is a good thing that we didn't as it erupts into violence when the police are sent to disperse the Communists. Reports say that the Berlin police killed dozens of demonstrators and innocent bystanders. The Communists are already calling this incident *Blutmai*, "Bloody May".

What is our party's position?

- @dont: The party will not adopt a position on this tragedy.
- @pacifism: We condemn political violence on all sides, and this is another case of that.
- @pro_kpd: It is the fault of corrupt and reactionary elements within the police, which we will purge, and the government that directed them. 
- @pro_z: It is the fault of the Communists for refusing to follow the rule of law.

@dont

It is not our place to campaign on a tragedy that did not involve us.

@pacifism
on-arrival: workers_spd += 1*(1-dissent); new_middle_spd += 3*(1-dissent); old_middle_spd += 2*(1-dissent); pacifism += 1; dvp_relation -= 5; kpd_relation -= 5; left_dissent += 5; rb_militancy -= 0.05; sh_militancy -= 0.02; rfb_militancy -= 0.02

What happened is a tragedy of political violence, and we will use this as an opportunity to direct our campaigning towards pacifistic messaging and urge against violence on all sides of the political spectrum, regardless of affiliation. 

@pro_kpd
on-arrival: workers_z -= 3; workers_dnvp -= 3; workers_spd += 2*(1-dissent); old_middle_spd -= 2; new_middle_spd -= 1 if not new_middle_conscious; kpd_relation += 10; z_relation -= 5; dvp_relation -= 10; prussian_police_loyalty += 0.05; reformist_dissent += 5; socialism += 1

What happened is the fault of the police who suppressed the demonstration, and the right-wing government's fault for ordering it. We will use our power in the Prussian Interior Ministry to purge those responsible, while blaming the government for the incident in official party communications.

@pro_z
on-arrival: new_middle_spd += 2*(1-dissent); old_middle_spd += 3*(1-dissent); workers_spd -= 1; kpd_relation -= 10; z_relation += 10; dvp_relation += 5; left_dissent += 15

What happened is the fault of the Communists for defying the rule of law and holding the demonstration in spite of the ban. We will defend the actions of the government, and the Prussian police force which we control, in the incident.
