title: A United Front Against Fascism
new-page: true
subtitle: The KPD proposes to organize against the rising Nazi threat
tags: event
view-if: communist_coalition >= 3 and kpd_relation >= 50 and nazi_urgency >= 2 and left_strength >= 50 and black_brown_seen = 1 and not spd_toleration
max-visits: 1

= A United Front Against Fascism

As the bourgeois voters and industrialists flock to the Nazis following the recent instability and the republic, with Brüning and the Center Party even considering turning to the Nazis for support before negotiations fell apart, the KPD, with whom we have made mends with, proposes the formation of a "united front". [? if iron_front_formed : This would include the already existing Iron Front, but expanded to include KPD-affiliate organizations as well as a joint plan of action. ?]

What remains of the party's right-wing is highly opposed, but doing this would significantly bolster the strength of our forces on the left in case a confrontation with the fascists becomes inevitable. It may also instigate the reactionaries into preempting us, however.

- @form_uf: Form the United Front between the SPD and KPD!
- @literally_why_would_anyone_whos_at_this_point_in_the_game_choose_this_like_what: We'll have to turn the offer down.

@form_uf
on-arrival: antifa_formed = 1; rb_militancy += 0.03; rfb_militancy += 0.04; coup_progress += communist_coalition; workers_kpd += 2; workers_spd += 4*(1-dissent); unemployed_kpd += 3; unemployed_spd += 3*(1-dissent); new_middle_spd += 4*(1-dissent) if new_middle_conscious; workers_nsdap -= 2; new_middle_nsdap -= 2; unemployed_nsdap -= 2
go-to: uf_menu

We have formed a United Front, composed of the KPD and SPD's various affiliate organizations through which to fight the class war against the increasingly fascistic bourgeoisie.

@uf_menu

- @rally_resources: Launch an ambitious campaign through the United Front!
- @rally_left: Rally behind the Left Plan!
- @rally_iron_front: Rally the full force of the Iron Front behind the United Front!
- @rally_afa: Rally Siegfried Aufhäuser and the AfA-bund behind the United Front!
- @done: That is enough.

@rally_resources
max-visits: 1
choose-if: resources >= 1
subtitle: -1 resource
on-arrival: uf_strength += 2; workers_spd += 3*(1-dissent); rb_strength += 100*(1-dissent); rfb_strength += 20; unemployed_spd += 2*(1-dissent); unemployed_kpd += 1; new_middle_spd += 1*(1-dissent); workers_nsdap -= 3*(1-dissent); unemployed_nsdap -= 2*(1-dissent); new_middle_nsdap -= 2*(1-dissent);
go-to: uf_menu

We launch an ambitious campaign through the new organization, targeting the working class and attacking the bourgeois class. The Reichsbanner swells in membership, as does the RFB.

@rally_left
max-visits: 1
view-if: nationalization_adopted
on-arrival: uf_strength += 2; workers_spd += 3*(1-dissent); workers_kpd += 2; unemployed_kpd += 3; unemployed_spd += 2*(1-dissent); workers_nsdap -= 3*(1-dissent); unemployed_nsdap -= 2*(1-dissent); old_middle_nsdap += 3; rural_nsdap += 2; new_middle_nsdap += 2 if not new_middle_conscious
go-to: uf_menu

We rally behind the Left Plan together with the KPD.

@rally_iron_front
max-visits: 1
view-if: iron_front_formed
on-arrival: uf_strength += 2; workers_spd += 3*(1-dissent); new_middle_spd += 2*(1-dissent); unemployed_spd += 2*(1-dissent); new_middle_nsdap -= 2*(1-dissent); neorevisionist_strength += 5; neorevisionist_dissent -= 5; rb_strength += 50*(1-dissent);
go-to: uf_menu

We rally the Iron Front behind the United Front, increasing the SPD's strength compared to the KPD's as well as overall further bolstering the campaign.

@rally_afa
max-visits: 1
view-if: new_middle_conscious
on-arrival: uf_strength += 2; new_middle_spd += 5*(1-dissent); new_middle_nsdap -= 3*(1-dissent); workers_spd -= 1; workers_kpd += 1; unemployed_spd -= 2; unemployed_kpd += 2;

We rally the AfA-bund behind the United Front, taking advantage of the recent inclination of the new middle class to align with the rest of the proletariat to put an end to Nazi gains in the demographic. As a consequence, however, we have allowed the KPD to grow further in traditional working class groups.

@done
on-arrival: old_middle_nsdap += uf_strength; rural_nsdap += uf_strength/2; new_middle_nsdap += uf_strength/4 if not new_middle_conscious;

@literally_why_would_anyone_whos_at_this_point_in_the_game_choose_this_like_what
on-arrival: communist_coalition -= 1; kpd_relation -= 5; left_dissent += 15; reformist_strength += 10; reformist_dissent -= 20; sa_militancy += 0.02

We politely turn the offer down, so as to retain the party's organizational independence. The dominant Left is perplexed.