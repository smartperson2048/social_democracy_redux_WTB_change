title: Reichstag Elections
new-page: true
tags: event
priority: -1
view-if: ((year = next_election_year and month >= next_election_month) or (year > next_election_year)) and not supreme_soviet
on-arrival: bb_failed = 0; status_quo = 0; reichswehr_loyalty -= 0.2 if reichswehr_loyalty >= 0.4 and chancellor == "Groener"; old_economic_health = economic_health; hindenburg_angry = 0; unity_refused = 0; wirth_lead = 0; dvp_willing = 0; dvp_willing = 1 if ((dvp_relation+spd_votes+(pro_republic/6)) >= ((100-pro_republic)+capital_strike_progress+(coup_progress/2))); z_willing = 0; z_willing = 1 if z_relation >= 50 or (z_relation >= 25 and bourgeois_coalition < 50)
on-departure: month_actions += 1; in_election = 0; next_election_year = next_election_year + 4; n_elections += 1; next_election_time = time + 48; papen_chancellor_timer = 4;
set-jump: post_election_1928

# the low priority means that this event is only seen after all other events are resolved for the month.
# note: z_coalition = z_relation + (pro_republic/2); z_coalition *= 1.25 if z_party_leader == "Joos";

= Elections, [+ month : month +] [+ year +]

The Reichstag elections are to be held now.

- @election_algorithm: May we do our best...
- @cancel_elections
#- @martial_law

# secret noske path...

@cancel_elections
title: We cannot hold the elections now. Cancel the elections.
subtitle: This is an anti-democratic move that may backfire.
view-if: in_emergency_government == 1 and president == "Braun" and not presidential_powers

Since we are currently ruling as part of an emergency government, we may use quasi-constitutional trickery to significantly delay these elections, and continue governing via Article 48. This is likely to be extremely unpopular both with our base and with the other parties. However, this may be necessary to prevent the extremists from gaining power.

- @cancel_elections_2: Yes, do it.
- @election_algorithm: No, let's hold the elections.

@cancel_elections_2
on-departure: next_election_year = next_election_year + 1; next_election_time = time + 12; pro_republic -= 10; dvp_relation -= 8; z_relation -= 8; ddp_relation -= 10; kpd_relation -= 8; coup_progress += 2; workers_spd -= 4; new_middle_spd -= 5; old_middle_spd -= 5; rural_spd -= 5; catholics_spd -= 5; unemployed_spd -= 5; coup_progress += 2 if nsdap_normalized >= 0.3; coup_progress += 2 if nsdap_normalized + dnvp_normalized >= 0.5; sa_strength += 100 if coup_progress >= 7; sh_strength += 100 if coup_progress >= 7
achievement: ausnahmezustand

Fine. President [+ president +] has taken the unprecedented step of canceling these elections. New elections are scheduled for [+ next_election_month : month +] [+ next_election_year +].

Let us hope this does not backfire.

@post_election_1928
on-arrival: {!
// set old results
// election threshold constitutional reform
// banning parties
var new_party_sum = 0;
for (var party of Q.parties) {
    Q['old_' + party + '_r'] = Q[party + '_r'];
    if (Q.constitutional_reform > 0) {
        if (Q[party + '_votes'] < Q.electoral_threshold || (Q.electoral_threshold > 1 && party == 'other') || (Q[party + '_banned'] == 1)) {
            Q[party + '_r'] = 0;
        } else {
            Q[party + '_r'] = Q[party + '_votes'];
        }
        new_party_sum += Q[party + '_r'];
    } else {
        Q[party + '_r'] = Q[party + '_votes'];
    }
        // if we use decimals, then we might want to truncate the numbers
    if (Q.use_decimals) {
        Q[party + '_r_disp'] = Q[party+'_r'].toFixed(1);
    }  else {
            Q[party + '_r_disp'] = Q[party+'_r'];
    }
    Q['change_' + party + '_r'] = Q[party + '_r'] - Q['old_' + party + '_r'];
    var sign = Q['change_' + party + '_r'] > 0 ? '+'  : '';
    Q['str_change_' + party] = sign + Q['change_' + party + '_r'];//.toFixed(1);
}
// calculate new party sum if electoral reform
if (Q.electoral_threshold && Q.electoral_threshold > 0 || Q.party_bans) {
    for (var party of Q.parties) {
        if (Q[party + '_r'] > 0) {
            if (Q.use_decimals) {
                Q[party + '_r'] = Math.round(Q[party + '_r']*1000/new_party_sum)/10;
            } else {
                Q[party + '_r'] = Math.round(Q[party + '_r']*100/new_party_sum);
            }
        }
        if (Q.use_decimals) {
            Q[party + '_r_disp'] = Q[party+'_r'].toFixed(1);
        } else {
            Q[party + '_r_disp'] = Q[party+'_r'];
        }
        Q['change_' + party + '_r'] = Q[party + '_r'] - Q['old_' + party + '_r'];
        var sign = Q['change_' + party + '_r'] > 0 ? '+'  : '';
        Q['str_change_' + party] = sign + Q['change_' + party + '_r'];//.toFixed(1);
    }
}

// get change arrows and signs


// The largest party can only be the NSDAP or the SPD.
Q.largest_party = 'SPD';
if (Q.nsdap_r >= Q.spd_r && Q.nsdap_r >= Q.kpd_r) {
    Q.largest_party = 'NSDAP';
}

if (Q.kpd_r >= Q.spd_r && Q.kpd_r >= Q.nsdap_r) {
    Q.largest_party = 'KPD';
}
Q.z_minus_bvp_r = Q.z_r - 3;
// coalitions
Q.weimar_coalition = Q.spd_r + Q.ddp_r + Q.z_r - 3;
Q.grand_coalition = Q.spd_r + Q.ddp_r + Q.z_r + Q.dvp_r;
Q.bourgeois_coalition = Q.ddp_r + Q.z_r + Q.dvp_r + Q.other_r;
Q.center_right_coalition = Q.z_r + Q.ddp_r + Q.dvp_r + Q.other_r + Q.dnvp_r;
Q.right_coalition = Q.z_r + Q.dvp_r + Q.other_r + Q.dnvp_r;
Q.far_right_coalition = Q.dnvp_r + Q.nsdap_r;
Q.left_coalition = Q.spd_r + Q.kpd_r;
// this is an absurd concept lol
Q.popular_front_coalition = Q.spd_r + Q.kpd_r + Q.z_r + Q.ddp_r - 3;
Q.anti_democratic_coalition = Q.kpd_r + Q.nsdap_r + Q.dnvp_r;
// aka everyone except nazi, kpd, dnvp
Q.neo_weimar_coalition = Q.spd_r + Q.ddp_r + Q.z_r + Q.dvp_r + Q.other_r;
// Hitler + Right-wing parties (only comes into effect if you've alienated Z too badly)
Q.other_nazi = Math.round(Q.other_r*(1-(Q.pro_republic/100)));
Q.hitler_right_coalition = Q.z_r + Q.dnvp_r + Q.nsdap_r + Q.dvp_r + Q.other_nazi;
// Progressive coalition isn't going to be implemented as a distinct coalition but it's the coalition that lets you pass improved women's rights/gay rights
Q.progressive_coalition = Q.spd_r + Q.sapd_r + Q.kpd_r + Q.ddp_r;
// nationaal front
Q.national_front_r = Q.nsdap_r + Q.dnvp_r + Q.dvp_r + Q.other_r + 3;
if (Q.z_national) {
    Q.national_front_r += Q.z_minus_bvp_r;
}

Q.leverage = Q.spd_r;

Q.grand_coalition_failed = 0;
// Reset government and ministers... Based on Brüning's 1930 govt
Q.coalition_dissent = 0;
Q.kpd_coalition_dissent = 0;
Q.spd_in_government = 0;
Q.in_weimar_coalition = 0;
Q.in_grand_coalition = 0;
Q.in_left_front = 0;
Q.in_popular_front = 0;
Q.in_emergency_government = 0;
Q.in_minority_government = 0;
Q.in_unity_government = 0;
Q.spd_toleration = 0;
Q.spd_caretaker = 0;
Q.foreign_minister_party = 'DVP';
Q.interior_minister_party = 'Z';
Q.justice_minister_party = 'I';
Q.labor_minister_party = 'Z';
Q.reichswehr_minister_party = 'I';
Q.economic_minister_party = 'DVP';
Q.finance_minister_party = 'DVP';
Q.agriculture_minister_party = 'Z';

// reset KPD goals
Q.kpd_goals_seen = 0;
Q.kpd_ultimatum_seen = 0;
Q.welfare_goal = 0;
Q.agriculture_goal = 0;
Q.economy_goal = 0;
Q.finance_goal = 0;
Q.labor_goal = 0;
Q.foreign_goal = 0;
Q.interior_goal = 0;
Q.reichswehr_goal = 0;

Q.welfare_goal_completed = 0;
Q.agriculture_goal_completed = 0;
Q.economy_goal_completed = 0;
Q.finance_goal_completed = 0;
Q.labor_goal_completed = 0;
Q.foreign_goal_completed = 0;
Q.interior_goal_completed = 0;
Q.reichswehr_goal_completed = 0;

Q.strike_term_seen = 0;

// understanding the enemy
if (Q.nsdap_r >= 15) {
    Q.understanding_enemy_timer = 2;
}

var election_result = {'date': new Date(Q.year, Q.month - 1)};
for (var party of Q.parties) {
    election_result[party] = Q[party + '_r'];
}
Q.election_records.push(election_result);
!}
on-departure: nazi_urgency = 1 if (nazi_urgency == 0 and nsdap_r >= 15)
on-display: {!
if (Q.sapd_formed == 1) {
    data = [
        {    "id": "kpd",
            "legend": "KPD",
            "name": "KPD",
            "seats": Q.kpd_r*5,
        },
        {
            "id": "sapd",
            "legend": "SAPD",
            "name": "SAPD",
            "seats": Q.sapd_r*5,
        },
        {
            "id": "spd",
            "legend": "SPD",
            "name": "SPD",
            "seats": Q.spd_r*5,
        },
];
        if (Q.vonc_r && Q.vonc_formed) {
            data.push({
                "id": "vonc",
                "legend": "VONC",
                "name": "VONC",
                "seats": Q.vonc_r*5,
            });
        }

    data = data.concat([
        {
            "id": "ddp",
            "legend": Q.ddp_name,
            "name": Q.ddp_name,
            "seats": Q.ddp_r*5,
        },
        {
            "id": "z",
            "legend": "Center",
            "name": "Center",
            "seats": (Q.z_r - 3)*5,
        },
        {
            "id": "bvp",
            "legend": "BVP",
            "name": "BVP",
            "seats": 3*5,
        },
        {
            "id": "dvp",
            "legend": "DVP",
            "name": "DVP",
            "seats": Q.dvp_r*5,
        },
        {
            "id": "other",
            "legend": "Other",
            "name": "Other",
            "seats": Q.other_r*5,
        },
        {
            "id": "dnvp",
            "legend": "DNVP",
            "name": "DNVP",
            "seats": Q.dnvp_r*5,
        },
    ]);
        if (Q.dne_r && Q.dne_formed) {
            data.push({
                "id": "dne",
                "legend": "DNE",
                "name": "DNE",
                "seats": Q.dne_r*5,
            });
        }

    data = data.concat([
        {
            "id": "nsdap",
            "legend": "NSDAP",
            "name": "NSDAP",
            "seats": Q.nsdap_r*5,
        },
    ]);
}
else {
    data = [
        {   "id": "kpd",
            "legend": "KPD",
            "name": "KPD",
            "seats": Q.kpd_r*5,
        },
        {
            "id": "spd",
            "legend": "SPD",
            "name": "SPD",
            "seats": Q.spd_r*5,
        },
];
        if (Q.vonc_r && Q.vonc_formed) {
            data.push({
                "id": "vonc",
                "legend": "VONC",
                "name": "VONC",
                "seats": Q.vonc_r*5,
            });
        }
    
    data = data.concat([
        {
            "id": "ddp",
            "legend": Q.ddp_name,
            "name": Q.ddp_name,
            "seats": Q.ddp_r*5,
        },
        {
            "id": "z",
            "legend": "Center",
            "name": "Center",
            "seats": (Q.z_r - 3)*5,
        },
        {
            "id": "bvp",
            "legend": "BVP",
            "name": "BVP",
            "seats": 3*5,
        },
        {
            "id": "dvp",
            "legend": "DVP",
            "name": "DVP",
            "seats": Q.dvp_r*5,
        },
        {
            "id": "other",
            "legend": "Other",
            "name": "Other",
            "seats": Q.other_r*5,
        },
        {
            "id": "dnvp",
            "legend": "DNVP",
            "name": "DNVP",
            "seats": Q.dnvp_r*5,
        },
    ]);
        if (Q.dne_r && Q.dne_formed) {
            data.push({
                "id": "dne",
                "legend": "DNE",
                "name": "DNE",
                "seats": Q.dne_r*5,
            });
        }

    data = data.concat([
        {
            "id": "nsdap",
            "legend": "NSDAP",
            "name": "NSDAP",
            "seats": Q.nsdap_r*5,
        },
    ]);
}
if (window && d3) {
    var width = 500; 
    var height = 500;
    var screenWidth = document.getElementById('content').offsetWidth;
    if (screenWidth < width - 50) {
        width = screenWidth - 50; 
        height = width;
        document.getElementById("reichstag").style.height = screenWidth/2 + "px";
    } 

    var parliament = d3.parliament();
    parliament.width(width).height(height).innerRadiusCoef(0.4);
    parliament.enter.fromCenter(true).smallToBig(true);
    parliament.exit.toCenter(false).bigToSmall(true);
    d3.select("#reichstag").datum(data).call(parliament);
}
!}
go-to: coalition_menu if not vonc_formed; coalition_vonc if vonc_formed

= Election results

{!<svg id="reichstag" style="width: 500px; height: 250px;"> </svg>!}

{!<table>!} 
{!<tr><td>Party</td><td>Vote share</td><td>% Change</td></tr>!}
{!<tr><td><div class="box" style='background-color:#E3000F; outline: 2px; outline-style: solid;'> </div> !} **SPD:** {!</td><td>!} **[+ spd_r +]%** {!</td><td>!} **[+ str_change_spd +]** {!</td></tr>!}

[? if vonc_formed == 1 : {!<tr><td><div class="box" style='background-color:#ffffff00'> </div> !}Voncspartei: {!</td><td>!} [+ vonc_r +]% {!</td><td>!} [+ str_change_vonc +] {!</td></tr>!} ?]

[? if sapd_formed == 1 : {!<tr><td><div class="box" style='background-color:#C40000'> </div> !}SAPD: {!</td><td>!} [+ sapd_r +]% {!</td><td>!} [+ str_change_sapd +] {!</td></tr>!} ?]

{!<tr><td><div class="box" style='background-color:#8B0000'> </div> !}KPD: {!</td><td>!} [+ kpd_r +]% {!</td><td>!} [+ str_change_kpd +] {!</td></tr>!}

{!<tr><td><div class="box" style='background-color:#DCCA4A'> </div> !} [+ ddp_name +]: {!</td><td>!} [+ ddp_r +]% {!</td><td>!} [+ str_change_ddp +] {!</td></tr>!}

{!<tr><td><div class="box" style='background-color:#000'> </div> !} Z: {!</td><td>!} [+ z_minus_bvp_r +]% {!</td><td>!} [+ str_change_z +] {!</td></tr>!}

{!<tr><td><div class="box" style='background-color:#69A2BE'> </div> !} [+ bvp_name +]: {!</td><td>!} 3% {!</td><td>!} 0 {!</td></tr>!}

{!<tr><td><div class="box" style='background-color:#D5AC27'> </div> !} DVP: {!</td><td>!} [+ dvp_r +]% {!</td><td>!} [+ str_change_dvp +] {!</td></tr>!}
 
{!<tr><td><div class="box" style='background-color:#a0a0a0'> </div> !} Others: {!</td><td>!} [+ other_r +] % {!</td><td>!} [+ str_change_other +] {!</td></tr>!}

{!<tr><td><div class="box" style='background-color:#3f7bc1'> </div> !} DNVP: {!</td><td>!} [+ dnvp_r +]% {!</td><td>!} [+ str_change_dnvp +] {!</td></tr>!}

[? if dne_formed == 1 : {!<tr><td><div class="box" style='background-color:#b81191'> </div> !}DNE: {!</td><td>!} [+ dne_r +]% {!</td><td>!} [+ str_change_dne +] {!</td></tr>!} ?]

{!<tr><td><div class="box" style='background-color:#954B00'> </div> !} NSDAP: {!</td><td>!} [+ nsdap_r +]% {!</td><td>!} [+ str_change_nsdap +] {!</td></tr>!}
{!</table>!}

**Potential coalitions:**

Weimar Coalition (SPD + Z + [+ ddp_name +]): [+ weimar_coalition +]%

Grand Coalition (SPD + Z + [+ bvp_name +] + [+ ddp_name +] + DVP): [+ grand_coalition +]%

Bourgeois Coalition (Z + [+ bvp_name +] + [+ ddp_name +] + DVP + Others): [+ bourgeois_coalition +]%

Right-wing Coalition (Z + [+ bvp_name +] + DVP + Others + DNVP): [+ right_coalition +]%

[? if nazi_urgency <= 1 and nsdap_r >= 15 : The rapid rise in NSDAP votes is extremely worrying. Many had believed that the party was moribound; now, some of us consider it to be the greatest threat to democracy in Germany. ?]

[? if anti_democratic_coalition >= 50 : Together, the anti-democratic KPD, DNVP, and NSDAP have a majority in the Reichstag. A stable governing majority will be almost impossible. ?]

There are some potential coalition arrangements.

@coalition_vonc

- @vonc_coalition: <span style="color: transparent;">VONC</span>

@vonc_coalition
on-arrival: in_vonc_coalition = 1; spd_in_government = 1; in_spd_majority = 1; 

As expected, the 

- @braun_majority_chancellor: Otto Braun for Chancellor.
- @breitscheid_majority_chancellor: Rudolf Breitscheid for Chancellor.
- @muller_majority_chancellor: Hermann Müller for Chancellor.
- @wels_majority_chancellor: Otto Wels for Chancellor.

@coalition_menu

- @spd_majority
- @weimar_coalition
- @grand_coalition
- @bourgeois_coalition
- @new_grand_coalition
- @unity_government
- @right_coalition
- @far_right_coalition
- @no_majority_marx
- @no_majority
- @black_brown
- @schleicher_coalition
- @no_majority_seldte2
- @no_majority_seeckt
- @no_majority_hitler2
- @no_majority_adenauer
- @no_majority_gessler
- @no_majority_eckener
- @brauns_dilemma
- @left_coalition
- @kpd_majority
- @popular_front_coalition
- @national_front_coalition
- @no_majority_hitler
- @no_govt_3

# TODO: black-brown coalition

@national_front_coalition
title: A "National Front" coalition
view-if: national_front
choose-if: national_front_r >= 50
subtitle: [? if z_national : Z + ?]BVP + DVP + Others + DNVP + NSDAP ([+ national_front_r +])
unavailable-subtitle: [? if z_national : Z + ?]BVP + DVP + Others + DNVP + NSDAP ([+ national_front_r +]) - The National Front lacks a majority.
on-arrival: started = 0
go-to: root

@bourgeois_coalition
title: A "Bourgeois" Coalition
subtitle: Z + BVP + [+ ddp_name +] + DVP + Others ([+ bourgeois_coalition +]%)
view-if: bourgeois_coalition >= 50 and (weimar_coalition < 50 or z_willing != 1) and not dvp_banned

The constitutional bourgeois parties have enough votes to form a coalition on their own, without even needing toleration from us. It is likely that such a government will be formed unless we act to prevent it.

- @plead_grand: Plead for the Center to form a "Grand Coalition" with us instead.
- @let_it_form: Let it form.

@plead_grand
subtitle: SPD + Z + [+ bvp_name +] + [+ ddp_name +] + DVP  ([+ grand_coalition +]%)
unavailable-subtitle: SPD + Z + [+ bvp_name +] + [+ ddp_name +] + DVP  ([+ grand_coalition +]%) - The DVP is unwilling to enter such a government[? if z_relation < 30 :, as is the Center party?].
view-if: not grand_coalition_failed and grand_coalition >= 50
choose-if: dvp_relation >= 30 and z_relation >= 30
go-to: grand_coalition

We have managed to convince Zentrum to instead support another Grand Coalition!

@let_it_form
subtitle: The Bourgeois Coalition will form.
on-arrival: spd_in_government = 0; spd_toleration = 0; in_bourgeois_coalition = 1; chancellor = "Brüning"; chancellor_party = "Z"; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "Z"; labor_minister_party = "Z"; reichswehr_minister_party = ddp_name; economic_minister_party = "Z"; finance_minister_party = "Z"; agriculture_minister_party = "Z"

The Bourgeois Coalition is formed, with Brüning as its chancellor. We will have to contend with opposition politics for the time being.

@popular_front_coalition
title: A new "Popular Front" coalition?
subtitle: SPD + KPD + Z + [+ ddp_name +] ([+ popular_front_coalition +]%)
unavailable-subtitle: SPD + KPD + Z + [+ ddp_name +] ([+ popular_front_coalition +]%) - [? if kpd_relation < 50: We do not have good enough relations with the KPD. ?][? if z_relation < 50: We do not have good enough relations with the Center Party. ?][? if communist_coalition < 3 : The Communists are unwilling to compromise at all. ?][? if president == "Hitler" : Hitler refuses to appoint a chancellor under such a governing arrangement. ?][? if president == "Hindenburg" and coup_progress >= 8 and popular_front < 55 and historical_mode : Hindenburg is unwilling to appoint this government. ?]
view-if: popular_front_coalition >= 50 or (sapd_formed == 1 and (popular_front_coalition + sapd_r) >= 50)
choose-if: ((((kpd_relation >= 65 and kpd_party_leader == "Thälmann") or (kpd_relation >= 45 and kpd_party_leader == "Conciliators")) and ((z_relation >= 45 and z_party_leader == "Joos") or (z_relation >= 65 and z_party_leader != "Joos" and kpd_party_leader != "Conciliators") or (z_relation >= 55 and z_party_leader != "Joos" and kpd_party_leader == "Conciliators")) and ddp_relation >= 50 and communist_coalition >= 3) or (z_party_leader == "Joos" and kpd_party_leader == "Conciliators" and z_relation >= 30 and kpd_relation >= 30)) and president != "Hitler" and (president != "Hindenburg" or ((coup_progress < 8 or popular_front >= 55 or not historical_mode) and not spd_sackings))

Could we really form a coalition that included both the Center and the Communists? It seems like an absurd concept, but given our good relationships with both sides, it is not an impossibility. [? if sapd_formed == 1 and popular_front_coalition < 50 : The SAPD will tolerate this governing arrangement, allowing us to form such a government despite a slim minority. ?]

The far-right might attempt a coup, if such a coalition were formed.

- @popular_front_2: Form the Popular Front!
- @great_popular_front: Form the Popular Front!

@popular_front_2
view-if: z_party_leader != "Joos" or kpd_party_leader != "Conciliators"

Somehow, both the Communists and Center are at the very least willing to entertain negotiations. Of course, KPD radicals and Center conservatives derail the negotiations almost immediately. The KPD demands leadership of the coalition for themselves and their chairman Ernst Thälmann, [? if spd_r > kpd_r : despite our higher vote share, ?]while the Center is offended by the prospect of helping Bolsheviks enter government. *Something* must be done to restore balance.

- @popular_front_resources
- @popular_front_democracy
- @popular_front_braun
- @coalition_menu: This is impossible. Try another governing arrangement.

@popular_front_resources
title: Use our resources to smooth things over.
unavailable-subtitle: Required: 4 resources
subtitle: -4 resources
choose-if: resources >= 4
on-arrival: resources -= 4
go-to: popular_front_success

@popular_front_democracy
title: Convince the KPD to accept democracy.
subtitle: [? if kpd_party_leader == "Conciliators" : The Conciliator leadership of the KPD at least claim to support the Popular Front. ?]
unavailable-subtitle: Our relationship with the KPD is not high enough.
choose-if: (kpd_relation >= 60 and communist_coalition >= 3) or kpd_party_leader == "Conciliators"
go-to: popular_front_success

@popular_front_braun
title: Use pressure from President Braun to force the parties to cooperate.
view-if: president == "Braun"
go-to: popular_front_success

@great_popular_front
view-if: z_party_leader == "Joos" and kpd_party_leader == "Conciliators"
on-arrival: spd_in_government = 1; in_popular_front = 1; chancellor = "Breitscheid"; chancellor_party = "SPD"; kpd_relation += 10; coup_progress += 2; in_left_front = 0; in_weimar_coalition = 0; in_grand_coalition = 0; coalition_dissent = 0;
achievement: grosse_volksfront
go-to: popular_front_civil_war if coup_progress >= 10; popular_front_no_civil_war if coup_progress < 10

Somehow, the coalition is successfully formed! The conciliatory leadership of the Center Party and KPD helped make the negotiations possible. We still need a chancellor; the only acceptable choice among the parties was Rudolf Breitscheid of the SPD, acceptable to the Center and KPD [? if president == "Hindenburg" : as well as President Hindenburg, albeit extremely reluctantly ?].

@popular_front_success
on-arrival: spd_in_government = 1; in_popular_front = 1; chancellor = "Breitscheid"; chancellor_party = "SPD"; kpd_relation += 10; coup_progress += 2; in_left_front = 0; in_weimar_coalition = 0; in_grand_coalition = 0; coalition_dissent = 0;
go-to: popular_front_civil_war if coup_progress >= 10 and (sa_banned != 1 or sh_banned != 1); popular_front_no_civil_war if coup_progress < 10 or (sa_banned and sh_banned)
achievement: volksfront

Somehow, surprisingly, the coalition is successfully formed! We still need a chancellor; the only acceptable choice among the parties was Rudolf Breitscheid of the SPD, acceptable to the Center and KPD [? if president == "Hindenburg" : as well as President Hindenburg, albeit extremely reluctantly ?].

@popular_front_no_civil_war
on-arrival: kpd_goals_seen = 0; finance_minister_party = "Z"; interior_minister_party = "Z"; economic_minister_party = "Z"; labor_minister_party = "Z"; foreign_minister_party = "Z"; agriculture_minister_party = "Z"; reichswehr_minister = "I"; leverage += kpd_r
achievement: volksfront

In response to the KPD's entrance into the government, the right-wing takes a "wait-and-see" approach for now. They expect that the Popular Front government will be unstable and incompetent, and will collapse of its own accord, discrediting the left and center and eventually enabling the destruction of the Weimar Republic. Our task is to prove them wrong.

The KPD has taken an abstentionist position with regards to ministries. They claim to be participating as the "loyal opposition" within the Popular Front government, supporting us from the outside while also running their own propaganda campaigns to turn workers to their side. They seem to expect that the Popular Front would inevitably fail, and that our failure would benefit them.

- @ministries: First, we must select our ministries.


@popular_front_civil_war
on-arrival: kpd_relation = 80; reichswehr_loyalty += 0.25; nazi_rebel = 1; labor_minister_party = "SPD"; interior_minister_party = "SPD"; finance_minister_party = "SPD"; economic_minister_party = "SPD"; justice_minister_party = "SPD"; foreign_minister_party = "SPD" if spd_r >= 35; foreign_minister_party = "Z" if spd_r < 35; agriculture_minister_party = "Z"; reichswehr_minister_party = "I"

In response to the KPD's entrance into the government, the right-wing immediately launches a coup attempt. Troops of the Stahlhelm and Nazi SA enter Berlin and immediately begin clashing with our supporters! Reactionary officers within the Reichswehr have taken their troops out of the barracks and joined them with the Stahlhelm to stop us from taking power.

The Center manages to diffuse some of the hostility from the Reichswehr and the Stahlhelm, but fail to prevent the outbreak of fighting. We are forced to fight on the same side as the KPD, whether we want to or not.

- @civil_war: A civil war, then.



@far_right_coalition
title: Far-right coalition
subtitle: NSDAP + DNVP ([+ far_right_coalition +]%)
view-if: far_right_coalition >= 50
go-to: far_right_win if (year >= 1932 or nsdap_r >= 44) and president != "Braun" and not hitler_seen; far_right_fail if (year < 1932 and nsdap_r < 44) and president != "Braun" and not hitler_seen; far_right_braun if president == "Braun"; far_right_incumbent if hitler_seen
priority: 2

There are enough votes for the Nazis and the DNVP to form a coalition. [+ nsdap_leader +] has made his participation in government contingent on being appointed chancellor. It is up to President [+ president +] to decide.

@far_right_win
on-arrival: chancellor = nsdap_leader; chancellor_party = "NSDAP"

[? if president == "Hindenburg" : President Hindenburg's camarilla ?][? if president == "Braun" : President Braun somehow ?] has decided that [+ nsdap_leader +] must be chancellor [? if president == "Hindenburg": , and the aging president accepts their recommendations. ?][? if president == "Braun" : because it is what the constitution would stipulate. ?]

- @hitler_takes_power: What do we do now?


@far_right_fail
on-arrival: chancellor = "Schleicher"; chancellor_party = "I"; pro_republic -= 10; next_election_time = time+5; next_election_month = month + 5; next_election_year = year; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12;

The reactionary establishment is still very skeptical of the Nazis, so instead, [+ president +] appoints Kurt von Schleicher, a former general, as chancellor, to try to "tame" the Nazis.

Schleicher's government is deeply unpopular among all parties of the Reichstag. Despite his support for a coalition with the NSDAP, the Nazis want power all to themselves, without Schleicher.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@far_right_braun
on-arrival: coup_progress += 2

President Braun faces a dilemma. Is it "democratic" to let democracy be destroyed, if the majority of voters have willed it so? Some, even on the left, suggest that the Nazis would not *truly* be as horrific as their platform suggests, and that giving them power would let them discredit themselves in the eyes of the public.

Alternatively, Braun could use the emergency powers of the presidency. Article 48 has been used by Hindenburg to promulgate anti-democratic and anti-worker policies; can Article 48 be used for good as well as evil?

- @emergency_braun: We will form an emergency government with [+ president +]'s support.
- @new_elections: We will have new elections.
- @new_elections_2: We will have new elections.
- @far_right_win: Let the Nazis form their government.

@far_right_incumbent
on-arrival: enabling_r += dnvp_r; enabling_r += nsdap_r; enabling_r += other_r; enabling_r += dvp_r if dvp_relation < 45; enabling_r += z_r if z_relation < 30; against_enable += spd_r*(1-(left_strength/100)); against_enable += dvp_r if dvp_relation >= 70; against_enable += z_r if z_relation >= 65; dvp_voted = 1 if dvp_relation >= 70 or dvp_relation < 45; z_voted = 1 if z_relation >= 65 or z_relation < 30
go-to: enabling_menu

Through their violent electioneering, the Nazis have, together with the DNVP, won a majority. But it is not enough, and Hitler desires total and absolute power. To this end, he has proposed an act - the "Enabling Act" - that would give him and his cabinet legislative powers, bypassing the Reichstag entirely. To pass, it requires a 2/3rds majority vote, though the KPD's deputies are under arrest, preventing them from voting against, as are our own deputies on the party's left-wing.

The key link now is the Center Party. They have been a valuable partner in upholding the Weimar Constitution - will they stand by us to stop Hitler from destroying it? [? if dvp_relation >= 60 : The DVP too has been surprisingly friendly towards us. Can we convince them to stop Hitler? ?]

@enabling_menu

- @contact_ddp: Can we convince the [+ ddp_name +] to vote against?
- @contact_z: Can we convince the Center Party to vote against?
- @contact_dvp: Can we convince the DVP to vote against?
- @voting_results: We have done all we can. Now we must vote against.

@contact_dvp
choose-if: dvp_relation >= 45 and dvp_relation < 70
unavailable-subtitle: They've already made their decision.
max-visits: 1
on-arrival: dvp_relation += 5
go-to: success_dvp if dvp_relation >= 70; partial_dvp if dvp_relation >= 52 and dvp_relation < 70; failure_dvp if dvp_relation < 52

@success_dvp
on-arrival: dvp_relation += 5; against_enable += dvp_r; dvp_voted = 1
go-to: enabling_menu

Thanks to our good relations, the DVP promises to vote against.

@partial_dvp

The DVP isn't convinced by our arguments, and wants more than just ideological nations of antifascism.

- @bribe_dvp: Give them the resources they need.
- @failure_dvp: We've done all we can.

@bribe_dvp
choose-if: resources >= 1
subtitle: -1 resource
on-arrival: resources -= 1; against_enable += dvp_r; dvp_voted = 1
go-to: enabling_menu

With our resources as well as our arguments, the DVP is convinced, and promises to vote against.

@failure_dvp
on-arrival: dvp_relation -= 10; enabling_r += dvp_r; dvp_voted = 1;
go-to: enabling_menu

Despite our best efforts, the DVP is not convinced. We'll have to hope we can lobby a majority without them.

@contact_z
choose-if: z_relation >= 30 and z_relation < 65
unavailable-subtitle: They've already made their decision.
max-visits: 1
on-arrival: z_relation += 5
go-to: success_z if z_relation >= 60; partial_z if z_relation >= 40 and z_relation < 60; failure_z if z_relation < 40

@success_z
on-arrival: z_relation += 5; against_enable += z_r; z_voted = 1;
go-to: enabling_menu

Thanks to our good relations, the Center Party promises to vote against.

@partial_z

The Center Party isn't so easily convinced by our arguments, and wants more than just ideological nations of antifascism.

- @bribe_z: Give them the resources they need.
- @joos_clutch: Surely Joos can convince his party?
- @concordat_kaas: Remind Kaas of the Concordat.
- @failure_z: We've done all we can.

@bribe_z
choose-if: resources >= 1
subtitle: -1 resource
on-arrival: resources -= 1; against_enable += z_r; z_voted = 1;
go-to: enabling_menu

With our resources as well as our arguments, the Center Party is convinced, and promises to vote against.

@joos_clutch
view-if: z_party_leader == "Joos"
on-arrival: against_enable += z_r; z_voted = 1
go-to: enabling_menu

Despite initial resistance, Joseph Joos eventually convinces a majority of his party to vote against the Enabling Act.

@concordat_kaas
view-if: z_party_leader == "Kaas" and reichskonkordat
on-arrival: against_enable += z_r; z_voted = 1
go-to: enabling_menu

We remind Kaas of the Reichskonkordat that Breitscheid brought about, removing a significant reason for the Center Party to side with the Nazis. Reminded, they promise to vote against.

@failure_z
on-arrival: z_relation -= 10; enabling_r += z_r; z_voted = 1
go-to: enabling_menu

Despite our best efforts, the Center Party is not convinced. We'll have to hope we can lobby a majority without them.

@contact_ddp
max-visits: 1
on-arrival: ddp_relation += 5
go-to: success_ddp if ddp_relation >= 70; partial_ddp if ddp_relation >= 45 and ddp_relation < 70; failure_ddp if ddp_relation < 45

@success_ddp
on-arrival: ddp_relation += 5; against_enable += ddp_r; ddp_voted = 1;
go-to: enabling_menu

Thanks to our good relations, the [+ ddp_name +] promises to vote against.

@partial_ddp

The [+ ddp_name +] isn't so easily convinced by our arguments, and wants more than just ideological nations of antifascism.

- @bribe_ddp: Give them the resources they need.
- @failure_ddp: We've done all we can.

@bribe_ddp
choose-if: resources >= 1
subtitle: -1 resource
on-arrival: resources -= 1; against_enable += ddp_r; ddp_voted = 1
go-to: enabling_menu

With our resources as well as our arguments, the [+ ddp_name +] is convinced, and promises to vote against.

@failure_ddp
on-arrival: ddp_relation -= 10; enabling_r += ddp_r; ddp_voted = 1
go-to: enabling_menu

Despite our best efforts, the [+ ddp_name +] is not convinced. We'll have to hope we can lobby a majority without them.

@voting_results
on-arrival: enabling_r += dvp_r if not dvp_voted; enabling_r += z_r if not z_voted; enabling_r += ddp_r if not ddp_voted; {!
var total_r = Q.enabling_r+Q.against_enable;
Q.enabling_r = Math.round((Q.enabling_r/total_r)*100);
!}
go-to: enabling_act_passed if enabling_r >= 67; enabling_act_failed if enabling_r < 67

@enabling_act_passed

Despite our best efforts...the Enabling Act has passed with [+ enabling_r +]% of the votes. [+ chancellor +] has near limitless power, and will begin the process of eliminating any remaining roadblocks.

Germany is lost.

- @game_over: Game over.
- @fight_enabling: We can still stop Hitler! Mobilize the Reichsbanner.

@fight_enabling
choose-if: rb_force >= 10
unavailable-subtitle: We lack the force.
on-arrival: spd_rebel = 1
go-to: civil_war

@enabling_act_failed
on-arrival: spd_rebel = 1; sa_militancy -= 0.2; sa_strength *= 0.8; reichswehr_loyalty += 0.25; rb_militancy += 0.1; rb_strength += 500; kpd_relation += 25

[? if president != "Hitler" and president != "Göring" : Somehow, we have stopped Hitler from passing his Enabling Act, with only [+ enabling_r +]% present voting for! Growing frustrated with the chaos on the streets Hitler himself is unleashing, [+ president +] turns on him and demands his resignation while he attempts to call a constitutional referendum to pass the Enabling Act instead. The Reichsbanner swells and fighting in the streets erupts, while the army is deployed, not for but against Hitler as civil war engulfs the nation. ?]

[? if president == "Hitler" or president == "Göring" : Somehow, we have stopped Hitler from passing his Enabling Act, with only [+ enabling_r +]% present voting for! [+ president +] dissolves the Reichstag for new elections, ruling by decree in the meantime. A constitutional referendum is called as Hitler seeks to pass it outside of the Reichstag's framework, and fighting erupts on the streets as, tired of the chaos, Reichswehr forces begin to join in the emerging rebellion against Hitler and civil war engulfs the nation. ?]

- @civil_war: To victory!

@left_coalition
title: Is a United Left coalition possible?
subtitle: SPD + KPD ([+ left_coalition +]%)
unavailable-subtitle: SPD + KPD ([+ left_coalition +]%). [? if kpd_relation < 50 : We do not have adequate relations with the KPD. ?][? if communist_coalition < 3 : The Communists are too committed to their radical ways to partake in government. ?][? if president == "Hitler" or president == "Göring" or president == "Seldte" or president == "Seeckt" or president == "Hindenburg" : [+ president +] refuses to appoint a chancellor under such a governing arrangement. ?]
view-if: left_coalition >= 50 or (sapd_formed == 1 and (left_coalition + sapd_r) >= 50)
choose-if: (kpd_relation >= 50 or (kpd_party_leader == "Conciliators" and kpd_relation >= 40)) and communist_coalition >= 3 and president != "Hitler" and president != "Göring" and president != "Seldte" and president != "Seeckt" and (president != "Hindenburg" or year < 1929)

There are enough votes for a SPD + KPD coalition government, if such a thing were actually possible. [? if sapd_formed == 1 and left_coalition < 50 : The SAPD will tolerate this governing arrangement, allowing us to form such a government despite a slim minority. ?]

The far-right might attempt a coup, if such a coalition were formed.

- @left_coalition_impossible: This is impossible.
- @left_coalition_possible: This could be possible.

@left_coalition_impossible
go-to: coalition_menu

A far left coalition is impossible.


@left_coalition_possible

The KPD is still very reluctant to join a coalition, despite grassroots friendliness between our memberships. The Stalinist leadership is vehemently against such a thing. They demand the appointment of Ernst Thälmann as chancellor in exchange for a coalition. [? if president == "Hindenburg" : There is no chance whatsoever that President Hindenburg would approve of the appointment of a die-hard communist as chancellor. ?] Alternatively, if our relations with them are good enough, we could convince them to support the appointment of one of our centrist or left-leaning members as chancellor.

- @thalmann_chancellor: Appoint Thälmann as chancellor.
- @left_coalition_resources
- @left_coalition_relations
- @coalition_menu: Try a different governing arrangement.

@thalmann_chancellor
choose-if: (president == "Braun" or president == "Schumacher" or president == "Juchacz" or president == "Thälmann" or president == "Münzenberg") and reformist_strength + neorevisionist_strength < left_strength
unavailable-subtitle: [? if president != "Braun": President [+ president +] would never allow such a thing. ?] [? if reformist_strength >= left_strength : Most in the parliamentary party would not support Thälmann as chancellor. ?]
on-arrival: chancellor = "Thälmann"; chancellor_party = "KPD"; in_left_front = 1; spd_in_government = 1; kpd_goals_seen = 0; rfb_strength += 100; rfb_militancy += 0.02; workers_kpd += 5; unemployed_kpd += 10; reichswehr_loyalty -= 0.05
go-to: thalmann_war if not victory_republic; thalmann_no_war if victory_republic

With great reluctance, President [+ president +] appoints [+ chancellor +] as chancellor.

Immediately, [+ chancellor +] goes to work building a "Soviet Germany": consolidating as much power as possible in the KPD, purging state institutions of anti-communists, and sending street fighters against the "enemies of the people".

@thalmann_war
on-arrival: nazi_rebel = 1; foreign_minister_party = "SPD"; interior_minister_party = "SPD"; justice_minister_party = "SPD"; labor_minister_party = "SPD"; reichswehr_minister_party = "SPD"; economic_minister_party = "SPD"; finance_minister_party = "SPD"; agriculture_minister_party = "SPD"; kpd_relation = 80

In response to [+ chancellor +] being appointed Chancellor, the right-wing forces launches almost immediately launch an attempted coup. Troops of the Stahlhelm and Nazi SA enter Berlin and immediately begin clashing with our supporters! Reactionary officers within the Reichswehr have taken their troops out of the barracks and joined them with the Stahlhelm to stop the KPD from taking power. We are forced to be on the same side as the KPD, whether we want to or not.

- @civil_war

@thalmann_no_war
on-arrival: coup_progress += 5; leverage = ((spd_r/(spd_r+kpd_r))*kpd_relation); reformist_dissent += 5; neorevisionist_dissent += 5; z_relation -= 10; rfb_strength += 100; rb_strength -= 50; kpd_relation -= 10; kpd_influence += 3; foreign_minister_party = "KPD"; interior_minister_party = "KPD"; justice_minister_party = "KPD"; labor_minister_party = "KPD"; reichswehr_minister_party = "KPD"; economic_minister_party = "KPD"; finance_minister_party = "KPD"; agriculture_minister_party = "KPD";
go-to: postwar_left

Despite our appointing of [+ chancellor +] as chancellor, there is no appetite for a civil war so soon after the last. [+ chancellor +] is unopposed as he attempts to consolidate power. There is still, however, the matter of cabinet talks.

#- @communist_victory
# todo: maybe until proper KPD content is made, just fade to black with communist victory ending? but then appointing thalmann after a civil war should too...

@left_coalition_resources
title: Use our resources to smooth things over.
unavailable-subtitle: Required: 3 resources
subtitle: -3 resources
choose-if: resources >= 3
on-arrival: resources -= 3
go-to: left_coalition_success

@left_coalition_relations
title: Convince the KPD to accept democracy.
unavailable-subtitle: Our relationships with the KPD are not good enough.
choose-if: kpd_relation >= 60  or kpd_party_leader == "Conciliators"
go-to: left_coalition_success


@left_coalition_success
on-arrival: spd_in_government = 1; in_left_front = 1; chancellor = "Breitscheid"; chancellor_party = "SPD"; coup_progress += 3; capital_strike_progress += 2; in_popular_front = 0; in_grand_coalition = 0; in_weimar_coalition = 0; kpd_relation += 10 if kpd_r < 50; kpd_relation -= 25 if kpd_r >= 50
go-to: left_coalition_civil_war if coup_progress >= 10 and (sa_banned != 1 or sh_banned != 1); left_coalition_no_civil_war if (coup_progress < 10 or (sa_banned and sh_banned)) and not victory_republic; post_war_left_coalition if (coup_progress < 10 or (sa_banned and sh_banned)) and victory_republic
achievement: einheitsfront

Somehow, surprisingly, the left coalition is successfully formed, without appointing Thälmann as chancellor. The consensus choice for chancellor, accepted by the SPD, KPD, and President [+ president +], is Rudolf Breitscheid, who belongs to the centrist faction of the SPD, but was acceptable to the left due to his previous membership in the left-wing anti-war USPD.

# just raise the coup limit, don't immediately go to civil war

@left_coalition_no_civil_war
on-arrival: kpd_goals_seen = 0; foreign_minister_party = "SPD"; interior_minister_party = "SPD"; justice_minister_party = "SPD"; labor_minister_party = "SPD"; reichswehr_minister_party = "SPD"; economic_minister_party = "SPD"; finance_minister_party = "SPD"; agriculture_minister_party = "SPD"; reichswehr_loyalty -= 0.05

In response to the KPD's entrance into the government, the right-wing takes a "wait-and-see" approach for now. They expect that our government will be unstable and incompetent, and will collapse of its own accord, discrediting the left and eventually enabling the destruction of the Weimar Republic. Our task is to prove them wrong.

The KPD has taken an abstentionist position with regards to ministries. They claim to be participating as the "loyal opposition" in the worker's government, supporting us from the outside while also running their own propaganda campaigns to turn workers to their side. They seem to expect that we would inevitably fail, and that our failure would benefit them. 

Whatever their reasoning is, the SPD has control over the ministries of government.

@left_coalition_civil_war
on-arrival: reichswehr_loyalty += 0.1; nazi_rebel = 1; kpd_goals_seen = 0; foreign_minister_party = "SPD"; interior_minister_party = "SPD"; justice_minister_party = "SPD"; labor_minister_party = "SPD"; reichswehr_minister_party = "SPD"; economic_minister_party = "SPD"; finance_minister_party = "SPD"; agriculture_minister_party = "SPD";

In response to the KPD's entrance into the government, the right-wing immediately launches a coup attempt. Troops of the Stahlhelm and Nazi SA enter Berlin and begin clashing with our supporters. Reactionary officers within the Reichswehr have taken their troops out of the barracks and joined them with the Stahlhelm to stop us from taking power.

- @civil_war: A civil war, then.

@post_war_left_coalition
on-arrival: kpd_goals_seen = 0; leverage = ((spd_r/(spd_r+kpd_r))*(kpd_relation/1.5)); leverage *= 0.8 if kpd_r > spd_r; leverage *= 0.6 if kpd_r >= 50; foreign_minister_party = "KPD"; interior_minister_party = "KPD"; justice_minister_party = "KPD"; labor_minister_party = "KPD"; reichswehr_minister_party = "KPD"; economic_minister_party = "KPD"; finance_minister_party = "KPD"; agriculture_minister_party = "KPD";
go-to: postwar_left

In response to the KPD's entrance into the government, the right-wing takes a "wait-and-see" approach for now. They expect that our government will be unstable and incompetent, and will collapse of its own accord, discrediting the left and eventually enabling the destruction of the Weimar Republic. Our task is to prove them wrong.

With our victory in the civil war, the Communists have found the confidence to directly participate in government. They have dropped their abstentionist approach, and will take any ministry which we do not.

@postwar_left

- #mins_left
- #drop_mins_left

@labor_left
title: Labor Ministry
subtitle: Taking the Labor Ministry is expected of us. [? if historical_mode : Labor will be displeased if we do not. ?]
unavailable-subtitle: We do not have enough leverage.
tags: mins_left
choose-if: leverage >= 5
view-if: labor_minister_party != "SPD"
on-arrival: leverage -= 5; labor_minister_party = "SPD"; labor_minister = "Wissell"
go-to: postwar_left

Rudolf Wissell of the SPD has been appointed labor minister.

@interior_left
title: Interior Ministry
subtitle: Taking the Interior Ministry is expected of us. The Interior Ministry controls domestic security.
unavailable-subtitle: We do not have enough leverage.
tags: mins_left
choose-if: leverage >= 10
view-if: interior_minister_party != "SPD"
on-arrival: leverage -= 10; interior_minister_party = "SPD"; interior_minister = "Severing"
go-to: postwar_left

Carl Severing of the SPD has been appointed Interior Minister.

@finance_left
title: Finance Ministry
subtitle: The Finance Ministry controls the government budget and taxes.
unavailable-subtitle: We do not have enough leverage.
tags: mins_left
choose-if: leverage >= 10 
view-if: finance_minister_party != "SPD"
on-arrival: leverage -= 10; finance_minister_party = "SPD"; finance_minister = "Hilferding"
go-to: postwar_left

Rudolf Hilferding of the SPD has been appointed Finance Minister.

@economic_left
title: Economic Ministry
subtitle: The Economic Ministry controls broader economic policy.
unavailable-subtitle: We do not have enough leverage.
tags: mins_left
choose-if: leverage >= 15 
view-if: economic_minister_party != "SPD"
on-arrival: leverage -= 15; economic_minister_party = "SPD"; economic_minister = "Schmidt"
go-to: postwar_left

Robert Schmidt of the SPD has been appointed Economic Minister.

@justice_left
title: Justice Ministry
subtitle: The Justice Ministry controls the courts.
unavailable-subtitle: We do not have enough leverage.
tags: mins_left
choose-if: leverage >= 10 
view-if: justice_minister_party != "SPD"
on-arrival: leverage -= 10; justice_minister_party = "SPD"; justice_minister = "Radbruch"
go-to: postwar_left

Gustav Radbruch of the SPD has been appointed Justice Minister.

@foreign_left
title: Foreign Ministry
subtitle: This is usually the domain of Gustav Stresemann of the DVP.
unavailable-subtitle: We do not have enough leverage.
tags: mins_left
choose-if: leverage >= 10 
view-if: foreign_minister_party != "SPD"
on-arrival: leverage -= 10; foreign_minister_party = "SPD"; foreign_minister = "Breitscheid"
go-to: postwar_left

Rudolf Breitscheid of the SPD has been appointed Foreign Minister.


@agriculture_left
title: Agricultural Ministry
subtitle: This is not our usual domain.
unavailable-subtitle: We do not have enough leverage.
choose-if: leverage >= 5 
view-if: agriculture_minister_party != "SPD"
on-arrival: leverage -= 5; agriculture_minister_party = "SPD"; agriculture_minister = "Baade"
go-to: postwar_left
tags: mins_left

Fritz Baade of the SPD has been appointed Agriculture Minister.

@reichswehr_left
title: Reichswehr Ministry
subtitle: Leaving this to the Communists could lead to a coup.
unavailable-subtitle: We do not have enough leverage.
tags: mins_left
choose-if: leverage >= 5 
view-if: reichswehr_minister_party != "SPD"
on-arrival: leverage -= 5; reichswehr_minister_party = "SPD"; reichswehr_minister = "Leber"
go-to: postwar_left

Julius Leber of the SPD has been appointed Reichswehr Minister.



@no_more_left
title: No more ministries.
on-arrival: labor_dissent += 10 if historical_mode and labor_minister_party != "SPD"
subtitle: [? if leverage >= 10 : We still have some more leverage to gain ministries. ?][? if leverage < 10 : We do not have much leverage left. ?]
tags: mins_left

This coalition arrangement is tolerable to all.

@drop_labor_left
title: Drop the Labor Ministry.
view-if: labor_minister_party = "SPD"
on-arrival: labor_minister_party = "KPD"; leverage += 5;
tags: drop_mins_left
go-to: postwar_left

@drop_interior_left
title: Drop the Interior Ministry.
view-if: interior_minister_party = "SPD"
on-arrival: interior_minister_party = "KPD"; leverage += 10;
tags: drop_mins_left
go-to: postwar_left

@drop_finance_left
title: Drop the Finance Ministry.
view-if: finance_minister_party = "SPD"
on-arrival: leverage += 10; finance_minister_party = "KPD"
tags: drop_mins_left
go-to: postwar_left

@drop_economic_left
title: Drop the Economic Ministry.
view-if: economic_minister_party = "SPD"
on-arrival: leverage += 15; economic_minister_party = "KPD"
tags: drop_mins_left
go-to: postwar_left

@drop_justice_left
title: Drop the Justice Ministry.
view-if: justice_minister_party = "SPD"
on-arrival: justice_minister_party = "KPD"; leverage += 10;
tags: drop_mins_left
go-to: postwar_left

@drop_foreign_left
title: Drop the Foreign Ministry.
view-if: foreign_minister_party = "SPD"
on-arrival: leverage += 10; foreign_minister_party = "KPD"
tags: drop_mins_left
go-to: postwar_left

@drop_agriculture_left
title: Drop the Agriculture Ministry.
view-if: agriculture_minister_party = "SPD"
on-arrival: agriculture_minister_party = "KPD"; leverage += 5
tags: drop_mins_left
go-to: postwar_left

@drop_reichswehr_left
title: Drop the Reichswehr Ministry.
view-if: reichswehr_minister_party = "SPD"
on-arrival: reichswehr_minister_party = "KPD"; leverage += 5;
tags: drop_mins_left
go-to: postwar_left

@kpd_majority
title: The KPD may govern alone.
subtitle: KPD ([+ kpd_r +]%)
view-if: kpd_r >= 50 and president == "Braun" or president == "Thälmann"
go-to: kpd_thalmann if president == "Thälmann"; kpd_braun if president == "Braun"

@kpd_thalmann

The Communists have, somehow, won a majority on their own. President Thälmann appoints Wilhelm Pieck to preside over  KPD-only cabinet.

- @communist_victory: The KPD is quick to consolidate power.

@kpd_braun

The Communists have, somehow, won a majority on their own. However, to actually take power, President Braun must appoint Thälmann Chancellor.

- @communist_victory: We will appoint Thälmann as Chancellor.
- @left_coalition: Perhaps they can include us?

@communist_victory
on-arrival: chancellor_party = "KPD"; chancellor = "Thälmann"

The Communists quickly rebuild Germany in the Soviet image, bringing it into their sphere and rooting out reactionaries and liberals alike. The fascists have been successfully kept away from power, but are the communists really so much better? This is a question that has been debated within our party for a while, and only the next few years can tell.

- @game_over: Game over.

# todo: playable thalmann chancellorship

@right_coalition
title: A right-wing coalition may be formed.
subtitle: Z + [+ bvp_name +] + DVP + DNVP + Others ([+ right_coalition +]%)
unavailable-subtitle: The DNVP is not interested in a coalition with the Center.
view-if: right_coalition >= 50
choose-if: year < 1928 or dnvp_reform
go-to: no_right_coalition if year >= 1930 and not dnvp_reform; right_coalition_2 if year <= 1929 and not dnvp_reform; right_coalition_3 if dnvp_reform

There are enough votes for a right-wing coalition of the Center (+ [+ bvp_name +]), DVP, DNVP, and minor conservative parties.

@no_right_coalition
go-to: no_majority

However, the DNVP and its leader Hugenberg is refusing to partake in a coalition with the "November criminals" in the Center Party. Thus, there is no clear path towards a Reichstag majority.



@right_coalition_2
view-if: right_coalition_failed != 1 and right_coalition >= 50
on-arrival: spd_in_government = 0; spd_toleration = 0; chancellor = "Brüning"; chancellor_party = "Z"; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "Z"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = "Z"; finance_minister_party = "Z"; agriculture_minister_party = "Z"; in_right_coalition = 1; workers_spd -= 3; new_middle_spd -= 2; judicial_reform -= 1; reichswehr_loyalty -= 0.05; interior_police_loyalty -= 0.05; welfare -= 1; budget += 1; nationalism += 6
go-to: right_coalition_3 if dnvp_reform; right_coalition_old if not dnvp_reform

@right_coalition_old

Even though the DNVP is very reluctant to participate in such a coalition, they still accede to [? if president == "Hindenburg" : President Hindenburg's wishes ?][? if president != "Hindenburg" : the Center Party's wishes ?] and to allow a government to be formed. Thus, a majority center-right coalition is formed, with Brüning as chancellor.

@right_coalition_3

With the DNVP's new more amicable position towards participation in parliamentary politics, the right-wing coalition is successfully formed.

@no_majority_gessler
title: There is no clear governing majority.
view-if: (weimar_coalition < 50 or not z_willing) and (grand_coalition < 50 or grand_coalition_failed == 1 or dvp_willing != 1) and president = "Gessler" and far_right_coalition < 50 and (right_coalition < 50 or right_coalition_failed) and bourgeois_coalition < 50
on-arrival: no_majority_elections += 1
go-to: bruning_lead if no_majority_elections < 3 and (chancellor != "Seldte" or ((harzburg_r < 25 or seldte_times >= 1) and not (army_force+sh_force >= 50 and dnvp+sh_force < 50 and reichswehr_loyalty < 0.3 and strife >= 4 and not presidential_powers and (president == "Hindenburg" or (president == "Gessler" and reichswehr_loyalty < 0.15))))); luther_lead if no_majority_elections >= 3 and (dvp_relation < 55 or bourgeois_coalition+far_right_coalition >= 50 or ddp_votes < 3) and (chancellor != "Seldte" or ((harzburg_r < 25 or seldte_times >= 1) and not (army_force+sh_force >= 50 and dnvp+sh_force < 50 and reichswehr_loyalty < 0.3 and strife >= 4 and not presidential_powers and (president == "Hindenburg" or (president == "Gessler" and reichswehr_loyalty < 0.15))))); kulz_lead if dvp_relation >= 45 and bourgeois_coalition+far_right_coalition < 50 and ddp_votes >= 3 and (chancellor != "Seldte" or ((harzburg_r < 25 or seldte_times >= 1) and not (army_force+sh_force >= 50 and dnvp+sh_force < 50 and reichswehr_loyalty < 0.3 and strife >= 4 and not presidential_powers and (president == "Hindenburg" or (president == "Gessler" and reichswehr_loyalty < 0.15))))); no_majority_seldte if chancellor == "Seldte" and (harzburg_r >= 25 and seldte_times < 1) or (army_force+sh_force >= 50 and dnvp+sh_force < 50 and reichswehr_loyalty < 0.3 and strife >= 4 and not presidential_powers and (president == "Hindenburg" or (president == "Gessler" and reichswehr_loyalty < 0.15)))

@luther_lead
on-arrival: chancellor = "Luther"; chancellor_party = "I"

There is no path to an obviously workable majority. President [+ president +] has given Hans Luther, Reichsbank director formerly of the DVP, the task of forming a government, tired of Brüning's repeated failures to govern and hoping that Hans Luther would be able to gain broad Reichstag support.

- @toleration_luther: We will tolerate Luther.
- @new_elections: We will have new elections.
- @new_elections_2: We will have new elections.

@toleration_luther
choose-if: neo_weimar_coalition >= 50
unavailable-subtitle: The parties supporting Luther do not have a majority.
on-arrival: spd_in_government = 0; spd_toleration = 1; dvp_relation += 3; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "I"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = ddp_name; finance_minister_party = "DVP"; agriculture_minister_party = "Z"; pro_republic -= 4; left_dissent += 5; labor_dissent += 5; center_dissent += 3; reformist_dissent += 3; workers_spd -= 5; unemployed_spd -= 5

We have decided to tolerate Luther's minority bourgeois government, a controversial decision in our party due to his economic policies and right-wing leanings.

@kulz_lead
on-arrival: chancellor = "Külz"; chancellor_party = ddp_name

There is no path to an obviously workable majority. President Gessler has given Wilhelm Külz, member of the [+ ddp_name +], the task of forming a government, tired of Brüning's repeated failures to govern and hoping to gain our backing.

- @toleration_kulz: We will tolerate Külz.
- @new_elections: We will have new elections.
- @new_elections_2: We will have new elections.

@toleration_kulz
choose-if: neo_weimar_coalition >= 50 or (neo_weimar_coalition+sapd_r >= 50 and kpd_relation >= 30)
unavailable-subtitle: The parties supporting Külz do not have a majority.
on-arrival: spd_in_government = 0; spd_toleration = 1; ddp_relation += 5; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "I"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = ddp_name; finance_minister_party = "DVP"; agriculture_minister_party = "Z"; pro_republic -= 2

We have decided to tolerate Külz's minority bourgeois government.

@no_majority_seeckt
title: There is no clear majority
view-if: president == "Seeckt" and not (hitler_right_coalition >= 50 and left_strength >= 50 and political_crisis_progress >= 1 and (president == "Hindenburg" or president == "Seeckt") and chancellor == "Brüning")
go-to: martial_law if army_force+sh_force >= 50 and dnvp+sh_force < 50 and reichswehr_loyalty < 0.3 and strife >= 4 and not presidential_powers and chancellor = "Seldte"; switch_to_seldte if chancellor = "Papen"

@switch_to_seldte
on-arrival: chancellor = "Seldte"; chancellor_party = "DNVP"; time_to_election = 3
call: set_next_election_time

Dissatisfied with the performance of Papen, Hans von Seeckt has dissolved the Reichstag for new elections and appointed Franz Seldte of Der Stalhelm as chancellor.

@no_majority_seldte2
title: There is no clear governing majority.
view-if: president == "Seldte"
go-to: no_majority_nsdap

@no_majority_hitler2
title: There is no clear governing majority.
view-if: president == "Hitler" or president == "Göring"
go-to: no_majority_nsdap

@no_majority_eckener
title: There is no clear governing majority.
view-if: (weimar_coalition < 50 or not z_willing) and (grand_coalition < 50 or grand_coalition_failed == 1 or dvp_willing != 1) and president = "Eckener" and far_right_coalition < 50 and (right_coalition < 50 or right_coalition_failed) and bourgeois_coalition < 50
on-arrival: no_majority_elections += 1
go-to: bruning_lead if no_majority_elections < 1; schacht_lead if investigate_far_right < 1 and spd_r < bourgeois_coalition + dnvp_r and no_majority_elections >= 1 and schacht_election != 1; luther_lead if (investigate_far_right >= 1 or spd_r >= bourgeois_coalition + dnvp_r or schacht_election == 1) and no_majority_elections >= 1

@schacht_lead
on-arrival: chancellor = "Schacht"; chancellor_party = "I"; schacht_stalhelm = 1 if sh_banned; sh_banned = 0; welfare -= 1; judicial_reform -= 1 if judicial_reform >= 1; schacht_election = 1; next_election_time = time+3; next_election_month = month + 3; next_election_year = year; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12;

There is no path to an obviously workable majority. President Eckener has decided to appoint former Reichsbank director, Hjalmar Schacht, to the position as head of a technocratic cabinet. Eckener expects us to tolerate him, but he is intolerable to most of the party. [? if schacht_stalhelm : He has already legalized the Stalhelm. ?]

Luckily, the difficult decision has been made for us, as regardless of how we vote a majority of the Reichstag votes against him. New elections have been set for The next elections are set for [+ next_election_month : month +] [+ next_election_year +].

@no_majority_adenauer
title: There is no clear governing majority.
view-if: (weimar_coalition < 50 or not z_willing) and (grand_coalition < 50 or grand_coalition_failed == 1 or dvp_willing != 1) and president = "Adenauer" and far_right_coalition < 50 and (right_coalition < 50 or right_coalition_failed) and bourgeois_coalition < 50
on-arrival: no_majority_elections += 1
go-to: bruning_lead if no_majority_elections < 2; stegerwald_lead if (((z_relation + spd_r < z_r + dvp_r + other_r) or (z_relation + spd_r < national_front and z_national == 1)) and bourgeois_coalition+far_right_coalition >= 50) and no_majority_elections >= 2; wirth_lead if (((z_relation + spd_r >= z_r + dvp_r + other_r and z_national != 1) or (z_relation + spd_r >= national_front)) or bourgeois_coalition+far_right_coalition < 50) and no_majority_elections >= 2

@stegerwald_lead
on-arrival: chancellor = "Stegerwald"; chancellor_party = "Z"

There is no path to an obviously workable majority. President Adenauer has given Adam Stegerwald of the Center Party's right-wing the task of forming a government, tired of Brüning's repeated failures to govern and hoping that Stegerwald would be able to win over support from the far-right. For better or worse, it seems that this has failed, with the far-right unwilling or unable to back Stegerwald. Now the task falls on us to decide the fate of Stegerwald's government.

- @toleration_stegerwald: We will tolerate Stegerwald.
- @new_elections: We will have new elections.
- @new_elections_2: We will have new elections.

@toleration_stegerwald
choose-if: neo_weimar_coalition >= 50 
unavailable-subtitle: The parties supporting Stegerwald do not have a majority.
on-arrival: spd_in_government = 0; spd_toleration = 1; z_relation += 5; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "I"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = ddp_name; finance_minister_party = "DVP"; agriculture_minister_party = "Z"; pro_republic -= 4; left_dissent += 5; labor_dissent += 5; reformist_dissent += 2; center_dissent += 2; workers_spd -= 4; unemployed_spd -= 4

We have decided to tolerate Stegerwald's minority bourgeois government, a controversial decision in our party due to his right-leaning tendencies and conservatism.

@bruning_lead
on-arrival: chancellor = "Brüning"; chancellor_party = "Z"

There is no path to an obviously workable majority. President [+ president +] has given Heinrich Brüning of the Center Party the task of forming a government. As the bourgeois parties lack a majority on their own, the task falls on us to decide the fate of Brüning's government.

- @toleration_bruning: We will tolerate Brüning.
- @new_elections: We will have new elections.
- @new_elections_2: We will have new elections.

@wirth_lead
choose-if: neo_weimar_coalition >= 50
unavailable-subtitle: The parties supporting Wirth do not have a majority.
on-arrival: chancellor = "Wirth"; chancellor_party = "Z"; wirth_lead = 1

There is no path to an obviously workable majority. President Adenauer has given Joseph Wirth of the Center Party the task of forming a government, tired of Brüning's repeated failures to govern and hoping that Wirth would be able to win over our support. Now the question remains, can he?

- @toleration_wirth: We will tolerate Wirth.
- @new_elections: We will have new elections.
- @new_elections_2: We will have new elections.

@black_brown
title: Brüning turns to his right for coalition support.
subtitle: With we on the left firmly refusing to support his government, Brüning considers a deal with the devil.
view-if: hitler_right_coalition >= 50 and left_strength >= 50 and political_crisis_progress >= 1 and (president == "Hindenburg" or president == "Seeckt") and chancellor == "Brüning"
on-arrival: black_brown_seen = 1; nazi_chance = nsdap_r; nazi_chance += socialism; nazi_chance += nationalism; nazi_chance -= pro_republic; nazi_chance += left_coalition if communist_coalition >= 3 and kpd_relation >= 50; nazi_chance += rb_force+rfb_force; nazi_chance -= sa_force; nazi_chance += (radicalization+capital_strike_progress+coup_progress)*political_crisis_progress;
go-to: black_brown_success if nazi_chance >= 200 and not hitler_deported; black_brown_failure if nazi_chance < 150 or (nazi_chance < 200 and not hitler_deported); black_brown_goring if hitler_deported and nazi_chance >= 150

@black_brown_success
on-arrival: chancellor = "Hitler"; chancellor_party = "NSDAP"; sa_militancy -= 0.1 if rb_force < 15; sa_strength *= 0.9 if rb_force < 15; z_relation -= 25; dvp_relation -= 25; ddp_relation -= 15

Negotiations are long and tense, with Hitler refusing to concede the position of chancellor. Eventually, somehow, to prevent more elections in which we socialists may gain, the Center Party agrees to allow Hitler as chancellor in return for the majority of cabinet positions, the gradual disarmament of the SA, and assurances of safety for Catholics. Brüning reluctantly steps aside, and Hitler is sworn in as chancellor with [+ hitler_right_coalition +]% behind him.

- @hitler_takes_power: What do we do now?
- @secret_path: What do we do now?

@secret_path
view-if: hitler_seen or presidential_powers
on-arrival: rb_militancy -= 0.1 if rb_militancy >= 0.1; reichswehr_loyalty = 0 if reichswehr_loyalty > 0
go-to: slow_consolidation if hitler_seen

[? if hitler_seen : This being Hitler's second tenure as chancellor, he is much more cautious, instead opting to slowly consolidate power. ?][? if presidential_powers : The limitations on presidential powers prevent Hitler from immediately consolidating power. ?]

@slow_consolidation
on-arrival: sneaky_hitler = 1; impending_fire = 1; time_to_election = 3; z_relation += 10 if not uf_formed; nazi_peak = 0; nazi_decline = 0; labor_dissent += 10 if labor_strength < center_strength; rb_strength -= 200*(dissent); new_middle_nsdap += 4; old_middle_nsdap += 3; catholics_nsdap += 3; rural_nsdap += 4; workers_nsdap += 6; unemployed_nsdap += 10; budget -= 3; economic_growth += 1; economic_growth += 1.5 if economic_growth < 0; inflation += 2; inflation += 1.5 if inflation < 0; unemployed -= 2; unemployed -= 4 if unemployed >= 30
call: set_next_election_time

A public works program is initiated by the Nazis, alongside subsidies for struggling businesses. Meanwhile, the Nazis contact the ADGB, attempting to turn them against us. Pre-empting a potential betrayal by the Center Party, Hitler dissolves the Reichstag, calling new elections for [+ next_election_month : month +] [+ next_election_year +].

Hitler is sure to have ideas once the Reichstag next convenes...

@black_brown_failure
on-arrival: bb_failed = 1; rural_nsdap += 3; old_middle_nsdap += 3; new_middle_nsdap += 3; workers_spd += 4*(1-dissent); workers_z -= 3; unemployed_nsdap -= 3*(1-dissent); workers_nsdap -= 3*(1-dissent);

As expected, the negotiations go nowhere. The Nazis are too intransingent and uncompromising for the bürgerblock to stomach, and inversely the bürgerblock are too demanding and picky for the Nazis. 

- @no_majority: Thus, there is no clear majority.

@black_brown_goring
on-arrival: in_right_coalition = 1; nsdap_toleration = 1; judicial_reform -= 2; reichswehr_loyalty -= 0.1; pro_republic -= 5; sa_militancy -= 0.05; new_middle_nsdap += 3; old_middle_nsdap += 6; rural_nsdap += 6; new_middle_spd += 4*(1-dissent); workers_nsdap -= 4*(1-dissent); unemployed_nsdap -= 6*(1-dissent); coup_progress += political_crisis_progress; coup_progress += 2; collapse_month = month+5; collapse_year = year; collapse_year += 1 if collapse_month > 12; collapse_month -= 12 if collapse_month > 12

Somehow, the NSDAP has agreed to join Brüning's government, giving it [+ hitler_right_coalition +]% of the Reichstag. The breaking of the de-facto cordon sanitaire against them significantly helps to legitimize them as a constructive political party in the country, though their association with Brüning's austerity measures simultaneously helps us to break their support amongst the working class.

Immediately, democratic reforms are undone as the cabinet sets to work sending Germany back into the dark ages. For now, democracy is being preserved, but the Nazis are sure to take their first opportunity to fully destroy it.

@schleicher_coalition
title: Schleicher attempts to hold on to power.
view-if: dne_formed and chancellor == "Schleicher"
subtitle: DNE ([+ dne_r +]%)
go-to: schleicher_majority if dne_r >= 50 or constructive_vonc; schleicher_negotiate if dne_r < 50 and not constructive_vonc
on-arrival: schleicher_support = dne_r

@schleicher_majority
on-arrival: schleicher_victory = 1

With the backing of the DNE behind him, Schleicher is able to hold onto power. [? if constructive_vonc and dne_r < 50 : This is thanks to the constructive motion of no confidence rule, which allows Schleicher to govern in a minority cabinet. ?]

- @game_over: Schleicher has won.

@schleicher_negotiate
go-to: schleicher_menu

As Kurt von Schleicher has fallen short of a majority, he must engineer another "Querfront" with which to prop himself up in the Reichstag. With the DNE as his basis, he considers his options.

@schleicher_menu

- @schleicher_spd: The SPD?
- @schleicher_ddp: The [+ ddp_name +]?
- @schleicher_z: The Center Party?
- @schleicher_dvp: The DVP?
- @schleicher_dnvp: The DNVP?
- @no_majority_schleicher: Schleicher fails to cobble together a majority.
- @schleicher_wins: Schleicher has managed to cobble together a majority.

@schleicher_spd
view-if: schleicher_support < 50 
max-visits: 1

Kurt von Schleicher has reached out once more to our party, promising further public works and even some ministries should we support his government.

- @accept_schleicher: Accept.
- @refuse_schleicher: Refuse! This was a temporary alliance, we will not support a dictator!

@accept_schleicher
on-arrival: schleicher_support += spd_r; schleicher_support -= dnvp_r if dnvp_support_schleicher; spd_support_schleicher = 1; left_dissent += 20; center_dissent += 20; reformist_dissent += 20; neorevisionist_dissent += 20; labor_dissent -= 5
go-to: schleicher_menu

Despite the protests of our party, we continue to support Schleicher. 

@refuse_schleicher
on-arrival: labor_dissent += 5; spd_refused_schleicher = 1
go-to: schleicher_menu

Despite the protests of the ADGB, we refuse to support Schleicher a second time.

@schleicher_ddp
view-if: schleicher_support < 50
choose-if: ddp_name != "DDP" and pro_republic < 40
unavailable-subtitle: The DDP is unwilling.
max-visits: 1
on-arrival: schleicher_support += ddp_r; ddp_support_schleicher = 1
go-to: schleicher_menu

The [+ ddp_name +] has agreed to back Schleicher.

@schleicher_z
view-if: schleicher_support < 50
choose-if: pro_republic < 50 and z_party_leader != "Stegerwald" and (unemployed > 30 or not reichskonkordat)
unavailable-subtitle: The DDP is unwilling.
max-visits: 1
on-arrival: schleicher_support += z_r; z_support_schleicher = 1
go-to: schleicher_menu

The Center Party has agreed to back Schleicher[? if not reichskonkordat : in return for a promise of his support for a Reichskonkordat?].

@schleicher_dvp
view-if: schleicher_support < 50
choose-if: pro_republic < 55 and nationalism < 55
unavailable-subtitle: The DVP is unwilling.
max-visits: 1
on-arrival: schleicher_support += (dvp_r+other_r); dvp_support_schleicher = 1
go-to: schleicher_menu

The DVP has agreed to back Schleicher, alongside the minor other parties.

@schleicher_dnvp
view-if: schleicher_support < 50
choose-if: dnvp_reform or (not spd_support_schleicher or sapd_formed)
unavailable-subtitle: The DNVP is unwilling.
max-visits: 1
on-arrival: schleicher_support += dnvp_r; dnvp_support_schleicher = 1
go-to: schleicher_menu

The DNVP has agreed to back Schleicher[? if not sapd_formed and not dnvp_reform : in return for an end of his cooperation with us. ?]

@schleicher_wins
view-if: schleicher_support >= 50
subtitle: The Red General secures his future.
on-arrival: schleicher_victory = 1

Having successfully cobbled together an odd mismatch across the political spectrum, Schleicher has won the vote of no confidence against him pushed by the energetic KPD. With the political machine of the DNE behind him, Hindenburg's trust, and the tenuous support of the Reichswehr, Schleicher's future is secure. Hitler has lost, but at what cost?

- @game_over: Game over.

@no_majority_marx
title: There is no clear governing majority.
view-if: (weimar_coalition < 50 or z_willing != 1) and (grand_coalition < 50 or grand_coalition_failed == 1 or dvp_willing != 1) and president = "Marx" and far_right_coalition < 50 and (right_coalition < 50 or right_coalition_failed) and (hitler_right_coalition < 50 or left_strength < 50 or political_crisis_progress < 1 or chancellor != "Brüning" or bb_failed) and bourgeois_coalition < 50 and not (dne_formed and chancellor == "Schleicher")
on-arrival: no_majority_elections += 1
go-to: no_majority_bracht if chancellor = "Bracht"; no_majority_scholz if chancellor = "Scholz"; no_majority_bruning2 if chancellor = "Brüning" or chancellor = "Wirth"; no_majority_spd2 if chancellor_party = "SPD" or n_elections <= 1; no_majority_nsdap if chancellor == "Hitler"

@no_majority_bruning2
go-to: no_majority_bruning_toleration if ((no_majority_elections <= 3 and anti_democratic_coalition <= 50 and marx_relation >= 0) or marx_relation >= 40); marx_appoints_bracht if ((no_majority_elections > 3 or anti_democratic_coalition > 50 or marx_relation < 0) and marx_relation < 40)

@marx_appoints_bracht
on-arrival: old_middle_nsdap += 5; rural_nsdap += 5; unemployed_nsdap += 5; new_middle_nsdap += 5; marx_relation -= 5; bracht_r = 0; chancellor = "Bracht"; chancellor_party = "Z" if z_relation < 30; chancellor_party = "I" if z_relation >= 30; sa_banned = 0; pro_republic -= 6; finance_minister_party = "I"; interior_minister_party = "I"; economic_minister_party = "I"; labor_minister_party = "I"; foreign_minister_party = "I"; agriculture_minister_party = "I"; reichswehr_minister = "I"; nazi_chance = nsdap_r; nazi_chance += socialism; nazi_chance += nationalism; nazi_chance -= pro_republic; nazi_chance += left_coalition if communist_coalition >= 3 and kpd_relation >= 50; nazi_chance += rb_force+rfb_force; nazi_chance -= sa_force; nazi_chance += (radicalization+capital_strike_progress+coup_progress)*political_crisis_progress; bracht_r = dvp_r; bracht_r += dnvp_r if z_relation < 25; bracht_r += z_r if z_relation < 45; bracht_r += nsdap_r if nazi_chance >= 200
go-to: bracht_falls if bracht_r < 50; bracht_wins if bracht_r >= 50

There is no path to a majority government. President Marx is tired of the chaos, and appoints Franz Bracht as chancellor instead of Brüning, hoping that Bracht would be able to form a coalition, or at least receive toleration, from the NSDAP. Bracht is significantly to the right of the majority of the Center Party.

Bracht is immediately faced with a vote of no confidence. [+ bracht_r +]% vote in support of his government.

@bracht_falls
on-arrival: marx_relation += 10; next_election_time = time+3; next_election_month = month + 3; next_election_year = year; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12;

Bracht has failed to win a majority. Seeing no majority in the Reichstag, Wilhelm Marx has called new elections, which have been set for [+ next_election_month : month +] [+ next_election_year +].

@bracht_wins
on-arrival: pro_republic -= 5; nationalism += 5; coup_progress += 3; reichswehr_loyalty -= 0.1; judicial_reform -= 2

Bracht has somehow won a majority in the Reichstag. Democratic reforms that we undertook in the past are undone. Let us hope President Marx's confidence in Bracht wears thin.

@no_majority_bracht
on-arrival: {!
var hitler_points = 0;
var braun_points = 0;
var bruning_points = 0;

hitler_points += Q.nsdap_r;
hitler_points += Q.nationalism;
hitler_points += Q.anti_democratic_coalition;
hitler_points -= Q.sa_force;
hitler_points += Q.kpd_r;
hitler_points -= Q.marx_relation;

braun_points += Q.spd_r;
braun_points += Q.socialism;
braun_points += Q.pro_republic;
braun_points += Q.weimar_coalition;
braun_points -= Q.rb_force;
braun_points += Q.marx_relation;
braun_points -= Q.kpd_relation;
braun_points += Q.dvp_relation;

bruning_points += Q.z_r;
bruning_points += Q.neo_weimar_coalition;
bruning_points += Q.pro_republic;
bruning_points += (100-Q.socialism);
bruning_points += (100-Q.nationalism);
bruning_points -= Q.no_majority_elections;
if (braun_points >= hitler_points) {
    bruning_points += hitler_points/4;
} else {
    bruning_points += braun_points/4;
}

Q.braun_chosen = 0;
Q.hitler_chosen = 0;
Q.bruning_chosen = 0;

if (braun_points >= hitler_points && braun_points >= bruning_points) {
    Q.braun_chosen = 1;
} else if (hitler_points >= braun_points && hitler_points >= bruning_points) {
    Q.hitler_chosen = 1;
} else {
    Q.bruning_chosen = 1;
}
!}

With there still being no majority for Bracht, Wilhelm Marx dismisses him and looks to alternative candidates. 

- @marx_hitler: Adolf Hitler?
- @marx_braun: Otto Braun?
- @marx_bruning: Heinrich Brüning?
- @marx_wirth: Joseph Wirth?

@marx_hitler
subtitle: Perhaps the Nazis can be tamed and demystified?
choose-if: hitler_chosen
unavailable-subtitle: Wilhelm Marx is not willing to appoint Hitler.
on-arrival: chancellor = "Hitler"; chancellor_party = "NSDAP"; marx_appointed_hitler = 1
go-to: slow_consolidation

Wilhelm Marx has very reluctantly appointed Adolf Hitler of the NSDAP as chancellor.

@marx_braun
subtitle: Perhaps we can lead Germany out of its crisis.
choose-if: braun_chosen
unavailable-subtitle: Wilhelm Marx is not willing to appoint Braun.
on-arrival: chancellor = "Braun"; chancellor_party = "SPD"; spd_in_government = 1; pro_republic -= 5; foreign_minister_party = "SPD"; interior_minister_party = "SPD"; justice_minister_party = "SPD"; labor_minister_party = "SPD"; reichswehr_minister_party = "SPD"; economic_minister_party = "SPD"; finance_minister_party = "SPD"; agriculture_minister_party = "SPD"; z_prussia = 1 if spd_prussia; braun_removed = 1 if spd_prussia; spd_prussia = 0

Wilhelm Marx has appointed Otto Braun as chancellor. [? if braun_removed : He first had to resign as Minister-President of Prussia however. ?]

- @emergency_no_confidence

@marx_bruning
subtitle: Perhaps Brüning can manage to form a government this time?
choose-if: bruning_chosen and (z_relation+spd_r < 100)
unavailable-subtitle: Wilhelm Marx is not willing to appoint Brüning.
on-arrival: chancellor = "Brüning"; chancellor_party = "Z"
go-to: no_majority_bruning_toleration

Wilhelm Marx has appointed Heinrich Brüning as head of a minority government.

@marx_wirth
subtitle: Perhaps the answer lies in the left-wing of the Center Party?
choose-if: bruning_chosen and (z_relation+spd_r >= 100)
unavailable-subtitle: Wilhelm Marx is not willing to appoint Wirth.

Wilhelm Marx has appointed Joseph Wirth as head of a minority government.

- @toleration_wirth: We will tolerate Wirth.
- @new_elections: We will have new elections.
- @new_elections_2: We will have new elections.



@no_majority_spd2
on-arrival: chancellor = "Brüning"; chancellor_party = "Z"

There is no path to an obviously workable majority. Since President Marx does not see potential for a successful minority or caretaker SPD government, his preferred chancellor is Heinrich Brüning of the Center Party.

- @toleration_bruning: We will tolerate Brüning.
- @new_grand_coalition: We will try to form a new grand coalition.
- @new_elections: We will have new elections.
- @new_elections_2: We will have new elections.

@no_majority
title: There is no clear governing majority.
view-if: (weimar_coalition < 50 or z_willing != 1) and (grand_coalition < 50 or grand_coalition_failed == 1 or dvp_willing != 1) and president = "Hindenburg" and far_right_coalition < 50 and (right_coalition < 50 or right_coalition_failed or dvp_willing != 1) and (hitler_right_coalition < 50 or left_strength < 50 or political_crisis_progress < 1 or chancellor != "Brüning" or bb_failed) and bourgeois_coalition < 50 and not (dne_formed and chancellor == "Schleicher")
on-arrival: no_majority_elections += 1
go-to: no_majority_scholz if chancellor = "Scholz"; no_majority_bruning if chancellor = "Brüning" or chancellor = "Wirth"; no_majority_papen if chancellor = "Papen" and no_majority_papen_elections == 0; no_majority_papen_2 if chancellor == "Papen" and no_majority_papen_elections > 0 and not resisted_hitler; no_majority_schleicher if chancellor = "Schleicher"; no_majority_spd if chancellor_party = "SPD" or chancellor == "Marx"; no_majority_seldte if chancellor == "Seldte"; no_majority_nsdap if hitler_seen and chancellor_party == "NSDAP"; no_majority_bruning_back if chancellor == "Papen" and resisted_hitler

# TODO: if there is an anti-democratic coalition majority, you should appoint Papen.

# either new elections, or Hindenburg appoints either Brüning, Papen, Schleicher, or Hitler.

@no_majority_scholz
on-arrival: chancellor = "Brüning"; chancellor_party = "Z"

With health issues plaguing Ernst Scholz and his seeming inability to command a majority, the mantle has transferred to Brüning of the Center Party.

- @toleration_bruning: We will tolerate Brüning.
- @new_grand_coalition: We will try to form a new grand coalition.
- @new_elections: We will have new elections.
- @new_elections_2: We will have new elections.

@no_majority_bruning_back
on-arrival: chancellor = "Brüning"; chancellor_party = "Z"

With the Reichstag still lacking a majority for Papen, and the failure of Hitler to consolidate power having seemingly spooked Hindenburg into, for the time being, ceasing his reactionary fantasies, Brüning is reappointed chancellor.

- @toleration_bruning: We will tolerate Brüning.
- @new_grand_coalition: We will try to form a new grand coalition.
- @new_elections: We will have new elections.
- @new_elections_2: We will have new elections.

@no_majority_nsdap

Following the election, despite heavy rigging, [+ chancellor +] still lacks a majority. However, with many communists and even some socialists jailed, can a no confidence vote still pass?

- @can_it_pass: Let's see.

@can_it_pass
on-arrival: against_hitler += spd_r*(1-(left_strength/100)); against_hitler += ddp_r if ddp_relation >= 55; against_hitler += z_r if z_relation >= 55; against_hitler += dvp_r if dvp_relation >= 60
go-to: far_right_incumbent if against_hitler < 50; hitler_defeated if against_hitler >= 50

[? if against_hitler < 50 : The motion of no confidence against Hitler is defeated, allowing the far-right coalition to, despite lacking a majority, govern nonetheless. ?]

@hitler_defeated
on-arrival: chancellor = "Papen"; chancellor_party = "I"; time_to_election = 3; workers -= 2; catholics -= 1; workers_nsdap += 4; new_middle_nsdap += 8; old_middle_nsdap += 8; rural_nsdap += 8; catholics_nsdap += 4; unemployed_nsdap += 6; old_middle_dnvp += 5; rural_dnvp += 5; new_middle_dnvp += 4; catholics_dnvp += 4
call: set_next_election_time

The motion of no confidence in Hitler succeeds, and he is replaced by vice-chancellor Papen, who calls snap elections. The Nazis, still controlling most state governments after the Reichstag Fire Decree, continue to consolidate, hoping to gain in the next elections, while Papen looks to the Reichswehr in the hopes of declaring martial law.

@no_majority_seldte
go-to: martial_law if army_force+sh_force >= 50 and dnvp+sh_force < 50 and reichswehr_loyalty < 0.3 and strife >= 4 and not presidential_powers and (president == "Hindenburg" or (president == "Gessler" and reichswehr_loyalty < 0.15)); switch_to_papen if president != "Gessler" and (harzburg_r < 25 or seldte_times >= 1) and not (army_force+sh_force >= 50 and dnvp+sh_force < 50 and reichswehr_loyalty < 0.3 and strife >= 4 and not presidential_powers and (president == "Hindenburg" or (president == "Gessler" and reichswehr_loyalty < 0.15))); seldte_continue if (harzburg_r >= 25 and seldte_times < 1) and not (army_force+sh_force >= 50 and dnvp+sh_force < 50 and reichswehr_loyalty < 0.3 and strife >= 4 and not presidential_powers and (president == "Hindenburg" or (president == "Gessler" and reichswehr_loyalty < 0.15)))

@switch_to_papen
on-arrival: chancellor = "Papen"; chancellor_party = "I"; next_election_time = time+3; next_election_month = month + 3; next_election_year = year; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; sa_banned = 0; pro_republic -= 8; finance_minister_party = "I"; interior_minister_party = "I"; economic_minister_party = "I"; labor_minister_party = "I"; foreign_minister_party = "I"; agriculture_minister_party = "I"; reichswehr_minister = "I";

Following the elections, there is no clear majority. Rather than appoint a continuation of the Harzburg Cabinet however, disappointed with their performance, Hindenburg appoints the independent Franz von Papen, who calls snap elections in a gambit to gain majority Reichstag support through a coalition with the Nazis. Papen is a indenpendent formerly from the Center Party, and is significantly to the right of the majority of the Center Party.

@seldte_continue
on-arrival: seldte_times += 1; dvp_relation -= 5; z_relation -= 5; harzburg_r = dnvp_r; harzburg_r += other_r; harzburg_r += 3; harzburg_r += dvp_r if dvp_relation + pro_republic < 85; harzburg_r += z_minus_bvp_r if z_relation + pro_republic < 60 and z_party_leader != "Joos"; stalhelm_strength += 50; stalhelm_militancy += 0.02; reichswehr_loyalty -= 0.05 if reichswehr_loyalty >= 0.05
go-to: seldte_minority if harzburg_r < 50; seldte_majority if harzburg_r >= 50

@seldte_minority
on-arrival: time_to_election = 3; old_middle_nsdap -= 3; rural_nsdap -= 2; old_middle_dnvp += 3; rural_dnvp += 3; new_middle_dnvp += 3; workers_spd -= 3; workers_kpd -= 3
call: set_next_election_time

After the election, the Harzburg Cabinet still lacks a majority. The Reichstag is dissolved for elections once more, while Seldte continues his agendas. 

@seldte_majority
on-arrival: workers_spd -= 5; workers_kpd -= 5; workers_dnvp += 5; new_middle_dnvp += 7; old_middle_nsdap -= 5; rural_nsdap -= 5; old_middle_dnvp += 8; rural_dnvp += 8; stalhelm_strength += 50

Somehow, the Harzburg Cabinet has this time secured a majority mandate. The continued regression of Germany continues, as does suppression of the left.

- @fight_harzburg: Rise up before it is too late!
- @nothing_to_do: There's nothing we can do.

@fight_harzburg
on-arrival: spd_rebel = 1
go-to: civil_war

@nothing_to_do
on-arrival: rb_militancy -= 0.05; rb_strength -= 100; left_dissent += 5; unemployed_nsdap += 5

There is, unfortunately, nothing we can do. We will have to bide our time until we can recapture power.

@martial_law

Seeing the continuing collapse of order in Germany, and convinced that they would be able to quell any resistance, President [+ president +] and Chancellor Seldte have come together to declare a state of martial law in Germany. Though this is unconstitutional, the courts have little sway over the enthusiastic army.

- @seldte_takes_power: What now?

@seldte_takes_power
subtitle: The beginning of the end.

The destruction of German democracy has begun, not at the hands of the Nazis but at the hands of the reactionaries who wish to see a return to the days of the German Empire. If we do not fight back now, then we may never get another chance, and all we as a party have stood for will be gone in an instant.

- @fight_harzburg: Mobilize our forces and declare a general strike!
- @reactionary_victory: We have lost.

@reactionary_victory

Party offices are raided by army and police forces. Our leaders are rounded up and arrested. Any and all of our reforms are undone, and a police state under the army's control is cemented. The Nazis have failed to take power, but we cannot take much comfort in the fact when a dictatorial fascist government rules all the same. Perhaps in the far future, we may have our moment in the sun once more. We will have a duty to do better then than we have now.

- @game_over: Game over.


@no_majority_spd
on-arrival: chancellor = "Brüning"; chancellor_party = "Z"

There is no path to an obviously workable majority. Since President Hindenburg is unwilling to work with a minority or caretaker SPD government, his preferred chancellor is Heinrich Brüning of the Center Party, who could lead a "presidential" government.

- @toleration_bruning: We will tolerate Brüning.
- @new_grand_coalition: We will try to form a new grand coalition.
- @new_elections: We will have new elections.
- @new_elections_2: We will have new elections.

@no_majority_hitler
title: Hitler appoints himself chancellor.
on-arrival: chancellor = "Hitler"; chancellor_party = "NSDAP"; foreign_minister_party = "NSDAP"; interior_minister_party = "NSDAP"; justice_minister_party = "NSDAP"; labor_minister_party = "NSDAP"; reichswehr_minister_party = "NSDAP"; economic_minister_party = "NSDAP"; finance_minister_party = "NSDAP"; agriculture_minister_party = "NSDAP"; spd_in_government = 0; spd_toleration = 0
view-if: president == "Hitler" and presidential_powers and far_right_coalition < 50

Despite the weakened powers of the presidency, appointing a chancellor has remained the duty of the president and Hitler wastes no time in appointing himself chancellor in an unprecedented move.

He has promised a campaign of revenge against the socialists, and the Nazis have already spilled blood. In his published manifesto, he promises a campaign of unspeakable horror against Jews, and endless wars of violent expansion that will ruin all of Europe.

What are we to do now?

- @fight: We will fight on the streets to prevent Hitler from taking power.
- @vote_against: We will vote against the nazis in the Reichstag.
#- @judicial_protest: We will file a complaint to the judiciary that this goes against the spirit of the constitution.


@fight
on-arrival: spd_rebel = 1

Do we realize that this means civil war, against the full force of the state?

- @civil_war: Yes.
- @vote_against: We are not ready for war. Instead, we will vote against the nazis in the Reichstag.

@vote_against
on-arrival: z_no_confidence = 1 if z_relation > 60 or z_party_leader == "Joos" and z_relation > 40; other_no_confidence = 1 if (z_relation + dvp_relation)/2 > 70; no_confidence_votes = spd_r + kpd_r + sapd_r + ddp_r; no_confidence_votes += z_r if z_no_confidence; no_confidence_votes += other_r + dvp_r if other_no_confidence; no_confidence_succeeds = 1 if no_confidence_votes > 50

A motion of no confidence is called against Hitler's government. 

In total, [+ no_confidence_votes +]% of the Reichstag is against Hitler. [? if no_confidence_succeeds : We have won the vote of no confidence. ?]

- @no_confidence_elections_new_hitler: There will be new elections.
- @no_confidence_win_hitler: Hitler has won, for now.

@no_confidence_elections_new_hitler
on-arrival: {!
Q.no_majority_elections += 1;
Q.next_election_month = Q.month + 6;
Q.next_election_time = Q.time + 6;
if (Q.next_election_month > 12) {
    Q.next_election_month -= 12;
    Q.next_election_year = Q.year + 1;
} else {
    Q.next_election_year = Q.year;
}
Q.pro_republic -= 2;
Q.workers_spd += 1;
Q.workers_kpd += 2;
Q.new_middle_spd += 1;
Q.old_middle_spd += 1;
Q.old_middle_dnvp += 2;
Q.rural_spd += 1;
Q.rural_dnvp += 2;
Q.catholics_spd += 1;
Q.workers_nsdap -= 1;
Q.old_middle_nsdap -= 2;
Q.new_middle_nsdap -= 2;
Q.rural_nsdap -= 2;
Q.unemployed_nsdap -= 1;
Q.nazi_rage_progress += 2;
!}
view-if: no_confidence_votes > 50

The next elections are set for [+ next_election_month : month +] [+ next_election_year +].


@no_confidence_win_hitler
view-if: no_confidence_votes <= 50
on-arrival: judicial_reform -= 3 if judicial_reform >= 3; judicial_reform -= 2 if judicial_reform = 2; judicial_reform -= 1 if judicial_reform = 1; interior_police_loyalty -= 0.2; workers_nsdap += 2; unemployed_nsdap += 5; new_middle_nsdap += 3; old_middle_nsdap += 3; rural_nsdap += 3; pro_republic -= 5; nazi_rage_progress -= 2

Hitler has won the vote of no confidence. He will continue to govern democratically until he is able to seize total control.


# - if NSDAP is the largest party and the 

@no_majority_bruning
go-to: no_majority_bruning_toleration if (no_majority_elections <= 3 and anti_democratic_coalition <= 50); hindenburg_appoints_papen if no_majority_elections > 3 or anti_democratic_coalition > 50


@hindenburg_appoints_papen
on-arrival: chancellor = "Papen"; chancellor_party = "I"; next_election_time = time+3; next_election_month = month + 3; next_election_year = year; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; sa_banned = 0; pro_republic -= 8; finance_minister_party = "I"; interior_minister_party = "I"; economic_minister_party = "I"; labor_minister_party = "I"; foreign_minister_party = "I"; agriculture_minister_party = "I"; reichswehr_minister = "I"; papen_two_term = 1;

There is no path to a majority government. President Hindenburg is tired of the chaos, and appoints Franz von Papen as chancellor instead of Brüning. Papen is a indenpendent formerly from the Center Party, and is significantly to the right of the majority of the Center Party.

Papen is immediately faced with a vote of no confidence, given his unpopularity. However the SPD votes, he is guaranteed to fall, due to the unanimous hatred against him from all other parties.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@no_majority_bruning_toleration
on-arrival: no_majority_bruning_elections += 1

There is no path to a majority government. One option would be to have a continuation of the current Brüning government; if we tolerate it, then it would retain a majority vote against no-confidence.

Alternatively, we could try to form a "new grand coalition" of all constitutional parties except the NSDAP, KPD, and DNVP. The stumbling block are the minor conservative parties such as the "Economic Party of the Middle Class", who detest any hint of socialism and would ruin the country before working with Marxists.

The third option is to have new elections.

- @toleration_bruning: We will tolerate Brüning.
- @new_grand_coalition: We will try to form a new grand coalition.
- @new_elections: We will have new elections.
- @new_elections_2: We will have new elections.
- @unity_government


@new_grand_coalition
title: A "New Grand Coalition" of all the constitutional parties?
subtitle: SPD + Z + [+ bvp_name +] + [+ ddp_name +] + DVP + Others ([+ neo_weimar_coalition +]%)
view-if: neo_weimar_coalition >= 50 and grand_coalition < 50
choose-if: z_relation >= 50 and dvp_relation >= 30 and president != "Hitler"
unavailable-subtitle: SPD + Z + [+ bvp_name +] + [+ ddp_name +] + DVP + Others ([+ neo_weimar_coalition +]%) - [? if z_relation < 50 : We do not have adequate relations with the Center Party. ?][? if dvp_relation < 30 : We do not have adequate relations with the DVP. ?][? if president == "Hitler" : Hitler refuses to appoint a chancellor under such a governing arrangement. ?]

How do we convince vehemently anti-socialist parties to allow us to form a majority coalition? Our largely benevolent relations with the Center and DVP may allow us some leverage.

- @use_resources
- @toleration_bruning: No. We will tolerate a minority government.
- @new_elections: No. We will have new elections.
- @unity_government

@use_resources
title: Use our resources to smooth over the creation of a coalition.
subtitle: -2 resources
unavailable-subtitle: Not enough resources (need 2).
choose-if: resources >= 2
on-arrival: resources -= 2; spd_in_government = 1; in_grand_coalition = 1; left_dissent += 4;
achievement: constitutional_coalition

We transfer some of our campaign funds to our putative coalition partners. Some would call this corruption, but perhaps it is necessary to save our democracy.

Now, we have a new grand coalition, a "Constitutional Coalition" of all the non-fascist and non-communist parties. Leftists in our party are somewhat skeptical of this arrangement. 


Who should be chancellor?

- @join_bruning: For better or for worse, it must be Heinrich Brüning of the Center.
- @braun_chancellor_new: Otto Braun for Chancellor?

@braun_chancellor_new
subtitle: This will reduce our leverage in the ministries.
view-if: not historical_mode or not spd_prussia
on-arrival: leverage -= 10; chancellor = "Braun"; chancellor_party = "SPD"
go-to: ministries

We have assembled a new coalition under Otto Braun.


@unity_government
title: The Center Party offers a "Government of National Unity" under Chancellor Joseph Wirth.
subtitle: SPD + Z + [+ bvp_name +] + [+ ddp_name +] + DVP + Others ([+ neo_weimar_coalition +]%)
unavailable-subtitle: SPD + Z + [+ bvp_name +] + [+ ddp_name +] + DVP + Others ([+ neo_weimar_coalition +]%) - [? if president == "Hitler" : Hitler refuses to appoint a chancellor under such a governing arrangement. ?]
choose-if: president != "Hitler"
view-if: z_party_leader == "Joos" and neo_weimar_coalition >= 50 and weimar_coalition < 50  and z_relation >= 10 and (anti_democratic_coalition + unemployed) >= 50 and unity_refused != 1

The Center Party leader, Joseph Joos, clearly sees the threat from the ascension of the anti-democratic extremist parties, and thus tries to negotiate a "Government of National Unity" to save the republic. This coalition would be led by Joseph Wirth, a left-leaning member of the Center Party and former chancellor, and would encompass all of the constitutional parties. Our role in this government might be limited, however.

- @unity_government_2
- @dvp_refuse
- @reject_unity_government: No, we will not accept this offer.

@unity_government_2
view-if: dvp_willing or dvp_relation >= (100-pro_republic)
title: Accept the offer!
on-arrival: chancellor = "Wirth"; chancellor_party = "Z"; spd_in_government = 1; in_grand_coalition = 1; in_unity_government = 1; leverage = 15; z_relation += 5; dvp_relation += 4
achievement: constitutional_coalition
go-to: ministries

We have formed a new unity government of all the constitutional parties! We have a limited choice in ministries for this government.

@dvp_refuse
view-if: dvp_willing != 1 and dvp_relation < (100-pro_republic) and unity_refused != 1
title: Accept the offer!
on-arrival: z_relation += 5; dvp_relation -= 5; unity_refused = 1

Despite our agreement, crucial components in the plan such as the DVP have become unwilling to enter into such a government. They are too set in their antidemocratic ways. We will have to find an alternative.

- @no_majority: Thus, there is no clear majority.
- @brauns_dilemma
- @coalition_menu: Are there any alternative governments we can form?

@reject_unity_government
on-arrival: z_relation -= 8; ddp_relation -= 5; dvp_relation -= 5

Our rejection of the unity government has hurt our relations with the bourgeois and center-right parties.

- @brauns_dilemma
- @toleration_bruning: We will tolerate a minority government led by Brüning.
- @toleration_wirth: We will tolerate a minority government led by Joseph Wirth.
- @new_elections: There will be new elections.


@toleration_wirth
view-if: z_party_leader = "Joos" or wirth_lead = 1
choose-if: president != "Hindenburg"
unavailable-subtitle: President Hindenburg will not support a minority government led by Joseph Wirth.
on-arrival: chancellor = "Wirth"; chancellor_party = "Z"; spd_in_government = 0; spd_toleration = 1; z_relation += 5; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "I"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = ddp_name; finance_minister_party = "DVP"; agriculture_minister_party = "Z"; pro_republic -= 2;

We have supported a minority government led by the Center Party under Chancellor Joseph Wirth.



@no_majority_papen
on-arrival: pro_republic -= 10; next_election_time = time+3; next_election_month = month + 3; next_election_year = year; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; no_majority_papen_elections += 1; nazi_peak = 1; nazi_peak_timer = 2; nazi_peak_timer += (nsdap_votes/5); workers_ndap -= 6; new_middle_nsdap -= 6; old_middle_nsdap -= 6; rural_nsdap -= 6; unemployed_nsdap -= 6; workers_nsdap -= 3 if largest_party == "NSDAP"; unemployed_nsdap -= 3 if largest_party == "NSDAP"; old_middle_nsdap -= 3 if largest_party == "NSDAP"; new_middle_nsdap -= 3 if largest_party == "NSDAP"; rural_nsdap -= 3 if largest_party == "NSDAP"; 
go-to: papen_second_term if papen_two_term

After the elections, Papen is still deeply unpopular, without any hint of coalition support, and there is a vote of no confidence called against him almost immediately. No matter how we vote, the measure would pass. The Reichstag is dissolved once more.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +]. [? if largest_party == "NSDAP" : With the Nazis, despite becoming the largest party, failing to form a government, their movement loses steam and gives ground to the KPD and DNVP. ?]

Nazi refusal to support Papen's government has lead to confusion and disappointment amongst its far-right base.

@papen_second_term
on-arrival: judicial_reform -= 2 if judicial_reform >= 2; reichswehr_loyalty -= 0.05; workers_dnvp += 2; new_middle_dnvp += 5; old_middle_dnvp += 6; rural_dnvp += 6; catholics_dnvp += 3; sh_strength += 50; workers_spd -= 3; workers_kpd -= 3; unemployed_kpd -= 2; dvp_relation -= 5; 

Papen has spent this time passing, with Hindenburg's consent, decree after decree that has launched his primary backers in the DNVP back into the forefront of reactionary politics and dismantled our democratic reforms across the country.

@no_majority_papen_2
on-arrival: chancellor = "Schleicher"; pro_republic -= 10; next_election_time = time+5; next_election_month = month + 5; next_election_year = year; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; finance_minister_party = "I"; interior_minister_party = "I"; economic_minister_party = "I"; labor_minister_party = "I"; foreign_minister_party = "I"; agriculture_minister_party = "I"; reichswehr_minister = "I";

There is no path to a majority government. Hindenburg is displeased with Papen, and calls on him to resign. Now, former army general Kurt von Schleicher has been appointed chancellor, again as part of a presidential government.

Schleicher's government is deeply unpopular among all parties of the Reichstag. Despite his support for a coalition with the NSDAP, the Nazis want power all to themselves, without Schleicher.[? if largest_party == "NSDAP" : However, the Nazis failure again to form a government leads to further loss of momentum. ?]

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].


@no_majority_schleicher
subtitle: Who will Hindenburg appoint in his place?
on-arrival: schleicher_times += 1
view-if: schleicher_support < 50 and (spd_support_schleicher or spd_refused_schleicher) and (ddp_support_schleicher or ddp_name == "DDP" or pro_republic >= 40) and (z_support_schleicher or z_party_leader == "Stegerwald" or pro_republic >= 50 or (unemployed <= 30 and reichskonkordat)) and (dvp_support_schleicher or pro_republic >= 55 or nationalism >= 55) and (dnvp_support_schleicher or (spd_support_schleicher and not dnvp_reform and not sapd_formed))
go-to: hitler_chancellor if schleichers_scheme_success or largest_party == "NSDAP" or schleicher_times > 1; schleicher_second_try if not schleichers_scheme_success and largest_party != "NSDAP" and schleicher_times <= 1

@schleicher_second_try

After the election, there is still no clear majority. Chancellor Kurt von Schleicher claims to have a plan to save Germany, and he has reached out to the Social Democrats with vague promises. He claims that in order to govern, the Reichstag must be temporarily suspended to prevent the Nazis and Communists from disrupting government, in effect giving him and Hindenburg dictatorial powers.

[? if works_program <= 0: The trade unions are somewhat favorable to Schleicher's plan, as they see a commonality between Schleicher's economic plan and the ADGB's WTB-plan for debt-funded job creation. However, the rest of our party is vehemently against Schleicher. ?]

Rumors are that Schleicher is also reaching out to the Nazis with similar promises of cooperation; he seems to believe that he can "tame" the Nazis. Most in our party do not trust him to not bring the Nazis to power. In addition, working with him would essentially mean the end of our claim to be a "democratic" party.

# if this works: delay elections by 1 year (requires over 50% support - need Z and DVP and others to join as well based on relations; DNVP might support? Also should depend on Hindenburg relations).

- @accept_schleichs
- @reject_schleichs

@accept_schleichs
title: Accept Schleicher's schemes.
subtitle: We will support the suspension of the Reichstag.

Are we sure that we wish to do this? The vast majority of the party is against this proposal.

- @accept_2_schleichs
- @reject_schleichs

@accept_2_schleichs
title: Accept Schleicher's schemes.
subtitle: We will support the suspension of the Reichstag.
on-arrival: schleicher_support = spd_r; workers_spd -= 3; new_middle_spd -= 3; reformist_dissent += 20; left_dissent += 30; center_dissent += 30; neorevisionist_dissent += 25; pro_republic -= 10; schleicher_support += z_r if z_relation >= 50; schleicher_support += other_r + dvp_r if dvp_relation >= 25; schleicher_support += dnvp_r if kpd_relation <= 10 and left_strength + center_strength < labor_strength; labor_dissent -= 10; {!
Q.strasser_percent = Q.socialism;
Q.hitler_percent = Q.nationalism;
Q.hitler_percent += (100-Q.pro_republic);
Q.strasser_percent += ((Q.workers_nsdap_normalized/100)*(Q.workers));
Q.strasser_percent += ((Q.unemployed_nsdap_normalized/100)*(Q.unemployed));
Q.hitler_percent += ((Q.rural_nsdap_normalized/100)*(Q.rural));
Q.hitler_percent += ((Q.old_middle_nsdap_normalized/100)*(Q.rural));
if (Q.hitler_deported) {
    Q.strasser_percent += ((Q.new_middle_nsdap_normalized/100)*(Q.new_middle));
} else {
    Q.hitler_percent += ((Q.new_middle_nsdap_normalized/100)*(Q.new_middle));
    Q.hitler_percent += Q.nsdap_votes;
}
Q.hitler_percent += Q.sa_force+Q.rfb_force;
Q.strasser_percent += Q.army_force+Q.rb_force;
Q.strasser_percent += Q.labor_dissent;
Q.hitler_percent += Q.labor_strength;
var total_nsdap = Q.hitler_percent+Q.strasser_percent;
Q.strasser_percent = Q.strasser_percent/total_nsdap;
Q.hitler_percent = Q.hitler_percent/total_nsdap;
!}
go-to: schleicher_success if schleicher_support >= 50; schleicher_failed if schleicher_support < 50

# There isn't enough resolution in this game to create conditions for when the DNVP would support Schleicher. It's possible that SPD support for Schleicher would push Hugenberg to ally with Hitler.

With the exception of the trade unions, every faction of the SPD is horrified by this betrayal of democracy.

[? if kpd_relation <= 10 and left_strength + center_strength < labor_strength: Somehow, the DNVP has agreed to Schleicher's proposal, forming a temporary alliance with the general against Nazis and Communists. ?][? if z_relation >= 50 : The Center Party also supports Schleicher's plan. ?][? if dvp_relation >= 25 : In addition, the DVP and some other smaller parties support this proposal. ?][? if kpd_relation > 10 or left_strength + center_strength >= labor_strength: The DNVP, despite being courted by Schleicher, is not interested in joining this alliance. DNVP chairman Hugenberg seems to prefer working with Hitler to any alliance that includes the SPD. ?]

[? if strasser_percent >= 0.25 and schleicher_support >= 50 : Seeing the writing on the wall, many Nazi deputies surrounding Strasser vote for Schleicher's motion alongside us, despite Hitler's continued opposition. Does this signal a Nazi split? ?]

@schleicher_success
on-arrival: next_election_month = month+13; next_election_time = time+13; next_election_year = year+1; next_election_month -= 12; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; schleichers_scheme_success = 1

The scheme has succeeded - there is a majority in the Reichstag backing Schleicher. New elections have been tentatively set for [+ next_election_month : month +] [+ next_election_year +].

@schleicher_failed
on-arrival: time_to_election = 3
call: set_next_election_time

Unfortunately, the coalition supporting the dissolution of the Reichstag does not constitute a majority. With Schleicher still unable to govern, the Reichstag is dissolved for new elections, set for [+ next_election_month : month +] [+ next_election_year +].

@reject_schleichs
title: Reject Schleicher's schemes.
subtitle: The Reichstag will continue to meet.
on-arrival: labor_dissent += 10 if works_program = 0; time_to_election = 3
call: set_next_election_time

In the end, there is no uptake for Schleicher's schemes by any political party. With Schleicher still unable to govern, the Reichstag is dissolved for new elections, set for [+ next_election_month : month +] [+ next_election_year +].

# TODO: there should be a second Schleicher option if the NSDAP isn't the strongest


@hitler_chancellor
on-arrival: chancellor = "Hitler"; chancellor_party = "NSDAP"

There is no path to a majority government for now. The scheming around President Hindenburg is leading to only one path - that of Chancellor Adolf Hitler. [? if largest_party == "NSDAP" : Since they are the largest party, it is not exactly contrary to parliamentary procedure, so it is difficult to raise objections solely on procedural grounds. ?] [? if hitler_deported: Despite his earlier deportation, the Nazi leaders and their reactionary allies have connived for the president to give him German citizenship after all. ?]

- @hitler_takes_power: What now?

# if NSDAP is largest party and Hindenburg is president, then Hindenburg appoints Hitler chancellor.

@spd_majority
title: SPD majority alone
subtitle: SPD: [+ spd_r +]%
unavailable-subtitle: SPD: [+ spd_r +]% - [? if president == "Hitler" : Hitler refuses to appoint a chancellor under such a governing arrangement. ?]
view-if: spd_r >= 50
choose-if: president != "Hitler"
on-arrival: chancellor = "Empty"; chancellor_party = "SPD"; spd_in_government = 1; in_spd_majority = 1; pro_republic += 5; foreign_minister_party = "SPD"; foreign_minister = "Breitscheid"; interior_minister_party = "SPD"; interior_minister = "Severing"; justice_minister_party = "SPD"; justice_minister = "Radbruch"; labor_minister_party = "SPD"; labor_minister = "Wissell"; reichswehr_minister_party = "SPD"; reichswehr_minister = "Leber"; economic_minister_party = "SPD"; economic_minister = "Schmidt"; finance_minister_party = "SPD"; finance_minister = "Hilferding"; agriculture_minister_party = "SPD"; agriculture_minister = "Baade"; reichswehr_loyalty -= 0.05; 
achievement: majority_party

Finally, the SPD can govern alone! Our long campaign of electioneering has paid off. We do not need to worry about coalition government; anything is possible. 

Who should be chancellor?

- @braun_majority_chancellor: Otto Braun for Chancellor.
- @breitscheid_majority_chancellor: Rudolf Breitscheid for Chancellor.
- @muller_majority_chancellor: Hermann Müller for Chancellor.
- @wels_majority_chancellor: Otto Wels for Chancellor.

@braun_majority_chancellor
view-if: not historical_mode or not spd_prussia
on-arrival: chancellor = "Braun"

[? if president == "Braun": Our experts have checked the constitution, and have discovered that there is no specific prohibition against the Chancellor also being the President (the President may not be a member of the Reichstag; there is no law saying that the Chancellor must be a member of the Reichstag). Thus, we have Chancellor-President Otto Braun. ?]
[? if president != "Braun": Otto Braun has become chancellor of the SPD majority. ?]

@breitscheid_majority_chancellor
on-arrival: chancellor = "Breitscheid"

Rudolf Breitscheid has become chancellor of the SPD majority.

@muller_majority_chancellor
view-if: muller_dead = 0
on-arrival: chancellor = "Müller"

Hermann Müller has become chancellor of the SPD majority.

@wels_majority_chancellor
view-if: muller_dead = 1
on-arrival: chancellor = "Wels"

Otto Wels has become chancellor of the SPD majority.

# TODO: Otto Wels should be able to be chancellor of a Grand Coalition, Weimar Coalition, or SPD majority after Müller dies.


@brauns_dilemma
title: There is no clear governing majority.
subtitle: President [+ president +] faces a dilemma.
view-if: president == "Braun" or president == "Schumacher" or president == "Juchacz" and (grand_coalition < 50 or grand_coalition_failed == 1 or dvp_willing != 1)

President [+ president +] faces a dilemma. [? if president == "Braun" or president == "Schumacher" : He ?][? if president == "Juchacz" : She ?] desperately desires a socialist or at least democratic government, but the electoral results are not there for such. Article 48 has been used by Hindenburg to promulgate anti-democratic and anti-worker policies; can Article 48 be used for good as well as evil?

- @emergency_braun: We will form an emergency government with [+ president +]'s support.
- @new_elections: We will have new elections.
- @new_elections_2: We will have new elections.
- @new_grand_coalition
- @left_coalition
- @popular_front_coalition
- @far_right_coalition



@emergency_braun
title: We will form an emergency government with [+ president +]'s support.
subtitle: This may have the effect of inflamming public sentiment against us, and the reactionaries will be against us each step of the way.
on-arrival: sa_strength += 100; sh_strength += 100; in_emergency_government = 1; z_no_confidence = 0; kpd_no_confidence = 0; dvp_no_confidence = 0; other_no_confidence = 0
go-to: emergency_menu

Yes, an emergency minority government, with the support of the SPD. Who should lead this emergency minority government?

@emergency_menu

- @braun_emergency_chancellor: Otto Braun for Chancellor-President!
- @braun_emergency_chancellor_2: Otto Braun for emergency Chancellor.
- @breitscheid_emergency_chancellor: Rudolf Breitscheid for emergency Chancellor.
- @bruning_emergency_chancellor: Brüning of the Center Party, as a goodwill gesture toward the bourgeoisie.
- @wirth_emergency_chancellor: Joseph Wirth of the Center Party, as a gesture of republican unity.

@braun_emergency_chancellor
subtitle: This is unprecedented.
view-if: (not historical_mode or not spd_prussia) and president == "Braun"
on-arrival: chancellor = "Braun"; chancellor_party = "SPD"; spd_in_government = 1; pro_republic -= 5; foreign_minister_party = "SPD"; interior_minister_party = "SPD"; justice_minister_party = "SPD"; labor_minister_party = "SPD"; reichswehr_minister_party = "SPD"; economic_minister_party = "SPD"; finance_minister_party = "SPD"; agriculture_minister_party = "SPD"; reichswehr_loyalty -= 0.1; 

Our experts have checked the constitution, and have discovered that there is no specific prohibition against the Chancellor also being the President (the President may not be a member of the Reichstag; there is no law saying that the Chancellor *must* be a member of the Reichstag).

Thus, we have Chancellor-President Otto Braun. He claims that he will resign as soon as the economic-political crisis is over. The right-wing parties, who were once clamoring to replace parliamentary rule with dictatorship or monarchy, are now clamoring for the restoration of democracy.

- @far_right_coup
- @emergency_no_confidence: The Nazis immediately call a no-confidence vote against us...

@breitscheid_emergency_chancellor
subtitle: The Communists are warming to him...
on-arrival: chancellor = "Breitscheid"; chancellor_party = "SPD"; spd_in_government = 1; kpd_relation += 5; foreign_minister_party = "SPD"; interior_minister_party = "SPD"; justice_minister_party = "SPD"; labor_minister_party = "SPD"; reichswehr_minister_party = "SPD"; economic_minister_party = "SPD"; finance_minister_party = "SPD"; agriculture_minister_party = "SPD"; reichswehr_loyalty -= 0.1;

We have appointed Rudolf Breitscheid as emergency chancellor. The Communists consider him less objectionable than the alternatives, although it is unclear whether they will tolerate this government.

- @far_right_coup
- @emergency_no_confidence

@braun_emergency_chancellor_2
subtitle: He is an experienced moderate; perhaps what the country needs.
view-if: (not historical_mode or not spd_prussia) and president != "Braun"
choose-if: not braun_campaign and year < 1935
unavailable-subtitle: Braun has mostly retired from politics.
on-arrival: chancellor = "Braun"; chancellor_party = "SPD"; spd_in_government = 1; foreign_minister_party = "SPD"; interior_minister_party = "SPD"; justice_minister_party = "SPD"; labor_minister_party = "SPD"; reichswehr_minister_party = "SPD"; economic_minister_party = "SPD"; finance_minister_party = "SPD"; agriculture_minister_party = "SPD"; reichswehr_loyalty -= 0.05;

We have appointed Rudolf Breitscheid as emergency chancellor. The Communists consider him less objectionable than the alternatives, although it is unclear whether they will tolerate this government.

- @far_right_coup
- @emergency_no_confidence

@bruning_emergency_chancellor

Brüning is happy to be Chancellor, but not as a figurehead for an all-SPD minority cabinet. He is asking that he be allowed to form a minority bourgeois government, backed by SPD toleration. Otherwise, he will refuse.

- @accept_bruning: Fine. We will allow Brüning to lead a bourgeois cabinet.
- @emergency_menu: Nevermind. What are the other options?

@accept_bruning
choose-if: neo_weimar_coalition >= 50
unavailable-subtitle: The parties supporting Brüning do not have a majority.
on-arrival: left_dissent += 5; z_relation += 10; spd_in_government = 0; spd_toleration = 1; minority_government = 1; chancellor = "Brüning"; chancellor_party = "Z"; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "I"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = ddp_name; finance_minister_party = "DVP"; agriculture_minister_party = "Z"; pro_republic -= 3; workers_spd -= 4; workers_kpd += 2; workers_kpd += 1 if not sapd_consolidated; workers_sapd += 1 if sapd_consolidated

The Center Party is pleased, and somewhat surprised, that we have appointed [+ chancellor +] as chancellor. Many of our supporters are distraught at this concession to the bourgeoisie, with the SPD holding the presidency.

- @far_right_coup

@wirth_emergency_chancellor
view-if: z_party_leader == "Joos"
on-arrival: chancellor = "Wirth"; chancellor_party = "Z"; spd_in_government = 1; z_relation += 10; foreign_minister_party = "SPD"; interior_minister_party = "SPD"; justice_minister_party = "SPD"; labor_minister_party = "SPD"; reichswehr_minister_party = "SPD"; economic_minister_party = "SPD"; finance_minister_party = "SPD"; agriculture_minister_party = "SPD" 

The Center Party is pleased that we have appointed [+ chancellor +] as chancellor.

- @far_right_coup
- @emergency_no_confidence



@far_right_coup
title: The far-right parties immediately launch a military coup against us...
view-if: far_right_coalition >= 45
on-arrival: sh_strength += 150; sa_strength += 150; sh_militancy += 0.05; sa_militancy += 0.05; nazi_rebel = 1

The far-right parties, seeing that we are willing to deny them government by questionably democratic means, have decided that they can only take power by undemocratic means, and have risen up in force against the sitting government.

Their numbers are bolstered by the many government workers who are against the prospect of a unified Socialist government. Police and soldiers defect to the SA and Stahlhelm.

- @civil_war: It is war.


@emergency_no_confidence
title: The Nazis immediately call a no-confidence vote against us...
view-if: far_right_coalition < 45
on-arrival: z_no_confidence = 1 if z_relation < 45; kpd_no_confidence = 1 if (kpd_relation < 45 or (kpd_r >= spd_r and kpd_r >= nsdap_r and kpd_r + spd_r >= 50)); other_no_confidence = 1 if (z_relation + dvp_relation)/2 < 45; dvp_no_confidence = 1 if dvp_relation < 45; sa_strength += 200; sh_strength += 200; no_confidence_votes = nsdap_r + dnvp_r; no_confidence_votes += z_r if z_no_confidence; no_confidence_votes += kpd_r if kpd_no_confidence; no_confidence_votes += sapd_r if kpd_no_confidence; no_confidence_votes += other_r if other_no_confidence; no_confidence_votes += dvp_r if dvp_no_confidence; kpd_relation -= 10 if kpd_no_confidence; no_confidence_succeeds = 1 if no_confidence_votes > 50

The NSDAP and DNVP will vote against us, of course. The other parties vote their conscience. [? if z_no_confidence: The Center Party is voting against us. ?] [? if kpd_no_confidence: The KPD votes against us. ?] [? if other_no_confidence: The assortment of minor parties are voting against us. ?]

In total, [+ no_confidence_votes +]% of the Reichstag is against us. [? if no_confidence_succeeds : We have lost the vote of no confidence. ?]

- @no_confidence_lose: We have lost the vote of no confidence.
- @no_confidence_win: We have won, for now.

@no_confidence_lose
view-if: no_confidence_votes > 50
on-arrival: previous_chancellor = chancellor; previous_chancellor_party = chancellor_party; chancellor = "Empty"; chancellor_party = "I"; in_emergency_government = 0

Who should be chancellor instead remains our decision. Is the Reichstag workable?

- @emergency_braun
- @no_confidence_elections_new: Dissolve the Reichstag for new elections.
- @coalition_menu: Are there any alternative governments?

@no_confidence_elections_new
on-arrival: {!
Q.chancellor = Q.previous_chancellor;
Q.chancellor_party = Q.previous_chancellor_party;
Q.in_emergency_government = 1;
Q.no_majority_elections += 1;
Q.political_crisis_progress += 1;
Q.next_election_month = Q.month + 6;
Q.next_election_time = Q.time + 6;
if (Q.next_election_month > 12) {
    Q.next_election_month -= 12;
    Q.next_election_year = Q.year + 1;
} else {
    Q.next_election_year = Q.year;
}
Q.pro_republic -= 5;
Q.workers_spd -= 3;
Q.new_middle_spd -= 3;
Q.old_middle_spd -= 3;
Q.rural_spd -= 3;
Q.catholics_spd -= 3;
Q.workers_nsdap += 5;
Q.old_middle_nsdap += 3;
Q.new_middle_nsdap += 3;
Q.rural_nsdap += 3;
Q.unemployed_nsdap += 5;
if (pro_republic < 45 && Q.pro_republic >= 38) {
    Q.workers_spd -= 6;
}
if (Q.pro_republic < 38) {
    Q.workers_spd *= 0.85;
    Q.new_middle_spd -= 5;
    Q.old_middle_spd -= 5;
    Q.rural_spd -= 5;
    Q.catholics_spd -= 3;
    Q.workers_nsdap += 5;
    Q.old_middle_nsdap += 5;
    Q.new_middle_nsdap += 5;
    Q.rural_nsdap += 5;
    Q.unemployed_nsdap += 5;   
}
!}
view-if: no_confidence_votes > 50

The next elections are set for [+ next_election_month : month +] [+ next_election_year +].


@no_confidence_win
view-if: no_confidence_votes <= 50
on-arrival: in_minority_government = 1; in_emergency_government = 0
achievement: minderheitsregierung

We have won the vote of no confidence, and are currently ruling under an SPD minority government. Now our task is to fix the country.


@weimar_coalition
title: We can form a Weimar Coalition.
subtitle: SPD + Z + [+ ddp_name +] ([+ weimar_coalition +]%)
choose-if: president != "Hitler" and z_willing == 1
unavailable-subtitle: SPD + Z + [+ ddp_name +] ([+ weimar_coalition +]%) - [? if president == "Hitler" : Hitler refuses to appoint a chancellor under such a governing arrangement. ?][? if not z_willing : The Center Party is unwilling to coalition with us. ?]
view-if: weimar_coalition >= 50 or (weimar_coalition + sapd_r >= 50 and kpd_relations >= 30)
on-arrival: leverage += 5; leverage += 5 if z_party_leader == "Joos"; in_weimar_coalition = 1; spd_in_government = 1; in_popular_front = 0; in_left_front = 0; foreign_minister_party = "Z"; interior_minister_party = "Z"; justice_minister_party = ddp_name; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = ddp_name; finance_minister_party = "Z"; agriculture_minister_party = "Z"; foreign_minister = "Wirth"; interior_minister = "Wirth"; justice_minister = "Koch-Weser"; labor_minister = "Stegerwald"; reichswehr_minister = "Groener"; economic_minister = "Dietrich"; finance_minister = "Köhler"; agriculture_minister = "Hermes";
achievement: weimar_coalition
go-to: weimar_normal if z_relation >= 40 and bourgeois_coalition < 50; weimar_z if z_relation < 40 or bourgeois_coalition >= 50

@weimar_normal

We have enough votes [? if weimar_coalition < 50 : with the SAPD's toleration accounted for ?] to form a "Weimar Coalition" government consisting of the SPD, Center (Z) and Democratic ([+ ddp_name +]) parties! This has been our preferred governing coalition. [? if weimar_coalition < 50 : We will need to take care not to offend the SAPD however. ?]

Who should be the chancellor?

- @muller
- @braun
- @breitscheid_coalition_chancellor
- @wels
- @z_chancellor
- @z_chancellor_wirth

@breitscheid_coalition_chancellor
title: Rudolf Breitscheid for Chancellor
choose-if: (president != "Hindenburg" or not spd_sackings)
subtitle: Breitscheid is viewed as friendlier towards the left of the SPD.
view-if: year >= 1930
on-arrival: chancellor = "Breitscheid"; chancellor_party = "SPD";
go-to: ministries

Rudolf Breitscheid has been appointed chancellor.

@weimar_z

We have enough votes to form a "Weimar Coalition" government consisting of the SPD, Center (Z) and Democratic ([+ ddp_name +]) parties! This has been our preferred governing coalition. However, we have, one way or another, offended the Center Party, and they have demanded the position of Chancellor

- @let_bruning: Let Brüning lead.
- @let_wirth: Let Wirth lead.
- @refuse_demand: No, the SPD must lead the government.

@let_bruning
view-if: z_party_leader != "Joos"
choose-if: weimar_coalition >= 50
unavailable-subtitle: The SAPD will not tolerate Brüning.
on-arrival: chancellor_party = "Z"; chancellor = "Brüning"; left_dissent += 5
go-to: ministries

@let_wirth
view-if: z_party_leader == "Joos"
on-arrival: chancellor_party = "Z"; chancellor = "Wirth"
go-to: ministries

@refuse_demand
on-arrival: z_relation -= 5; reformist_dissent += 2
go-to: coalition_menu if bourgeois_coalition < 50; let_it_form if bourgeois_coalition >= 50

@grand_coalition
title: We can form a Grand Coalition.
view-if: grand_coalition >= 50 and grand_coalition_failed == 0
choose-if: president != "Hitler" and dvp_willing == 1
unavailable-subtitle: SPD + Z + [+ bvp_name +] + [+ ddp_name +] + DVP ([+ grand_coalition +]%) - [? if president == "Hitler" : Hitler refuses to appoint a chancellor under such a governing arrangement. ?][? if not dvp_willing : The DVP is unwilling to work with us. ?]
subtitle: SPD + Z + [+ bvp_name +] + [+ ddp_name +] + DVP  ([+ grand_coalition +]%)
on-arrival: in_grand_coalition = 1; in_popular_front = 0; in_left_front = 0
go-to: grand_coalition_spd_lead if ((n_elections <= 1 and largest_party == "SPD") or (largest_party == "SPD" and spd_r >= 30) and spd_sackings <= 0) or president = "Braun"; grand_coalition_z_lead if president != "Braun" and (spd_sackings > 0 or (n_elections > 1 and spd_r < 30) or (n_elections <= 1 and largest_party != "SPD"))

# TODO: if there are enough votes to form a grand coalition, who leads? Does the SPD lead or does Brüning (Z) lead? In reality this depended on Hindenburg's will, basically, which in turn depended on what his camarilla supported.
# In the first election, SPD always leads. In subsequent elections, if spd_r >= 29 then SPD leads, otherwise Z leads?

@grand_coalition_z_lead
go-to: grand_coalition_z_lead_2 if dvp_relation >= 30; grand_coalition_fail if dvp_relation < 30

There are enough votes to form a majority coalition consisting of the SPD, Center (Z), Bavarian People's ([+ bvp_name +]), Democratic ([+ ddp_name +]), and People's (DVP) parties. Given our rather weak electoral performance, [? if spd_no_confidence = 1 : and our previous vote of no confidence against the government, ?][? if no_confidence_against_spd = 1 : and the vote of no confidence against us, ?] President [+ president +] has asked the Center Party to take the lead role in forming a government.

# TODO: if the SPD participated in bringing down the previous government via a vote-of-no-confidence, or if we were essentially removed from government by our coalition partners, we should be more reluctant to participate in a new government.

@grand_coalition_z_lead_2

The Center chancellor is likely to be Heinrich Brüning of the Center Party [? if chancellor = "Brüning" : again. ?][? if chancellor != "Brüning": . ?] He is known for his conservatism and sympathy for monarchism, being on the right wing of the Center. [? if austerity >= 2 : Due to the government's austerity policies, many of our members are reluctant to participate in this coalition again. Some support a "toleration" arrangement in which we will provide outside support to prevent the far-right from either entering government or calling a successful no-confidence vote. Others support entering into pure opposition, and letting the right do what it wishes. ?]

Brüning is not opposed to a coalition with us. Alternatively, we could enter a "toleration" agreement.

- @join_bruning: Join the Brüning coalition.
- @bruning_oppo: Join the opposition.
- @toleration_bruning: We will "tolerate" a minority Brüning government.
- @unity_government

@join_bruning
view-if: grand_coalition >= 50 or neo_weimar_coalition >= 50
on-arrival: spd_in_government = 1; spd_toleration = 0; in_grand_coalition = 1; chancellor = "Brüning"; chancellor_party = "Z"; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "Z"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = "Z"; finance_minister_party = "Z"; agriculture_minister_party = "Z"; leverage = 25 if leverage >= 25;
go-to: ministries

Heinrich Brüning welcomes us into his new cabinet. Now, we have our pick of ministries.

@bruning_oppo
on-arrival: spd_in_government = 0; spd_toleration = 0; chancellor = "Brüning"; chancellor_party = "Z"; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "Z"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = "Z"; finance_minister_party = "Z"; agriculture_minister_party = "Z"; grand_coalition_failed = 1

By refusing to join the grand coalition or tolerating it, we essentially throw the balance of government to the right wing. [? if right_coalition >= 50 : There are enough votes to form a right-wing coalition that includes the DNVP. However, the DNVP leader, Hugenberg, is opposed to a coalition with the "November criminals" of the Center Party. And of course, the NSDAP is not interested in coalition government. This means that there will not be enough votes to form a majority coalition. ?][? if right_coalition < 50 : There are not enough votes to form a right-wing coalition that includes the DNVP, and they are not interested in joining in a coalition with the "November criminals" of the Center Party, anyhow. And of course, the NSDAP is not interested in coalition government. This means that there is no route to a majority coalition. ?]

If we do not at least "tolerate" the government, there will be new elections in a few months. In the meantime, the President and Chancellor may use the emergency powers granted by Article 48 of the constitution to essentially govern by decree. And it is not at all clear that we will do any better in the next election.

- @join_bruning: Join the Brüning coalition.
- @toleration_bruning: We will tolerate Brüning after all.
- @brauns_dilemma
- @new_elections: New elections, it is!


@new_elections
view-if: neo_weimar_coalition >= 50
on-arrival: {!
Q.marx_relation -= 10;
Q.next_election_month = Q.month + 3;
Q.next_election_time = Q.time + 3;
if (Q.next_election_month > 12) {
    Q.next_election_month -= 12;
    Q.next_election_year = Q.year + 1;
} else {
    Q.next_election_year = Q.year;
}
if (Q.chancellor == "Brüning") {
    Q.z_relation -= 5;
    Q.dvp_relation -= 5;
}
Q.pro_republic -= 4;
Q.new_middle_spd -= 3;
Q.old_middle_spd -= 3;
Q.rural_spd -= 3;
Q.catholics_spd -= 2;
Q.new_middle_dvp -= 5;
Q.old_middle_dvp -= 5;
Q.new_middle_ddp -= 5;
Q.old_middle_ddp -= 5;
Q.rural_dnvp -= 4;
Q.workers_nsdap += 3;
Q.old_middle_nsdap += 5;
Q.new_middle_nsdap += 5;
Q.rural_nsdap += 5;
Q.unemployed_nsdap += 3;
// Thresholds are kind of arbitrary - adjusted because of negative economic effects on pro_republic
if (Q.pro_republic < 45 && Q.pro_republic >= 36) {
    Q.workers_spd -= 2;
}
if (Q.pro_republic < 36 && Q.left_strength < 50) {
    // TODO: if you pursue a consistently revolutionary course, you might avoid this? But you might also cause a coalition between Hitler and the Center.
    Q.z_relation -= 5;
    Q.dvp_relation -= 5;
    Q.ddp_relation -= 3;
    Q.workers_spd -= 3;
    Q.unemployed_spd -= 3;
    Q.new_middle_spd -= 5
    Q.old_middle_spd -= 6;
    Q.rural_spd -= 6;
    Q.catholics_spd -= 3;
    Q.workers_nsdap += 3;
    Q.old_middle_nsdap += 6;
    Q.new_middle_nsdap += 6;
    Q.rural_nsdap += 6;
    Q.unemployed_nsdap += 4;
}
if (Q.left_strength >= 50) {
    Q.z_relation -= 8;
    Q.dvp_relation -= 8;
    Q.ddp_relation -= 4;
    Q.new_middle_nsdap += 4;
    Q.old_middle_nsdap += 6;
    Q.rural_nsdap += 6;
    Q.rural_dnvp += 2;
    Q.rural_dvp -= 4;
    Q.old_middle_dvp -= 4;
    Q.new_middle_dvp -= 4;
    Q.new_middle_ddp -= 2;
    Q.old_middle_other -= 2;
    Q.rural_other -= 2;
    Q.new_middle_other -= 2;
    Q.sa_militancy += 0.01;
    Q.workers_spd += Q.radicalization;
    Q.unemployed_spd += Q.radicalization;
    Q.old_middle_spd -= Q.radicalization;
    Q.rural_spd -= Q.radicalization;
    Q.pro_republic -= 4;
    Q.unemployed_kpd += Q.strife;
    Q.unemployed_nsdap -= Q.strife;
    Q.coup_progress += Q.political_crisis_progress;
}
if (Q.far_right_coalition >= 50) {
    Q.sa_strength += 100;
    Q.sh_strength += 100;
    Q.coup_progress += 3;
}
if (Q.no_majority_elections >= 2) {
    Q.political_crisis_progress += 1
}
!}

There is no path to a workable Reichstag majority or minority government. The next elections are set for [+ next_election_month : month +] [+ next_election_year +].

The people's faith in democracy is shaken by this ordeal. Of course, the Nazis are exploiting the instability to claim that democracy is unworkable, and that Germany needs a strong leader.

[? if left_strength >= 50 : Bourgeois voters and financial backers are increasingly turning to the Nazis as defenders from potential socialist revolution, though as a result Nazi support amongst the working class is diminished. ?]

@new_elections_2
view-if: neo_weimar_coalition < 50
call: set_next_election_time
on-arrival: time_to_election = 3; strife += 0.5; marx_relation -= 5; pro_republic -= 5; rural_nsdap += 2; old_middle_nsdap += 2; new_middle_nsdap += 2; political_crisis_progress += 1

As the anti-democratic parties hold a majority, there is no path to a majority government. New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@toleration_bruning
choose-if: neo_weimar_coalition >= 50
unavailable-subtitle: The parties supporting Brüning do not have a majority.
on-arrival: spd_in_government = 0; spd_toleration = 1; minority_government = 1; chancellor = "Brüning"; chancellor_party = "Z"; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "I"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = ddp_name; finance_minister_party = "DVP"; agriculture_minister_party = "Z"; pro_republic -= 4; new_middle_dvp -= 5; new_middle_ddp -= 5; old_middle_dvp -= 5; old_middle_ddp -= 5; old_middle_nsdap += 5; new_middle_nsdap += 5; rural_dnvp -= 5;

We have decided to tolerate Brüning's government. This means that we will vote against no-confidence votes to make sure they fail, but also that we will not be directly participating as ministers in the government. Thus, we can prevent a new election and stop the far right from entering government.

@grand_coalition_fail
on-arrival: grand_coalition_failed = 1

Unfortunately, the DVP and [+ bvp_name +] are unwilling to enter into another coalition with the SPD, so the grand coalition is unable to be formed.

- @no_majority: Thus, there is no clear majority.
- @brauns_dilemma
- @right_coalition_2: Perhaps a right-wing coalition may form.
- @bourgeois_coalition: Perhaps a bourgeois coalition may form.
- @beg_dvp: Perhaps we could ask the DVP to change their minds?
- @unity_government
- @coalition_menu: What are the other governing arrangements?

@beg_dvp
view-if: dvp_relation >= 15
go-to: dvp_conservative if stresemann_dead == 1; dvp_majority if stresemann_dead == 0

@dvp_majority
go-to: join_bruning

Under the influence of Gustav Stresemann, the DVP agrees to allow us into the coalition, and so does the [+ bvp_name +].


@dvp_conservative

The DVP asks us to adhere to their conservative economic policies (that is, extreme austerity and labor discipline) in exchange for us joining the coalition.

- @beg_dvp_2: But they would not have a majority without the SPD! The DNVP and NSDAP have no interest in participating in government.
- @no_majority: There is no chance that we would agree.
- @brauns_dilemma
- @coalition_menu: What are the other governing arrangements?


@beg_dvp_2
go-to: beg_dvp_succeed; beg_dvp_fail

We make the case that without the SPD, there is no parliamentary majority, given the DNVP and NSDAP's hostility to constitutional government.

@beg_dvp_succeed
go-to: join_bruning

Somehow, we succeed in convincing them! We are now in a coalition.


@beg_dvp_fail
go-to: no_majority

As expected, the DVP is too set in their anti-democratic ways.

@budget_dispute

As expected, the SPD is the largest party, and this time, we are expected to take the lead role in the formation of the next government, including selecting the chancellor. President [+ president +] has asked us to form a majority coalition with the Center (Z), Bavarian People's ([+ bvp_name +]), Democratic ([+ ddp_name +]), and People's (DVP) parties.

However, the DVP and SPD significantly diverge on the matter of the budget. While the DVP would like to dismantle the welfare state in order to save Germany's finances, this is obviously unacceptable to us. And while we would be happy to increase taxes on the rich, they would not have that. Before a government can be formed, this dispute must be resolved.

- @postpone: Postpone the issue until after the Young Plan's ratification, preserving the status quo until then.
- @austerity: Agree to moderate tax increases across the board, and no further modifications.
- @resign_prussia: Surrender Prussia to the bourgeoisie and delay making a committment.
- @blow_up: Withdraw from talks, and refuse to form a government.

@postpone
on-arrival: left_dissent += 5; status_quo = 1; budget_resolved = 1
subtitle: We won't need to make a commitment for now, but our freedom in government will be severely constrained. The Left is opposed.
go-to: grand_coalition_spd_lead

The DVP has agreed to this compromise. Our party's left-wing is upset, but what must be done must be done.

@austerity
on-arrival: left_dissent += 5; labor_dissent += 5; upper_tax_rates += 1; lower_tax_rates += 1; budget += 2; workers_spd -= 4; unemployed_spd -= 4; status_quo = 1; budget_resolved = 1; dvp_relation += 4*(1-dissent); kpd_relation -= 3
subtitle: +3 Budget. The bourgeois parties will be pleasantly pleased, but our actions in government will be highly constrained. Left and Labor are both opposed.
go-to: grand_coalition_spd_lead

The DVP has agreed to this compromise. Our party's left-wing and the ADGB are both upset, but what must be done must be done.

@blow_up
subtitle: [? if right_coalition >= 50 and not right_coalition_failed : The right-wing parties will likely form a coalition. ?][? if right_coalition < 50 or right_coalition_failed : There is no clear majority. ?]
on-arrival: dvp_relation -= 5; z_relation -= 3; left_strength += 5; left_dissent -= 5;
go-to: right_coalition_2 if right_coalition >= 50 and not right_coalition_failed; no_majority if right_coalition < 50 or right_coalition_failed

@resign_prussia
subtitle: Is surrendering such a bulwark of democracy worth it for one government?
on-arrival: reformist_dissent += 5; spd_prussia = 0; z_prussia = 1; budget_resolved = 1; labor_dissent += 5; center_dissent += 5
go-to: grand_coalition_spd_lead

Braun has resigned as Minister-President of Prussia, replaced by Adam Stegerwald. The bourgeois parties accept this concession for now, and the budget issue is left unresolved.

@grand_coalition_spd_lead
on-arrival: leverage -= 15 if dvp_relation < 20
go-to: budget_dispute if not budget_resolved and historical_mode

[? if n_elections <= 1 : As expected, the SPD is the largest party, and this time, we are expected to take the lead role in the formation of the next government, including selecting the chancellor. President [+ president +] has asked us to form a majority coalition with the Center (Z), Bavarian People's ([+ bvp_name +]), Democratic ([+ ddp_name +]), and People's (DVP) parties. ?]

[? if dvp_relation < 20 : As our relation with the DVP is poor, they are demanding more minister posts in order to remain part of the coalition. ?]

Who should be the chancellor?

- @muller
- @braun
- @wels
- @z_chancellor
- @z_chancellor_wirth
- @no_govt


@muller
title: Hermann Müller for Chancellor
view-if: muller_dead = 0
choose-if: (president != "Hindenburg" or not spd_sackings)
subtitle: Müller is popular within the party, and is the preferred candidate of the Center and other parties.
on-arrival: spd_in_government = 1; chancellor = "Müller"; chancellor_party = "SPD"; in_grand_coalition = 1 if in_weimar_coalition == 0
go-to: ministries

Müller's appointment as chancellor is approved by all our potential allies.

@wels
title: Otto Wels for Chancellor
choose-if: (president != "Hindenburg" or not spd_sackings)
view-if: muller_dead = 1
subtitle: Otto Wels is a party leader, but can he also be chancellor?
on-arrival: spd_in_government = 1; chancellor = "Wels"; chancellor_party = "SPD"; in_grand_coalition = 1 if in_weimar_coalition == 0
go-to: ministries

Otto Wels's appointment as chancellor is approved by our potential allies.

@braun
choose-if: (president != "Hindenburg" or not spd_sackings)
title: Otto Braun for Chancellor
view-if: not historical_mode or not spd_prussia
subtitle: Braun is currently the Minister-President of Prussia, and is one of our most popular leaders. However, other parties may have qualms about concentrating too much power in one man.

The Center party has significant qualms about appointing Braun as chancellor, given that he is already Minister-President of Prussia. They ask for either more cabinet positions, or for Braun to resign his state government role.

- @muller
- @wels
- @braun_2: Let the Center have their cabinet positions.
- @braun_resigned: Let the Center have Prussia.

@braun_2
subtitle: Otto Braun will be Chancellor.
view-if: not historical_mode
on-arrival: spd_in_government = 1; chancellor = "Braun"; chancellor_party = "SPD"; leverage -= 10; in_grand_coalition = 1 if in_weimar_coalition == 0
go-to: ministries

We have promised to let the Center have more influence in the government in response to the appointment of Braun as chancellor.

@braun_resigned
subtitle: Adam Stegerwald will be Minister-President.
choose-if: spd_prussia
unavailable-subtitle: Wait, what? Damn, this shit's bugged. Ping me on discord or on reddit, I'll fix it.
on-arrival: spd_prussia = 0; z_prussia = 1; spd_in_government = 1; chancellor = "Braun"; chancellor_party = "SPD"; in_grand_coalition = 1 if in_weimar_coalition == 0
go-to: ministries

Otto Braun has resigned as Minister-President of Prussia to pursue his aspirations on the national level.

@z_chancellor
title: Let the Center Party have their chancellor.
subtitle: Heinrich Bruning will be chancellor. This is unexpected and will cause internal dissent, but may give us a better position in negotiating for other cabinet positions.
on-arrival: left_dissent += 5; center_dissent += 5; labor_dissent += 5; z_relation += 10; dvp_relation += 5; kpd_relation -= 5; in_grand_coalition = 1 if in_weimar_coalition == 0; chancellor = "Brüning"; chancellor_party = "Z"; spd_in_government = 1; leverage += 5
go-to: ministries

This is highly unexpected, and many in the left and center of our party are criticizing this decision.

[? if year == 1928 : Since Wilhelm Marx resigned, Heinrich Brüning has been appointed chancellor. ?]
[? if year > 1928: Heinrich Brüning of the Center Party has been appointed chancellor. ?]

@z_chancellor_wirth
title: Let Joseph Wirth of the Center Party be chancellor.
view-if: z_party_leader == "Joos"
subtitle: Wirth is on the left wing of the Center Party. This may give us a better position in negotiating for other cabinet positions.
on-arrival: left_dissent += 2; z_relation += 6; ddp_relation += 3; kpd_relation -= 5; in_grand_coalition = 1 if in_weimar_coalition == 0; chancellor = "Wirth"; chancellor_party = "Z"; spd_in_government = 1; leverage += 5
go-to: ministries


@no_govt_3
view-if: (weimar_coalition >= 50 and z_willing == 1) or (grand_coalition >= 50 and grand_coalition_failed != 1 and dvp_willing == 1)
title: We do not form a government.
subtitle: [? if right_coalition >= 50 and right_coalition_failed == 0 : A right-wing coalition may form instead. ?] This will cause significant internal dissent, and will lead to increased distrust from the bourgeois parties.
on-arrival: reformist_dissent += 5; center_dissent += 5; labor_dissent += 5; neorevisionist_dissent += 5

Are we sure about not participating in government? This will cause significant internal dissent, and will lead to increased distrust from the bourgeois parties. Even debating this option causes internal backlash. In addition, it is possible that the bourgeois and right-wing parties will also be unable to form a coalition government, leading to a regime appointed by President [+ president +].

- @coalition_menu: What are the governing arrangements?
- @no_govt_goals: Yes, we're sure.

@no_govt
title: We do not form the government.
subtitle: This will cause significant internal dissent, and will lead to increased distrust from the bourgeois parties.
on-arrival: reformist_dissent += 5; center_dissent += 5; labor_dissent += 5; neorevisionist_dissent += 5

Are we sure about not participating in government? This will cause significant internal dissent, and will lead to increased distrust from the bourgeois parties. Even debating this option causes internal backlash. In addition, it is possible that the bourgeois and right-wing parties will also be unable to form a coalition government, leading to a regime appointed by President [+ president +].

- @muller
- @braun
- @z_chancellor
- @no_govt_goals: Yes, we're sure.

@no_govt_goals

What are our goals here, exactly?

- @majority: We hope to reach a socialist majority in the next election.
- @trap: It is a trap to form a government now. We would be unable to accomplish our goals.
- @socialism: Revolutionary socialism is our goal.

@majority
on-arrival: kpd_relation += 5; communist_coalition += 1 if comintern_seen 
go-to: no_govt_2

@trap
on-arrival: radicalization += 1
go-to: no_govt_2

@socialism
on-arrival: left_strength += 5; rb_militancy += 0.02
go-to: no_govt_2


@no_govt_2
on-arrival: reformist_dissent += 25; center_dissent += 10; labor_dissent += 25; neorevisionist_dissent += 25; in_weimar_coalition = 0; in_grand_coalition = 0; chancellor = "Brüning" if chancellor == "Marx"; spd_in_government = 0; grand_coalition_failed = 1
go-to: right_coalition_post_spd if right_coalition >= 50 and president != "Braun" and right_coalition_failed == 1; new_elections_spd if right_coalition < 50 and president != "Braun"; brauns_dilemma if president == "Braun" or president == "Schumacher" or president == "Juchacz"; right_coalition_2 if right_coalition_failed == 0 and right_coalition >= 50 and president == "Hindenburg"


@right_coalition_post_spd
on-arrival: chancellor = "Brüning"; new_middle_spd -= 3; old_middle_spd -= 2; workers_spd -= 2; catholics_spd -= 4; next_election_time = time+3; next_election_month = month + 3; next_election_year = year; next_election_year = year + 1 if next_election_month > 12; next_election_month = next_election_month - 12 if next_election_month > 12; 

There are enough votes to form a right-wing coalition of the Z, DVP, DNVP, and various minor parties, essentially replicating the previous Marx government. However, the DNVP is very reluctant to join the government[? if year <= 1928 : , as it had just exited from a coalition with the Center. ?][? if year > 1928 : , as its leader, Alfred Hugenberg, is against the very existence of parliamentary democracy, and is unwilling to join such a coalition anymore. ?]

Chancellor Heinrich Brüning of the Center Party currently rules on an emergency basis, as part of a "presidential government". New elections have been called for [+ next_election_month : month +] [+ next_election_year +]


@new_elections_spd
on-arrival: next_election_time = time+3; next_election_month = month + 3; next_election_year = year; next_election_year = year + 1 if next_election_month > 12; next_election_month = next_election_month - 12 if next_election_month > 12; new_middle_spd -= 5; old_middle_spd -= 5; workers_spd -= 3; catholics_spd -= 3; in_election = 1

Since the remaining bourgeois and right-wing parties cannot form a tolerable governing agreement, new elections have been called for [+ next_election_month : month +] [+ next_election_year +]. The previous chancellor has resigned, and has been replaced by Heinrich Brüning, also of the Center Party, ruling as part of a presidential cabinet. 

We are seen as responsible for the failure to form a government, and many of our middle-class and even some working-class supporters have deserted the party.


@ministries

We can pick from any number of ministries. Controlling a ministry allows us to directly effect policy in that area. [? if in_left_front : Any ministry that we do not take will be given to the KPD. ?] [? if in_left_front and reichswehr_minister_party == "KPD" : If the KPD controls the Reichswehr Ministry, the army will most likely revolt. ?]

[? if historical_mode : Labor expects us to take the Labor Ministry in order to represent their interests in government and will be upset if we do not. ?]

- #mins_1928
- #drop_mins_1928

@labor
title: Labor Ministry
subtitle: Taking the Labor Ministry is expected of us. [? if historical_mode : Labor will be displeased if we do not. ?]
unavailable-subtitle: We do not have enough leverage.
tags: mins_1928
choose-if: leverage >= 5
view-if: labor_minister_party != "SPD"
on-arrival: leverage -= 5; labor_minister_party = "SPD"; labor_minister = "Wissell"
go-to: ministries

Rudolf Wissell of the SPD has been appointed labor minister.

@interior
title: Interior Ministry
subtitle: Taking the Interior Ministry is expected of us. The Interior Ministry controls domestic security.
unavailable-subtitle: We do not have enough leverage.
tags: mins_1928
choose-if: leverage >= 5
view-if: interior_minister_party != "SPD"
on-arrival: leverage -= 5; interior_minister_party = "SPD"; interior_minister = "Severing"
go-to: ministries

Carl Severing of the SPD has been appointed Interior Minister.

@finance
title: Finance Ministry
subtitle: The Finance Ministry controls the government budget and taxes.
unavailable-subtitle: We do not have enough leverage.
tags: mins_1928
choose-if: leverage >= 10 
view-if: finance_minister_party != "SPD"
on-arrival: leverage -= 10; finance_minister_party = "SPD"; finance_minister = "Hilferding"
go-to: ministries

Rudolf Hilferding of the SPD has been appointed Finance Minister.

@economic
title: Economic Ministry
subtitle: The Economic Ministry controls broader economic policy.
unavailable-subtitle: We do not have enough leverage.
tags: mins_1928
choose-if: leverage >= 10 
view-if: economic_minister_party != "SPD"
on-arrival: leverage -= 10; economic_minister_party = "SPD"; economic_minister = "Schmidt"
go-to: ministries

Robert Schmidt of the SPD has been appointed Economic Minister.

@justice
title: Justice Ministry
subtitle: The Justice Ministry controls the courts.
unavailable-subtitle: We do not have enough leverage.
tags: mins_1928
choose-if: leverage >= 10 
view-if: justice_minister_party != "SPD"
on-arrival: leverage -= 10; justice_minister_party = "SPD"; justice_minister = "Radbruch"
go-to: ministries

Gustav Radbruch of the SPD has been appointed Justice Minister.

@foreign
title: Foreign Ministry
subtitle: This is usually the domain of Gustav Stresemann of the DVP.
unavailable-subtitle: We do not have enough leverage.
tags: mins_1928
choose-if: leverage >= 10 and year >= 1928
view-if: foreign_minister_party != "SPD"
on-arrival: leverage -= 10; foreign_minister_party = "SPD"; foreign_minister = "Breitscheid"
go-to: ministries if (stresemann_dead = 1 or in_weimar_coalition or in_left_front or in_popular_front); foreign_fail if stresemann_dead != 1 and not (in_weimar_coalition or in_left_front or in_popular_front)

[? if stresemann_dead = 1 or in_weimar_coalition or in_left_front or in_popular_front : Rudolf Breitscheid of the SPD has been appointed Foreign Minister. ?]

@foreign_fail
on-arrival: dvp_relation -= 5

With Gustav Stresemann as the main member of the DVP advocating a Grand Coalition with the SPD in the first place, under the expectation of being made foreign minister, our push for the ministry has caught him completely by surprise. The DVP has refused to cede the foreign ministry. 

We can call their bluff, jeopardizing the coalition [? if bourgeois_coalition >= 50 and right_coalition < 50 : and risk a bourgeois coalition forming instead ?][? if right_coalition >= 50 : and risk a right-wing coalition [? if bourgeois_coalition >= 50 : or a bourgeois coalition ?] forming instead ?], or back down.

- @call_bluff: Call their bluff.
- @scholz_chancellor: Offer to make Ernst Scholz of the DVP chancellor.
- @drop_foreign: Concede.

@call_bluff
go-to: right_coalition_2 if right_coalition >= 50 and bourgeois_coalition < 50 and dvp_relation < 55; let_it_form if bourgeois_coalition >= 50; new_elections_spd if bourgeois_coalition < 50 and right_coalition < 50 and dvp_relation < 35; ministries if (dvp_relation >= 55 and bourgeois_coalition < 50) or (dvp_relation >= 35 and bourgeois_coalition < 50 and right_coalition < 50)
on-departure: dvp_relation -= 10 if (dvp_relation >= 55 and bourgeois_coalition < 50) or (dvp_relation >= 35 and bourgeois_coalition < 50 and right_coalition < 50); coalition_dissent += 1 if (dvp_relation >= 55 and bourgeois_coalition < 50) or (dvp_relation >= 35 and bourgeois_coalition < 50 and right_coalition < 50)

[? if (dvp_relation >= 60 and bourgeois_coalition < 50) or (dvp_relation >= 40 and bourgeois_coalition < 50 and right_coalition < 50) : Rudolf Breitscheid of the SPD has been appointed Foreign Minister. ?]

@scholz_chancellor
on-arrival: dvp_relation -= 10; chancellor = "Scholz"; chancellor_party = "DVP"; left_dissent += 15; labor_dissent += 10; center_dissent += 5; workers_spd -= 4; new_middle_dvp += 2; old_middle_dvp += 3
go-to: ministries

In exchange for the Chancellory going to Ernst Scholz, we have been granted the Foreign Ministry. Our party is in upheaval over this concession.

@agriculture
title: Agricultural Ministry
subtitle: This is not our usual domain.
unavailable-subtitle: We do not have enough leverage.
choose-if: leverage >= 10 
view-if: agriculture_minister_party != "SPD"
on-arrival: leverage -= 10; agriculture_minister_party = "SPD"; agriculture_minister = "Baade"
go-to: ministries
tags: mins_1928

Fritz Baade of the SPD has been appointed Agriculture Minister.

@reichswehr
title: Reichswehr Ministry
subtitle: The Ministry responsible for the armed forces is not typically our domain; General Groener and the bourgeois-conservatives will be displeased.
unavailable-subtitle: We do not have enough leverage.
tags: mins_1928
choose-if: leverage >= 15 and year >= 1928
view-if: reichswehr_minister_party != "SPD"
on-arrival: leverage -= 15; reichswehr_minister_party = "SPD"; reichswehr_minister = "Leber"; coup_progress += 1
go-to: ministries

Julius Leber of the SPD has been appointed Reichswehr Minister.

@postal_affairs
title: Postal Affairs Ministry
view-if: fun_value >= 62 and fun_value < 70 and postal_affairs_minister_party != "SPD"
subtitle: This is not our typical domain, and it is not recommended we take this ministry unless we're fine with silly jokes.
unavailable-subtitle: We do not have enough leverage.
tags: mins_1928
choose-if: leverage >= 35 and chancellor == "Wirth"
on-arrival: leverage -= 35; postal_affairs_minister_party = "SPD"; postal_affairs_minister = "Fillak"
go-to: ministries

Tobias Fillak of the SPD has been appointed Postal Affairs Minister.



@no_more
title: No more ministries.
on-arrival: labor_dissent += 10 if historical_mode and labor_minister_party != "SPD"
subtitle: [? if leverage >= 10 : We still have some more leverage to gain ministries. ?][? if leverage < 10 : We do not have much leverage left. ?]
tags: mins_1928

This coalition arrangement is tolerable to all.

@drop_labor
title: Drop the Labor Ministry.
view-if: labor_minister_party = "SPD"
on-arrival: labor_minister_party = "Z"; labor_minister = "Stegerwald"; leverage += 5;
tags: drop_mins_1928
go-to: ministries

@drop_interior
title: Drop the Interior Ministry.
view-if: interior_minister_party = "SPD"
on-arrival: interior_minister_party = "Z"; interior_minister = "Wirth"; leverage += 5;
tags: drop_mins_1928
go-to: ministries

@drop_finance
title: Drop the Finance Ministry.
view-if: finance_minister_party = "SPD"
on-arrival: finance_minister_party = "DVP"; finance_minister = "Moldenhauer"; leverage += 10; finance_minister_party = "Z" if in_weimar_coalition or in_popular_front; finance_minister = "Köhler" if in_weimar_coalition or in_popular_front
tags: drop_mins_1928
go-to: ministries

@drop_economic
title: Drop the Economic Ministry.
view-if: economic_minister_party = "SPD"
on-arrival: economic_minister_party = "DVP"; economic_minister = "Curtius"; leverage += 10; economic_minister_party = ddp_name if in_weimar_coalition or in_popular_front; economic_minister = "Dietrich" if in_weimar_coalition or in_popular_front;
tags: drop_mins_1928
go-to: ministries

@drop_justice
title: Drop the Justice Ministry.
view-if: justice_minister_party = "SPD"
on-arrival: justice_minister_party = ddp_name; justice_minister = "Koch-Weser"; leverage += 10;
tags: drop_mins_1928
go-to: ministries

@drop_foreign
title: Drop the Foreign Ministry.
view-if: foreign_minister_party = "SPD"
on-arrival: foreign_minister_party = "DVP"; foreign_minister = "Stresemann"; foreign_minister = "Curtius" if stresemann_dead; leverage += 10; foreign_minister_party = "Z" if in_weimar_coalition or in_popular_front; foreign_minister = "Wirth" if in_weimar_coalition or in_popular_front;
tags: drop_mins_1928
go-to: ministries

@drop_agriculture
title: Drop the Agriculture Ministry.
view-if: agriculture_minister_party = "SPD"
on-arrival: agriculture_minister_party = ddp_name; agriculture_minister = "Dietrich"; leverage += 10; agriculture_minister_party = "Z" if in_weimar_coalition; agriculture_minister = "Hermes" if in_weimar_coalition
tags: drop_mins_1928
go-to: ministries

@drop_reichswehr
title: Drop the Reichswehr Ministry.
view-if: reichswehr_minister_party = "SPD"
on-arrival: reichswehr_minister_party = "I"; reichswehr_minister = "Groener"; leverage += 15; coup_progress -= 1
tags: drop_mins_1928
go-to: ministries

@drop_postal_affairs
title: Drop the Postal Affairs Ministry.
view-if: postal_affairs_minister_party == "SPD"
tags: drop_mins_1928
on-arrival: leverage += 35; postal_affairs_minister_party = "I"; postal_affairs_minister = "Nobody"
go-to: ministries