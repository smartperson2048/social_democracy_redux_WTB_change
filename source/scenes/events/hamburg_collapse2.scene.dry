title: Dissolution of the Hamburg Landtag
subtitle: The NSDAP has motioned in Hamburg to dissolve the Landtag for new elections
new-page: true
tags: event
max-visits: 1
view-if: ((in_hamburg_groko and h_grand_coalition < 50) or (in_hamburg_soclib and h_social_liberal_coalition < 50) or (in_hamburg_left and h_left_coalition < 50) or (in_spd_hamburg and spd_hl < 50)) and nsdap_votes >= nsdap_hl and unemployed >= 25 and year >= 1932 and landtags_enabled
on-arrival: dissolve_hl += dvp_hl if dvp_relation < 40; dissolve_hl += nsdap_hl; dissolve_hl += dnvp_hl; dissolve_hl += other_hl if dvp_relation < 60; dissolve_hl += z_hl if z_relation < 40; dissolve_hl += kpd_hl if ((kpd_relation < 60 and kpd_party_leader != "Conciliators") or (kpd_relation < 30 and kpd_party_leader == "Conciliators"))
go-to: landtag_dissolved if dissolve_hl >= 50; landtag_not_dissolved if dissolve_hl < 50

= Dissolution of the Hamburg Landtag

Following the collapse of our national cooperation and further declining relations, the Hamburg DVP has turned on our regional Grand Coalition. Though they cannot remove the state government from power, due to the constructive VONC mechanism, they can dissolve the Landtag by a simple majority vote.

@landtag_dissolved
on-arrival: nazi_urgency += 1; hamburg_nsdap += 0.05; hamburg_dvp -= 0.05; hamburg_dnvp -= 0.05; hamburg_other -= 0.05; hamburg_election_year = year; hamburg_election_month = month+3; hamburg_election_year += 1 if hamburg_election_month > 12; hamburg_election_month -= 12 if hamburg_election_month > 12
go-to: kpd_responsible if (((kpd_relation < 60 and kpd_party_leader != "Conciliators") or (kpd_relation < 30 and kpd_party_leader == "Conciliators")) and (dissolve_hl-kpd_hl < 50))

Ultimately, with [+ dissolve_hl +]% in favor, the Landtag is dissolved. [? if not ((kpd_relation < 70 and kpd_party_leader != "Conciliators") or (kpd_relation < 50 and kpd_party_leader == "Conciliators")) : This is due to the KPD's crucial vote to dissolve the Landtag. ?]

New elections have been called for [+ hamburg_election_month : month +] [+ hamburg_election_year +].

@kpd_responsible
on-arrival: hamburg_kpd -= 0.15; hamburg_spd += 0.1

The KPD's vote to dissolve the Landtag seems to have backfired, as many of their voters, feeling that they are voting not for communism but for the Nazism that the KPD is enabling in their virulent focus on attacking Social Democracy, have jumped ship to the SPD.

@landtag_not_dissolved

Surprisingly, the motion fails, with only [+ dissolve_hl +]% in favor. [? if ((kpd_relation < 70 and kpd_party_leader != "Conciliators") or (kpd_relation < 50 and kpd_party_leader == "Conciliators"))  : This is due to the KPD's crucial vote not to dissolve the Landtag. ?]
