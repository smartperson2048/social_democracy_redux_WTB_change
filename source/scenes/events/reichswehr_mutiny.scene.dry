title: Crisis in the Army
subtitle: The Reichswehr refuses to follow the Communists...
new-page: true
tags: event
max-visits: 1
view-if: reichswehr_minister_party == "KPD"

A crisis! With the KPD taking control over the Reichswehr Ministry in our most recent coalition, generals in the army have openly refused to cooperate. Entire barracks and garrisons have effectively risen up in defiance, while the KPD, for their part, have prepared an extensive set of reforms which would effectively dissolve the Reichswehr entirely. 

The army demands we sack the KPD Reichswehr Minister, while the Communists stand ready to crush the mutinies by force with the RFB, expecting us to support them in this endeveour by arming loyal worker's militias and deploying the police alongside. What do we do?

- @gegen_groener: We will assist the KPD in suppressing the mutiny.
- @gegen_thalmann: We have no choice but to sack the Reichswehr Minister.

@gegen_groener
on-arrival: government_strength = rfb_strength*rfb_militancy; government_strength += ((left_strength/100)*rb_strength*rb_militancy)/2; government_strength += (prussian_police_strength*prussian_police_militancy*prussian_police_loyalty); mutiny_strength = (reichswehr_strength*reichswehr_militancy*(1-(reichswehr_loyalty/2))); mutiny_strength += (sa_strength*sa_militancy) if sa_banned != 1; mutiny_strength += (sh_strength*sh_militancy) if sh_banned != 1

We choose to crush the mutiny by force and stand by our coalition partners. The Communists however worry that this will not be enough, and call for us to arm SPD and KPD aligned workers into state-sanctioned militias to help occupy rebellious barracks.

- @arm_workers: We will arm the workers.
- @the_fight: This is too far. The law can handle this.

@arm_workers
subtitle: -1 budget.
on-arrival: budget -= 1; kpd_relation += 5; government_strength += (rfb_strength * 0.1); rfb_militancy += 0.1; government_strength += (rb_strength * 0.05); rb_militancy += 0.05; government_strength += (spd_normalized + kpd_normalized)*unemployed; government_strength += ((spd_normalized + kpd_normalized)*unemployed)/2; z_relation -= 10; judicial_reform -= 1; interior_police_loyalty -= 0.05; reichswehr_loyalty -= 0.05; capital_strike_progress += 2
go-to: the_fight

@the_fight
go-to: mutiny_suppressed if government_strength >= mutiny_strength; mutiny_victorious if mutiny_strength >= government_strength

We send our forces to suppress the mutiny and a confrontation quickly erupts.

@mutiny_suppressed
on-arrival: workers_kpd += 5; unemployed_kpd += 10; reichswehr_strength = 0; rb_militancy += 0.05; rfb_militancy += 0.15; coup_progress = 0; sa_strength *= 0.9 if sa_banned != 1; sh_strength *= 0.9 if sh_banned != 1; pro_republic += 2; nationalism -= 7

The mutiny has successfully been suppressed. The officers involved are tried and punished harshly while the Communists proceed with their plans, seeing the effective dissolution of the army. Arms are redirected towards our paramilitaries, though the RFB's is the most bolstered, receiving an influx of military grade equipment.

The bourgeois parties have rallied against us, but their cries are ineffectual.

@mutiny_victorious
on-arrival: nationalism += 10; pro_republic -= 7; sa_strength += 100; sh_strength += 100; rb_strength -= 200; reichswehr_loyalty = 0; rfb_strength -= 25; reichswehr_strength *= 0.95

We send our forces against the mutinies, but, despite hard-fought victories against some garrisons, it quickly becomes clear that even with the full force of the government on our side, we have been unable to restore order. Our attempts at suppressing the Reichswehr mutinies have only fully turned it against us, and, once the initial fighting settles down, they are quick to mobilize and march on Berlin.

- @civil_war: It is a civil war, then.

@gegen_thalmann
on-arrival: reichswehr_minister_party = "I"; kpd_relation -= 15; kpd_coalition_dissent += 4; coup_progress += 2; reichswehr_loyalty -= 0.05; chancellor = "Breitscheid" if chancellor_party == "KPD"; chancellor_party = "SPD" if chancellor_party == "KPD"; left_dissent += 15; rb_strength -= 200; rfb_strength += 100

We cave to the Reichswehr's demands and sack the Reichswehr Minister. The KPD is outraged at this betrayal and plans to do what they can to collapse the coalition, while the message given to the Reichswehr is that rebelling pays off. Let's hope this was not a mistake.