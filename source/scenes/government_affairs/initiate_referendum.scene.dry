title: Direct Democracy
new-page: true
is-card: true
card-image: img/petition_referendum.jpg
view-if: referendum_timer = 0 and (spd_in_government == 0 or spd_toleration == 1)
on-arrival: referendum_timer += 6; month_actions += 1
tags: govt_affairs

= Direct Democracy

As part of the Weimar constitution, petitions, if they gain enough signatures, may be put to nationwide referendums for the people to directly vote on laws. We may not be in government, but we can try to bypass this and campaign ourselves for certain laws or actions, which, even if fail, may invigorate our supporters or provide publicity.

Should we initiate any petitions?

- @petitions: Let's initiate a petition.
- @no_petition: No, we won't initiate any petitions.
- @easy_discard

@petitions
subtitle: -1 resource.
on-arrival: resources -= 1
choose-if: resources >= 1
unavailable-subtitle: -1 resource. We don't have the resources to initiate a petition.

We have a variety of options for laws to push for via petition. What should we petition for?

- @land_reform_vote: Land reform.
- @nationalization_vote: A nationalization program.
- @wtb_vote: Implementation of the WTB plan.
- @work_council_vote: The empowerment of works councils.
- @state_buyer_vote: A state buyer program.
- @abortion_vote: The legalization of abortion.
- @constitutional_reform_vote: Constitutional reforms.

@land_reform_vote
subtitle: Call for breaking up the large aristocratic estates and giving land to landless workers.
choose-if: budget >= 1
unavailable-subtitle: The government doesn't have the budget to pursue this.
on-arrival: kpd_relation += 5; rural_spd += 3*(1-dissent); rural_kpd += 1; rural_dnvp -= 2 if year < 1931; rural_nsdap -= 1 if year >= 1931; dvp_relation -= 5; z_relation -= 4; left_strength += 6; left_dissent -= 4; ddp_support = 1 if ddp_relation >= 65; referendum_support = (spd_votes + kpd_votes + sapd_votes); referendum_support += ddp_votes if ddp_support == 1; referendum_support += ((100*(rural/(workers+unemployed+new_middle+old_middle+rural+catholics)))*((1-dissent)/2)); referendum_succeed = 1 if referendum_support >= 50; referendum_succeed = 0 if referendum_support < 50

We initiate a petition for the implementation of a land reform program and launch a campaign through the countryside to get it the signatures it needs. Many of our grassroots members in their campaigning for the petition campaign alongside the Communists, who quickly involve themselves in the referendum [? if kpd_relation < 25 : despite their hostility toward Social Democracy?]. Soon it gains enough signatures to be put to a referendum, which the DNVP aggressively campaigns against, leading to a decrease in their rural support as previously reactionary farmers turn against the party.

- @land_reform_success: The referendum succeeds.
- @land_reform_fail: The referendum fails.

@land_reform_success
view-if: referendum_succeed
on-arrival: budget -= 1; land_reform += 1; coalition_dissent += 1; rural_spd += 2*(1-dissent); rural_nsdap -= 3*(1-dissent); unemployed -= 1 if unemployed >= 5; left_strength += 2; left_dissent -= 1; rural_sol += 3; rural_policy += 1; coup_progress += 1

The referendum has succeeded with [+ referendum_support +]% of the vote! The influence of reactionaries in the rural regions of our country has diminished as the estates of junkers are dismantled and redistributed, though quite reluctantly and shoddily by [+ agricultural_minister +], the agricultural minister.

@land_reform_fail
view-if: not referendum_succeed

Unfortunately, the referendum has failed with only [+ referendum_support +]% of the vote. Nevertheless, our advocacy of land reform through the referendum has improved our ties with the KPD and our standing amongst rural regions.

@nationalization_vote
view-if: (nationalization_adopted or socializations >= 2) and unemployed >= 20
subtitle: Call for mass nationalizations and the transformation of the economy.
choose-if: budget >= (nationalize_budget/2)
unavailable-subtitle: The government doesn't have the budget to pursue this.
on-arrival: kpd_relation += 10; nationalization_support += 2 if nationalization_adopted == 0; workers_spd += 3*(1-dissent); new_middle_spd -= 2; old_middle_spd -= 2; workers_spd += (works_councils*(1-dissent)) if works_councils >= 2; workers_kpd += 1; workers_kpd += 1 if sapd_consolidated == 0; workers_sapd += 1 if sapd_consolidated == 1; unemployed_spd += 2*(1-dissent); unemployed_kpd += 1; dvp_relation -= 8; z_relation -= 6; ddp_relation -= 3; reformist_dissent += 2; left_dissent -= 8; left_strength += 5; left_strength += 5 if left_split == 0; referendum_support = (spd_votes + kpd_votes + sapd_votes); referendum_support -= (spd_votes*dissent); referendum_succeed = 1 if referendum_support >= 50; referendum_succeed = 0 if referendum_support < 50

We initiate a petition for the nationalization of large idle industry by the government and its bringing under the control of [? if works_councils >= 2 : national planning boards made up of works council and state representatives ?][? if works_councils < 2 : the government ?] in order to fix the economy. We launch a campaign for this petition through the industrial regions of Germany, from workplace to workplace [? if works_councils >= 2 : and works council to works council ?] as soon the KPD joins in our campaign. The petition quickly gains the required signatures and is put to a nationwide referendum.

- @nationalization_success: The referendum succeeds.
- @nationalization_fail: The referendum fails.

@nationalization_success
view-if: referendum_succeed
on-arrival: nationalization_progress += 0.5; capital_strike_progress += 1; unemployed -= 1 if unemployed >= 6; old_middle_spd -= 1; unemployed -= 0.5 if works_councils >= 3; workers += 1; budget -= 2; works_program += 1; workers_spd += 3*(1-dissent); unemployed_spd += 2*(1-dissent); inflation += 0.8

The referendum has succeeded with [+ referendum_support +]% of the vote! The nationalization program is implemented, though very reluctantly and shoddily by the Economic Minister, [+ economic_minister +], leaving the economy not nearly as repaired as it could otherwise have been. Though, as we are not in government, this still amounts to a victory for Social Democracy, which has managed to increase its publicity amongst the working class.

@nationalization_fail
view-if: not referendum_succeed

Unfortunately, the referendum has failed with only [+ referendum_support +]% of the vote. Nevertheless, our advocacy of nationalization through the referendum has improved our standing amongst the workers.

@work_council_vote
view-if: works_councils < 3
subtitle: Call for the expansion and empowering of works councils to further economic democracy.
on-arrival: workers_spd += 3*(1-dissent); workers_kpd += 1 if sapd_consolidated == 0; workers_sapd += 1 if sapd_consolidated == 1; workers_z += 1 if z_relation >= 60 and z_party_leader == "Joos"; labor_dissent -= 4; labor_strength += 6; dvp_relation -= 5; referendum_support = (spd_votes + kpd_votes + sapd_votes); referendum_support += (workers_z_normalized*(workers/(workers+unemployed+new_middle+old_middle+rural+catholics))) if z_party_leader != "Joos" or z_relation < 60; referendum_support += z_votes if z_relation >= 60 and z_party_leader == "Joos"; referendum_support += ddp_votes if works_council < 1 and ddp_relation >= 50; referendum_support += ddp_votes if works_council < 2 and works_council > 1 and ddp_relation >= 60; referendum_support += ddp_votes if works_council >= 2 and ddp_relation >= 75; union_council_factor = 100*(((labor_strength/(left_strength+center_strength+labor_strength+reformist_strength+neorevisionist_strength))*workers)/(workers+unemployed+new_middle+old_middle+rural+catholics)); referendum_support += union_council_factor/(works_councils+1); referendum_succeed = 1 if referendum_support >= 50; referendum_succeed = 0 if referendum_support < 50

We initiate a petition for the expansion of works councils in line with our previous policies of economic democracy. We rally the workers in favor [? if z_party_leader == "Joos" and z_relation >= 60 : with the help of Joseph Joos in the Center Party ?], and quickly the petition gains enough signatures to be put to a referendum.

- @work_council_success: The referendum succeeds.
- @work_council_fail: The referendum fails.

@work_council_success
view-if: referendum_succeed
on-arrival: economic_democracy += 1; workers_spd += 2*(1-dissent); capital_strike_progress += 1; works_councils += 1; labor_dissent -= 2; labor_strength += 2

The referendum has succeeded with [+ referendum_support +]% of the vote! [? if works_councils == 1 : Now, Germany will have worker representation on the boards of companies, giving them more influence in management decisions. ?][? if works_councils == 2 : National and sectorial workers councils have been part of the Weimar Constitution, but they were disempowered in the past, acting as nothing more than advisory bodies. With the success of this referendum, we can change that. ?][? if works_councils == 3 : Sectorial works councils can now negotiate wage and price policies for entire economic sectors, helping to both give workers more of a voice, and to rationalize the economy, smoothing over the anarchy of the market.?]

@work_council_fail
view-if: not referendum_succeed

Unfortunately, the referendum has failed with only [+ referendum_support +]% of the vote. Nevertheless, our ties with labor have strengthened, as has our position amongst the workers, thanks to our attempts to empower them.

@state_buyer_vote
subtitle: Call for the creation of a state monopoly for grain purchases, guaranteeing prices for farmers and stable prices for consumers.
choose-if: budget >= 2
unavailable-subtitle: The government doesn't have the budget to pursue this.
on-arrival: rural_spd += 1*(1-dissent); rural_nsdap -= 1*(1-dissent); workers_spd += 2*(1-dissent); unemployed_spd += 3*(1-dissent); unemployed_kpd += 1; dvp_relation -= 5; z_relation -= 3; peoples_party_support += 1; referendum_support = (spd_votes + sapd_votes + (0.8*kpd_votes)); referendum_support += ddp_votes if ddp_relation >= 75; referendum_succeed = 1 if referendum_support >= 50; referendum_succeed = 0 if referendum_support < 50

We initiate a petition for the implementation of a state buyer program, campaigning for it in both the countryside for farmers and amongst the workers. The KPD joins in campaigning for the petition, though not significantly, and it soon gains the signatures it needs for a referendum.

- @state_buyer_success: The referendum succeeds.
- @state_buyer_fail: The referendum fails.

@state_buyer_success
view-if: referendum_succeed
on-arrival: budget -= 2; state_buyer = 1; rural_spd += 2*(1-dissent); rural_nsdap -= 1*(1-dissent); workers_spd += 1*(1-dissent); unemployed_spd += 1*(1-dissent); rural_policy += 2; capital_strike_progress += 1

The referendum has succeeded with [+ referendum_support +]% of the vote! A state buyer program is initialized, though with reluctance from the Economic Minister, [+ economic_minister +]. Let us hope that the farmers and workers remember that it is us who campaigned for this stabilization of prices and the market.

@state_buyer_fail
view-if: not referendum_succeed

Unfortunately, the referendum has failed with [+ referendum_support +]% of the vote. Nevertheless, our standing amongst workers and farmers alike has improved somewhat.

@wtb_vote
view-if: wtb_adopted == 1
subtitle: Call for the implementation of the WTB plan.
on-arrival: wtb_budget = 4; wtb_budget -= 1 if works_councils >= 2 or socializations >= 1 or pro_labor >= 2; workers_spd += 5*(1-dissent); workers_spd += works_councils*(1-dissent); unemployed_spd += 5*(1-dissent); workers_nsdap -= 2*(1-dissent); labor_strength += 4; labor_dissent -= 2; unemployed_nsdap -= 3*(1-dissent); new_middle_spd += 4*(1-dissent); old_middle_spd += 2*(1-dissent); rural_spd += 2*(1-dissent); dvp_relation -= 3; kpd_relation -= 2; referendum_support = spd_votes; referendum_support += ddp_votes if ddp_relation >= 75; referendum_support += (workers_z_normalized*(workers/(workers+unemployed+new_middle+old_middle+rural+catholics))) if z_relation >= 65; referendum_support += (workers_nsdap_normalized*(workers/(workers+unemployed+new_middle+old_middle+rural+catholics))); referendum_support += (unemployed_nsdap_normalized*(unemployed/(workers+unemployed+new_middle+old_middle+rural+catholics))); union_council_factor = 100*((workers*((labor_strength*works_councils)/((labor_strength*works_councils)+center_strength+reformist_strength+left_strength+neorevisonist_strength))/(workers+unemployed+new_middle+old_middle+rural+catholics))); referendum_support += union_council_factor*(1-dissent); referendum_succeed = 1 if referendum_support >= 50; referendum_succeed = 0 if referendum_support < 50

We initiate a petition for the implementation of Phase 1 of the WTB plan, rallying behind it. The ADGB spearheads our efforts [? if works_council >= 1 : and helps us organize in support of the petition through the works councils?], and the petition quickly gains the necessary signatures. Many of the NSDAP's working class supporters also support the petition, helping split some support off of the party.

- @wtb_vote_success_surplus: The referendum succeeds.
- @wtb_vote_success_deficit: The referendum succeeds.
- @wtb_vote_failure: The referendum fails.

@wtb_vote_success_surplus
view-if: referendum_succeed and budget >= wtb_budget
on-arrival: budget -= wtb_budget; works_program += 1; unemployed -= 3; workers_spd += 5*(1-dissent); unemployed_spd += 5*(1-dissent); wtb_implemented += 1; inflation += 3

The referendum has succeeded with [+ referendum_support +]% of the vote! Money is beginning to flow through the economy via investments in large-scale public works, though the reluctant Economic Minister, [+ economic_minister +], is executing the plan less efficiently than we would be able to. Nevertheless, the economy seems to improve, and we claim credit.

@wtb_vote_success_deficit
view-if: referendum_succeed and budget < wtb_budget
on-arrival: works_program += 1; budget -= wtb_budget; unemployed -= 3; workers += 2; workers_spd += 4*(1-dissent); unemployed_spd += 4*(1-dissent); capital_strike_progress += 1; wtb_implemented += 1; inflation += 4; welfare -= 2 if not spd_toleration; inflation -= 3 if not spd_toleration; unemployed += 2 if not spd_toleration; budget += 2 if not spd_toleration; 

The referendum has succeeded with [+ referendum_support +]% of the vote! Money is beginning to flow through the economy via investments in large-scale public works, though a deficit manifests. The government plans to handle this deficit by, as always, cutting welfare, and they blame us for creating the necessity to do so. Of course, this argument is ridiculous, but it does dampen our celebrations. 

[? if spd_toleration : We can vote against the planned austerity measures and effectively bring down the government, though this will mean snap elections. ?][? if not spd_toleration : Unfortunately, there is nothing we can do to stop these austerity measures from being implemented. ?]

- @break_toleration: We will bring the government down.
- @maintain_toleration: We cannot afford to give the far-right a chance to gain power.

@break_toleration
view-if: spd_toleration
subtitle: New elections will be held.
on-arrival: time_to_election = 3; spd_in_government = 0; in_grand_coalition = 0; spd_toleration = 0; pro_republic -= 5
call: set_next_election_time

We will not tolerate austerity done to spite us and the WTB plan's victory in the referendum. We have brought down the [+ chancellor +] government, and called for new elections, set for [+ next_election_month : month +] [+ next_election_year +]. 

@maintain_toleration
view-if: spd_toleration
subtitle: This may reflect badly.
on-arrival: welfare -= 2; inflation -= 3; unemployed += 2; budget += 2; workers_spd -= 5; unemployed_spd -= 5; workers_kpd += 1 if sapd_consolidated == 0; workers_sapd += 1 if sapd_consolidated == 1; left_dissent += 10; labor_dissent += 10; kpd_relation -= 5

We do not take a stand against the government's new austerity measures, significantly reducing the effects of our victory in the WTB referendum. We argue that this is necessary to maintain a functional democratic government that can fix the economy, but it falls on many deaf ears.

@wtb_vote_failure
view-if: not referendum_succeed

Unfortunately, the referendum has failed to pass with only [+ referendum_support +]% of the vote. Nevertheless, our ties with the ADGB have been strengthened, as well as our standing amongst the working and even employing classes, while many former Nazi supporters have been introduced to Social Democracy through our advocacy of the WTB plan.

@abortion_vote
subtitle: We will push for the relaxation of restrictions on abortion.
on-arrival: z_relation -= 5; workers_spd += 3*(1-dissent); catholics_spd -= 5; new_middle_ddp += 4; new_middle_spd -= 3; old_middle_ddp += 3; old_middle_spd -= 2; old_middle_nsdap -= 1; rural_nsdap -= 1; rural_dnvp += 1; old_middle_dnvp += 1; referendum_support = (spd_votes + ddp_votes + sapd_votes + kpd_votes); referendum_support -= (catholics_spd_normalized*(catholics/(catholics+workers+unemployed+old_middle+new_middle+rural))); referendum_succeed = 1 if referendum_support >= 50; referendum_succeed = 0 if referendum_support < 50

We initiate a petition for the legalization of abortion, which quickly whips up a storm. Of course, the progressive parties all throw their support behind the petition, but the Center Party and the DNVP both staunchly oppose it. Whether the controversy helps the SPD or not is uncertain, but it certainly helps the more minor progressive parties, who use it as an opportunity to gain publicity, one such example being the DDP which has grown its reputation amongst the middle class significantly.

- @abortion_success: The referendum succeeds.
- @abortion_fail: The referendum fails.

@abortion_success
view-if: referendum_succeed
on-arrival: abortion_rights += 1; womens_rights += 1; z_relation -= 3; catholics_spd -= 5; dvp_relation -= 5; new_middle_spd += 2*(1-dissent); new_middle_ddp += 1; workers_z += 3; new_middle_z += 3; old_middle_z += 3; old_middle_dnvp += 2; rural_dnvp += 2; workers_nsdap -= 3; new_middle_nsdap -= 3; old_middle_nsdap -= 1; rural_nsdap -= 1; unemployed_nsdap -= 2; unemployed_z += 3; unemployed_spd += 3*(1-dissent)

The referendum succeeds with [+ referendum_support +]% of the vote! This has heartened our more progressive supporters, while hurting our relations with the Center Party. With the increasing political discourse around abortion, the religiously-oriented Center Party and DNVP have gained conservative votes at the expense of the NSDAP.

@abortion_fail
view-if: not referendum_succeed

Unfortunately, the referendum has failed with only [+ referendum_support +]% of the vote. This has disheartened the progressives who spearheaded the petition, who have decided to bide their time.

@constitutional_reform_vote
view-if: constitutional_reform_timer = 0 and neorevisionism and constitutional_reform < 3
choose-if: judicial_reform >= 4
unavailable-subtitle: Pushing through constitutional reforms is unfeasable so long as the judiciary remains reactionary.
go-to: constitutional_reform.reforms_menu

@no_petition

We will not attempt any referendums right now.